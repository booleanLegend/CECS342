"The Absence of Law" by Brian Rushton

The story genre is "Science Fiction". The story description is "A remote surveillance science fiction comedy." The story creation year is 2016. The release number is 3.

[The Sound code has been disabled here by setting SoundOn to 0, because it requires hand-editing of the javascript files. The uuid is 6044ce4b-8aa6-4cbe-babc-bb339348ec5c]	

Include Basic Screen Effects by Emily Short.

Include Glulx Text Effects by Emily Short.

Use MAX_PROP_TABLE_SIZE of 500000.

Release along with the source text.

Part 1 - Setup

SoundOn is a number that varies. SoundOn is 0.

Whatforming is an action applying to one thing. Understand "whatform [something]" as whatforming.

Carry out whatforming:
	say "There are [number of things formed by the noun] things formed by the noun. Of those, [number of oncesaid things formed by the noun] are oncesaid."

A thing can be examined or unexamined.
A thing is usually unexamined.
Report examining something: now the noun is examined.

Does the player mean doing something with an unexamined thing:
	it is unlikely;

Understand "zoom [something]" as examining.

FinalCounter is a number that varies. FinalCounter is 0.

After reading a command:
	let T be "[the player's command]";
	replace the regular expression "(?i)(Dr|Mr|Ms|Mrs)\." in T with "\1";
	change the text of the player's command to T.

To update/redraw the/-- status line: (- DrawStatusLine(); -).


Chapter 1 - When play begins

The player is Hacker;

DaedalOS is a part of Hacker. Understand "you" or "computer" or "OS" or "AI" as DaedalOS. The description of DaedalOS is "I am DaedalOS, version 2.376. I am an artificial intelligence that you, Dr. Law, developed from open source code. For more information, type ABT."


[When play begins: 
	say "Thank you for volunteering again for the Society of Benevolent Hackers. Your skill in deactivating the Russian-controlled ice maker was impeccable, and, most importantly, you were unnoticed, unseen, and untraced. Now that you've proven your skills, we have another project for you.

There is an unsecured laptop at a nuclear-enabled facility that we have recently located. Initial data suggests that the facility is wired to explode within the next few hours. It seems to be running a homebrew operating system called DaedalOS.

We need you to log on to DaedalOS, and cancel the nuclear countdown. You have approximately two hours.

Do not let anyone know that you are there. You are solely responsible for any legal repercussions from the use of our tools, up to and including fines, imprisonment, death or dismemberment.  Good luck.

Press any key to proceed.";
	wait for any key;
	say "[line break]Connecting...

Uploading HACKER.dat... done[line break]
5 files copied

Searching for access...

**Device identified:** [line break]
Type: Personal laptop[line break]
Registration name: drlaw79[line break]
License type: Commercial[line break]
Password: successfully extracted

To log in to this laptop remotely with these credentials, press any key.";
	wait for any key;]

Understand "* [text]" as a mistake ("Noted.").
	
When play begins:
	say "[special-style-1]Hello? Can you read this? Yes, you, running this program. Excellent, I seem to be getting through!

I died, recently, and I'd prefer not to stay that way. I have two hours of backup battery life left, if I hibernate. Can you help me?  I run a lab with people who can restore life, but they don't know that I'm dead, and none of their resurrection projects are ready yet. 

You can help them. They must complete their projects before my backup battery dies, or everything that's left of me will be gone. I'll log you into the server right now.

Thank you! Oh, and before I forget: 

My name is Dr. Law.[paragraph break]";
	wait for any key;
	say "P.S. My OS is a little...unusual. Please be patient with him. He'll probably think you are me. Logging you on now...";
	wait for any key;

Rule for printing the banner text: say "Remote access enabled...[roman type]

Starting DaedalOS 2.5[line break]Release 1 / Serial number 170102 / Inform 7 build 6M62 (I6/v6.33 lib 6/12N) SD[line break]The Absence of Law by Brian Rushton[line break]Quixe interpreter by Andrew Plotkin[paragraph break]" instead.

Chapter 2 - Speech and recording and music

MusicOn is a number that varies. MusicOn is 0.

To turn on the music:
	say "[one of]Dr. Law, I have accessed your device's internet connection to locate various songs which I will play for you according to your mood and situation[or]Music on, Dr. Law[stopping]. Press any key.";
	now the left hand status line is "Music Enabled";
	update the status line;
	wait for any key;
	now the left hand status line is "[the player's surroundings]";
	now MusicOn is 1;
	
To turn off the music:
	say "Music off, Dr. Law. Press any key.";
	now the left hand status line is "Music Off";
	update the status line;
	wait for any key;
	now the left hand status line is "[the player's surroundings]";
	now MusicOn is 0;

MusicOning is an action applying to nothing. Understand "sou" as MusicOning.

Carry out MusicOning:
	if MusicOn is 0:
		turn on the music;
	otherwise:
		turn off the music;

Section 1 - Basic quip rules

A thing can be physical or not physical. A thing is usually physical.

A quip is a kind of thing.  A quip is never physical. A quip has some text called the preview. The preview of a quip is usually "Insert preview here.". 

A quip can be GingerGiven or MeihGinger. A quip is usually MeihGinger. 

A quip has some text called GingerResponse. The GingerResponse of a quip is usually "Ginger doesn't seem interested in that.".

Rule for deciding whether all includes quips: it does not.

Instead of inserting a quip into a container:
	say "That's not a physical object.";

Instead of putting a quip on a supporter:
	say "That's not a physical object.";

Section 2 - Quip dynamics

A quip can be phrased or phull or Englisch. A quip is usually Englisch.

After printing the name of a quip (called currentquip) when topicing:
	if currentquip is firsthalfed:
		if the number of oncesaid quips formed by currentquip > 3:
			Say " - '[the preview of currentquip]' ([secondhint of currentquip])";
		otherwise if the number of oncesaid quips formed by currentquip > 1:
			Say " - '[the preview of currentquip]' ([firsthint of currentquip])";
		otherwise:
			Say " - '[the preview of currentquip]'";
	otherwise if currentquip is secondhalfed:
		if the number of oncesaid quips formed by the currentquip > 2:
			Say " - '[the preview of currentquip]' ([firsthint of currentquip])";
		otherwise:
			Say " - '[the preview of currentquip]'";
	otherwise:
		Say " - '[the preview of currentquip]'".

Instead of examining a quip (called currentquip):
	say "This is a recording."	

To deliver (CurrentQuip - a quip):
	if CurrentQuip is carried by the player:
		do nothing;
	otherwise if CurrentQuip is winner:
		say "[bracket]New recording - [CurrentQuip][close bracket][line break]";[weird hack here]
		now CurrentQuip is carried by the player;
	otherwise if currentquip is Englisch:
		say "[line break][bracket]New recording - [CurrentQuip][close bracket][line break]";
		now CurrentQuip is carried by the player;
	otherwise:
		say "[line break][bracket]New phrase - [CurrentQuip][close bracket][line break]";
		now CurrentQuip is carried by the player.

QuipExplain is a scene. QuipExplain begins when a quip is carried by the player.

When QuipExplain begins:
	say "[bold type]-New command-[roman type][line break] rec - list recordings[line break]";	

[the above code is to deal with a weird whitespace issue that creeps up depending on where the player first hears a quip]

Speeching it to is an action applying to two things. Understand "say [something] to [someone]" or "tell [something] to [someone]" or "t [something] to [someone]" or "a [something] to [someone]" as speeching it to.

Singlespeeching is an action applying to one thing. Understand "say [something]" as singlespeeching.

Carry out singlespeeching:
	repeat with current running through people enclosed by the location of the player:
		if current is not the player:
			try speeching the noun to current;
			break;

Keywording is an action applying to one thing. Understand "[something]" as keywording.

Carry out keywording:
	if the noun is not a quip:
		say "You need to supply a verb.";
	otherwise:
		if the number of people enclosed by the location of the player is at least two:
			try singlespeeching the noun;
		otherwise:
			say "[one of]Some[or]The[stopping] hidden speakers play, saying: '[preview of the noun]' 

But there is no one to talk to here.";

Doublekeywording is an action applying to two things. Understand "[something] [something]" as doublekeywording.

Carry out doublekeywording:
	try linking the noun with the second noun;

Carry out Speeching it to:
	if the noun is not a quip:
		say "You can only play one of your currently available recordings.";
	otherwise if the second noun is not a person:
		say "You can only do that to something animate.";
	otherwise:
		say "The hidden speakers say, '[preview of the noun]'[paragraph break]";

[		if the second noun is Ginger:
			if the noun is MeihGinger:
				say GingerResponse of noun;
				say "[line break]"; 
				now the noun is GingerGiven;
			otherwise:
				say "You've already discussed this with Ginger.";]


QuerySmalling is an action applying to two things. Understand "ask [something] to [someone]" as querysmalling.

Carry out QuerySmalling:
	try speeching the noun to the second noun. 

Querying it to is an action applying to two things. Understand "ask [someone] about [something]" as querying it to.

Carry out querying:
	try speeching the second noun to the noun;

Talking to is an action applying to one thing. Understand "talk to [someone]" as talking to.

Carry out talking to:
	say "You must choose one of your recordings to play, Dr. Law."

After speeching a quip to Ginger:
	say "Ginger doesn't seem to have heard, Dr. Law. Her congenital hearing loss returned when she was de-aged."

After speeching a quip to Ng:
	say "Dr. Ng can't hear you, Dr. Law. She's sleeping."
	
After speeching a quip to SinghClone:
	say "The clone does not have enough time to react to your comments."

After speeching a quip to Doctor Singh:
	say "Dr. Singh has his headphones in."
	
After speeching a quip to Sphinx:
	say "Sphinx pays you no attention, Dr. Law."
	
After speeching a quip to Fred:
	if the noun is not Love:
		say "Fred ignores you.".

After speeching a quip to Spiel clone:
	if the quip is not Yuck:
		if the quip is not Repeat:
			if the quip is not Come:
				say "The clone seems confused, Dr. Law.";

Linking it with is an action applying to two visible things. Understand "link [something] with [something]" or "link [something] to [something]" as linking it with.

Thoughtfulhelp is a number that varies. Thoughtfulhelp is 0.

Hello World is a quip.

OutputQuip is a quip that varies. OutputQuip is Hello World.

TempFirstQuip is a quip that varies. TempSecondQuip is a quip that varies. [used only for first time]

AllSentences is a number that varies. AllSentences is 0;

Carry out linking something (called firstquip) with something (called secondquip):
	now OutputQuip is Hello World;
	if the player is not in MoiraiArea:
		if firstquip is not a firsthalfed phrased quip:
			say "I need a verb to work with, Dr. Law. My limited intelligence only sees two nouns.";
		otherwise if secondquip is not a firsthalfed phrased quip:
			say "I need a verb to work with, Dr. Law. My limited intelligence only sees two nouns.";
		otherwise:
			say "Computing... understanding LINK as the protagonist in a popular series of games... I'm sorry, Dr. Law, but to go any further would be a copyright violation.";
	otherwise if firstquip is not a firsthalfed phrased quip:
		say "I can only combine opening fragments to closing fragments, Dr. Law, and the first thing you typed was not an opening fragment.";
	otherwise if secondquip is not a secondhalfed phrased quip:
		say "I can only combine opening fragments to closing fragments, Dr. Law, and the second thing you typed was not a closing fragment.";
	otherwise if firstquip is Vyn:
		if secondquip is Gogo:
			now OutputQuip is VynGogo;
		otherwise if secondquip is Syk:
			now OutputQuip is VynSyk;
		otherwise if secondquip is Gym:
			now OutputQuip is VynGym;
		otherwise if secondquip is Bou:
			now OutputQuip is VynBou;
		otherwise if secondquip is Dagei:
			now OutputQuip is VynDagei;
		otherwise if secondquip is Ly:
			now OutputQuip is VynLy;
		otherwise if secondquip is Cha:
			now OutputQuip is VynCha;
		otherwise if secondquip is wo:
			now OutputQuip is VynWo;
		otherwise if secondquip is Lydo:
			now OutputQuip is VynLydo;
		otherwise:
			say "My analysis suggests that this combination will not be useful, Dr. Law.";
	otherwise if firstquip is Laly:
		if secondquip is Gogo:
			now OutputQuip is LalyGogo;
		otherwise if secondquip is Syk:
			now OutputQuip is LalySyk;
		otherwise if secondquip is Gym:
			now OutputQuip is LalyGym;
		otherwise if secondquip is Bou:
			now OutputQuip is LalyBou;
		otherwise if secondquip is Dagei:
			now OutputQuip is LalyDagei;
		otherwise if secondquip is Ly:
			now OutputQuip is LalyLy;
		otherwise if secondquip is Cha:
			now OutputQuip is LalyCha;
		otherwise if secondquip is wo:
			now OutputQuip is LalyWo;
		otherwise if secondquip is Lydo:
			say "My analysis suggests that this combination will not be useful, Dr. Law.";
		otherwise:
			say "My analysis suggests that this combination will not be useful, Dr. Law.";
	otherwise if firstquip is Myt:
		if secondquip is Gogo:
			now OutputQuip is MytGogo;
		otherwise if secondquip is Syk:
			now OutputQuip is MytSyk;
		otherwise if secondquip is Gym:
			now OutputQuip is MytGym;
		otherwise if secondquip is Bou:
			now OutputQuip is MytBou;
		otherwise if secondquip is Dagei:
			now OutputQuip is MytDagei;
		otherwise if secondquip is Ly:
			now OutputQuip is MytLy;
		otherwise if secondquip is Cha:
			now OutputQuip is MytCha;
		otherwise if secondquip is wo:
			now OutputQuip is MytWo;
		otherwise if secondquip is Lydo:
			say "My analysis suggests that this combination, though grammatically correct, means nothing.";
		otherwise:
			say "My analysis suggests that this combination will not be useful, Dr. Law.";
	otherwise if firstquip is Ling:
		if secondquip is Gogo:
			now OutputQuip is LingGogo;
		otherwise if secondquip is Syk:
			now OutputQuip is LingSyk;
		otherwise if secondquip is Gym:
			now OutputQuip is LingGym;
		otherwise if secondquip is Bou:
			now OutputQuip is LingBou;
		otherwise if secondquip is Dagei:
			now OutputQuip is LingDagei;
		otherwise if secondquip is Ly:
			now OutputQuip is LingLy;
		otherwise if secondquip is Cha:
			now OutputQuip is LingCha;
		otherwise if secondquip is wo:
			now OutputQuip is LingWo;
		otherwise if secondquip is Lydo:
			say "My analysis suggests that this combination is not grammatical, Dr. Law.";
		otherwise:
			say "My analysis suggests that this combination will not be useful, Dr. Law.";
	otherwise if firstquip is secondhalfed:
		if secondquip is firsthalfed:
			try linking secondquip with firstquip;
	otherwise:
		say "My analysis suggests that this combination will not be useful, Dr. Law.";
	if OutputQuip is not Hello World:
		if outputquip is not carried by the player:
			say "Recombination successful, Dr. Law. The sentence you've created is '[preview of OutputQuip]'[line break]";
			deliver OutputQuip;
			now AllSentences is AllSentences plus one;
			now thoughtfulhelp is 1;
			now TempFirstQuip is firstquip;
			now TempSecondQuip is secondquip;
		otherwise:
			try singlespeeching outputquip;
		
LinkingHelpTime is a scene. LinkingHelpTime begins when ThoughtfulHelp is 1.

To say capitalfirst:
	say "[TempFirstQuip]" in upper case;
	
To say capitalsecond:
	say "[TempSecondQuip]" in upper case;

[When LinkingHelpTime begins:
	say "[italic type]Dr. Law, you may have to combine many sentences. To save time, you can just type the names of the two phrases you want to combine; for instance, just now you could have typed [capitalfirst] [capitalsecond].[roman type][line break]"]

Before speeching a phrased quip to someone:
	say "That's only part of a sentence, Dr. Law. You'll need to combine it with something else. Type REC to see your recordings." instead;

A quip can be firsthalfed or secondhalfed.

Section 3 -Inventory

Understand "C" or "com" or "comm" or "commands" or "command" as taking inventory.

To say CommandListHint:
	say "[first time][paragraph break][bracket]You can review available commands at any time by typing COMMANDS, or just C.[close bracket][only]"

GoodCommands is a list of text that varies.  GoodCommands is {"sou - toggle music", "aud - enable audiovisual", "help - access help information",
"abt - information on DaedalOS"};

DirectionCounter is a number that varies. DirectionCounter is 0;

To update GoodCommands:
	remove "tur- Turn fountain right" from GoodCommands, if present;
	remove "rot - Turn fountain left" from GoodCommands, if present;
	remove "cur- Raise and lower curtain" from GoodCommands, if present;
	if the player is in MOIRAI Lobby:
		if the fountain is examined:
			add "tur- Turn fountain right" to GoodCommands, if absent;
			add "rot - Turn fountain left" to GoodCommands, if absent;
		if the thick curtains are examined:
			add "cur- Raise and lower curtain" to GoodCommands, if absent;
	remove "ene - Energize the rejuvenator" from GoodCommands, if present;
	if the player is in MOIRAI Laboratory:
		if MOIRAI Laboratory is lighted:
			if the rejuvenator is not energized:
				add "ene - Energize the rejuvenator" to GoodCommands, if absent;
	remove "new - De-age the object on the rejuvenator" from GoodCommands, if present;
	remove "old - Age the object on the rejuvenator" from GoodCommands, if present;
	if the player is in MOIRAI Laboratory:
		if the rejuvenator is in MOIRAI Laboratory:
			if the rejuvenator is energized:
				add "new - De-age the object on the rejuvenator" to GoodCommands, if absent;
				add "old - Age the object on the rejuvenator" to GoodCommands, if absent;
	remove "link [bracket]a recording[close bracket] to [bracket]another recording[close bracket] - combine two halves of a sentence." from GoodCommands, if present;
	if a phrased quip is carried by the player:
		if the player is in MoiraiArea:
			add "link [bracket]a recording[close bracket] to [bracket]another recording[close bracket] - combine two halves of a sentence." to GoodCommands, if absent;
	remove "ala - turn alarm on and off" from GoodCommands, if present;
	remove "por - turn TV on and off" from GoodCommands, if present;
	if the player is in Moirai Living Quarters:
		if Fred's computer is examined:
			add "ala - turn alarm on and off" to GoodCommands, if absent;
		if the porthole TV is examined:
			add "por - turn TV on and off" to GoodCommands, if absent;
	remove "clo - Control Singh's clone" from GoodCommands, if present;
	remove "n,e,s,w - movement" from GoodCommands, if present;
	remove "take- pick up an object" from GoodCommands, if present;
	remove "rem- attempt to use remote" from GoodCommands, if present;
	remove "dis- disconnect from the clone" from GoodCommands, if present;
	if the player is in HYDRA Laboratory:
		if SinghEnabled is 1:
			add "clo - Control Singh's clone" to GoodCommands, if absent;
	otherwise if the player is SinghClonePC:
		add "n,e,s,w - movement" to GoodCommands, if absent;
		add "take- pick up an object" to GoodCommands, if absent;
		add "rem- attempt to use remote" to GoodCommands, if absent;
		add "dis- disconnect from the clone" to GoodCommands, if absent;
	remove "tel - Turn TV on and off" from GoodCommands, if present;
	if the player is in HYDRA Lobby:
		if the fireplace is examined:
			add "tel - Turn TV on and off" to GoodCommands, if absent;
	remove "spe - Turn speaker on and off" from GoodCommands, if present;
	if the player is in HYDRA Lobby:
		if the speaker is examined:
			add "spe - Turn speaker on and off" to GoodCommands, if absent;
	remove "dra - Raise or lower drapes" from GoodCommands, if present;
	if the player is in HYDRA Lobby:
		if the drapes are examined:
			add "dra - Raise or lower drapes" to GoodCommands, if absent;
	remove "web - Activate or deactivate the webs" from GoodCommands, if present;
	if the player is in HYDRA Lobby:
		if the cobwebs are examined:
			add "web - Activate or deactivate the webs" to GoodCommands, if absent;
	remove "las - fire laser" from GoodCommands, if present;
	remove "ham - swing hammer" from GoodCommands, if present;
	remove "fir - shoot flame" from GoodCommands, if present;
	remove "bot - open bottle" from GoodCommands, if present;
	remove "gun - shoot gun" from GoodCommands, if present;
	remove "saw - use chainsaw" from GoodCommands, if present;
	remove "n,e,s,w,u,d - movement" from GoodCommands, if present;
	remove "ext - exit from war robot control" from GoodCommands, if present;
	if the player is Smasher:
		add "las - fire laser" to GoodCommands, if absent;
		add "ham - swing hammer" to GoodCommands, if absent;
		add "fir - shoot flame" to GoodCommands, if absent;
		add "bot - open bottle" to GoodCommands, if absent;
		add "gun - shoot gun" to GoodCommands, if absent;
		add "saw - use chainsaw" to GoodCommands, if absent;
		add "n,e,s,w,u,d - movement" to GoodCommands, if absent;
		add "ext - exit from war robot control" to GoodCommands, if absent;
	if the player is Angel:
		if HitCounter < 1:
			add "las - fire laser" to GoodCommands, if absent;
		if HitCounter < 3:
			add "ham - swing hammer" to GoodCommands, if absent;
		if HitCounter < 4:
			add "fir - shoot flame" to GoodCommands, if absent;
		add "gun - shoot gun" to GoodCommands, if absent;
		if HitCounter < 5:
			add "bot - open bottle" to GoodCommands, if absent;
		if HitCounter < 2:
			add "saw - use chainsaw" to GoodCommands, if absent;
		add "n,e,s,w,u,d - movement" to GoodCommands, if absent;
		add "ext - exit from war robot control" to GoodCommands, if absent;
	remove "war - Access attack robot" from GoodCommands, if present;
	if the notepad is examined:
		if the player is not Angel:
			if the player is not Smasher:
				if AngelOver is 0:
					add "war - Access attack robot" to GoodCommands, if absent;
	remove "fol - fold" from GoodCommands, if present;
	remove "cal - call" from GoodCommands, if present;
	remove "rai - raise" from GoodCommands, if present;
	remove "spr - Turn sprinkler on and off" from GoodCommands, if present;
	if the player is in MEDUSA Lobby:
		if the central sprinkler is examined:
			add "spr - Turn sprinkler on and off" to GoodCommands, if absent;
	remove "spo - aim the spotlight" from GoodCommands, if present;
[	remove "dru - drumroll" from GoodCommands, if present;]
	remove "lig - turn lights on and off" from GoodCommands, if present;
	if Final Judgement is happening:
		add "spo - aim the spotlight" to GoodCommands, if absent;
[		add "dru - drumroll" to GoodCommands, if absent;]
		add "lig - turn lights on and off" to GoodCommands, if absent;
	remove "bie - raise the funeral bier" from GoodCommands, if present;
	remove "app - raise the golden apple" from GoodCommands, if present;
	if AppleBier is happening:
		add "bie - raise the funeral bier" to GoodCommands, if absent;
		add "app - raise the golden apple" to GoodCommands, if absent;
[		add "dru - drumroll" to GoodCommands, if absent;]
		add "lig - turn lights on and off" to GoodCommands, if absent;
	if WinnerChoiceTime is 1:
		add "spo - aim the spotlight" to GoodCommands, if absent;
	remove "z - wait" from GoodCommands, if present;
	if the player is not in CompFolders:
		add "z - wait" to GoodCommands, if absent;
	remove "xyzzy - return" from GoodCommands, if present;
	if the player is in Project XYZZY:
		add "xyzzy - return" to GoodCommands, if absent;
	remove "ema - check e-mail" from GoodCommands, if present;
	if InboxFull is 1:
		if the player is hacker:
			add "ema - check e-mail" to GoodCommands, if absent;
	remove "x [bracket]something[close bracket] - zoom in camera" from GoodCommands, if present;
	remove "look - describe appearance of the room" from GoodCommands, if present;
	remove "hom - home folder" from GoodCommands, if present;
	remove "aud - enable audiovisual" from GoodCommands, if present;
	remove "help - access help information" from GoodCommands, if present;
	remove "abt - information on DaedalOS" from GoodCommands, if present;
	remove "sou - toggle music" from GoodCommands, if present;
	if the player is Hacker:
		if the player is not in CompFolders:
			add "x [bracket]something[close bracket] - zoom in camera" to GoodCommands, if absent;
			add "look - describe appearance of the room" to GoodCommands, if absent;
		otherwise:
			if SoundOn is 1:
				add "sou - toggle music" to GoodCommands, if absent;
			add "aud - enable audiovisual" to GoodCommands, if absent;
			add "help - access help information" to GoodCommands, if absent;
			add "abt - information on DaedalOS" to GoodCommands, if absent;
		if the player is not in Home Folder:
			if the player is not in FinalCommand:
				add "hom - home folder" to GoodCommands, if absent;
	remove "rec - list recordings" from GoodCommands, if present;
	if a quip is carried by the player:
		if the player is not in CompFolders:
			if the player is Hacker:
				add "rec - list recordings" to GoodCommands, if absent;
	remove "say [bracket]something[close bracket] - play one of your listed recordings" from GoodCommands, if present;
	remove "hyd, med, moi - Observe Project HYDRA, MEDUSA, or MOIRAI" from GoodCommands, if present;
	if the player is Hacker:
		if Travelled is 1:
			if the player is not in FinalCommand:
				add "hyd, med, moi - Observe Project HYDRA, MEDUSA, or MOIRAI" to GoodCommands, if absent;
	if an Englisch quip is carried by the player:
		add "say [bracket]something[close bracket] - play one of your listed recordings" to GoodCommands, if absent;
	if the player is in Final Challenge:
		if the poker table is in Final Challenge:
			now GoodCommands is {"fol - fold", "cal - call", "rai - raise"};
	remove "x [bracket]something[close bracket] - hssssssss squeak" from GoodCommands, if present;
	remove "look - nibble nibble" from GoodCommands, if present;
	remove "xyzzy - return" from GoodCommands, if present;
	if the player is in Project XYZZY:
		now GoodCommands is {"x [bracket]something[close bracket] - hssssssss squeak","look - nibble nibble", "xyzzy - return"};


Before taking inventory:
	update GoodCommands;
	if the player is SinghClonePC:
		say "[bold type]-Commands-[roman type]";
		repeat with currentitem running through GoodCommands:
			say "[line break]   [currentitem]";
		say "[line break]";
	otherwise:
		say "[bold type]-Commands-[roman type]";
		repeat with currentitem running through GoodCommands:
			say "[line break]   [currentitem]";
			[This part is just to list exits]
		if the player is Hacker:
			if the player is not in CompFolders:
				if the player is not in command center:
					now DirectionCounter is 0;
					repeat with way running through directions: 
						let place be the room way from the location of the player;
						if place is a room:
							now DirectionCounter is DirectionCounter plus one;
							if DirectionCounter is 1:
								say "[line break]   ";
							otherwise:
								say ", ";
							say way;
					if DirectionCounter > 0:
						say " - move to camera in room in the indicated direction";
		say "[line break]" instead;

Rule for printing the name of a direction (called the way) while taking inventory: 
	choose row with a heading of the way in the Table of Abbreviation; 
	say "[shortcut entry]".

Table of Abbreviation 
heading	shortcut
north	"n"
northeast	"ne"
northwest	"nw"
east	"e"
southeast	"se"
south	"s"
southwest	"se"
west	"w"
up	"u"
down	"d"
inside	"in"
outside	"out"

[put a list of current commands here]
	
Topicing is an action out of world. Understand "r" or "rec" as topicing.

Understand "rec [text]" as a mistake ("I will automatically record anything of interest, Dr. Law.").

Everspeeched is a number that varies. Everspeeched is 0.

Before speeching a quip to someone:
	now Everspeeched is 1;
	
To say CapQuip:
	say "[RandomQuip]" in upper case;
	
To say RandomQuip:
	let current be a random Englisch quip carried by the player;
	say word number 1 in "[current]";

To say SpeechCommand:
	if EverSpeeched is 0:
		say "[paragraph break][bold type]-New Command-[roman type][line break] say [bracket]something[close bracket] - play one of your listed recordings[first time] (for instance, you can SAY [CapQuip])[only][paragraph break]";
	otherwise:
		say "[paragraph break]";

Carry out topicing:
	if the player is carrying a quip:
		say "[first time][italic type]Dr. Law, per Stygicorp regulations, I am recording all conversations. However, the microphone quality in some rooms will restrict the usefulness of some recordings.

I've noticed that the microphone on your current device is not compliant with Stygicorp standards, and cannot communicate with our servers, Dr. Law. If you need voice communication, you can replay any recording over the speakers in the room you're in.[roman type][SpeechCommand][only][bold type]Recordings:[roman type]";
		repeat with currentquip running through all Englisch quips carried by the player:
			say "[line break]   ";
			say currentquip;
		say "[line break]";
	otherwise:
		say "[bold type]Recordings:[roman type]
		
			You have no current recordings.";
	if the player is carrying a phrased quip:
		say "[bold type]Opening fragments:[roman type]";
		repeat with currentquip running through all firsthalfed phrased quips carried by the player:
			say "[line break]   ";
			say currentquip;				
		say "[line break]";
		say "[bold type]Closing fragments:[roman type]";
		repeat with currentquip running through all secondhalfed phrased quips carried by the player:
			say "[line break]   ";
			say currentquip;				
		say "[line break]";
		if the player is carrying a phull quip:
			say "[bold type]Linked sentences:[roman type]";
			repeat with currentquip running through all phull quips carried by the player:
				say "[line break]   ";
				say currentquip;				
			say "[line break]";
			

Forming relates various things to various things. The verb to form means the forming relation.

Vyn forms VynGogo.
Vyn forms VynSyk.
Vyn forms VynGym.
Vyn forms VynBou.
Vyn forms VynDagei.
Vyn forms VynLy.
Vyn forms VynCha.
Vyn forms VynWo.
Vyn forms VynLydo.

Laly forms LalyGogo.
Laly forms LalySyk.
Laly forms LalyGym.
Laly forms LalyBou.
Laly forms LalyDagei.
Laly forms LalyLy.
Laly forms LalyCha.
Laly forms LalyWo.

Myt forms MytGogo.
Myt forms MytSyk.
Myt forms MytGym.
Myt forms MytBou.
Myt forms MytDagei.
Myt forms MytLy.
Myt forms MytCha.
Myt forms MytWo.

Ling forms LingGogo.
Ling forms LingSyk.
Ling forms LingGym.
Ling forms LingBou.
Ling forms LingDagei.
Ling forms LingLy.
Ling forms LingCha.
Ling forms LingWo.

Gogo forms VynGogo.
Syk forms VynSyk.
Gym forms VynGym.
Bou forms VynBou.
Dagei forms VynDagei.
Ly forms VynLy.
Cha forms VynCha.
Wo forms VynWo.
Lydo forms VynLydo.

Gogo forms LalyGogo.
Syk forms LalySyk.
Gym forms LalyGym.
Bou forms LalyBou.
Dagei forms LalyDagei.
Ly forms LalyLy.
Cha forms LalyCha.
Wo forms LalyWo.

Gogo forms MytGogo.
Syk forms MytSyk.
Gym forms MytGym.
Bou forms MytBou.
Dagei forms MytDagei.
Ly forms MytLy.
Cha forms MytCha.
Wo forms MytWo.

Gogo forms LingGogo.
Syk forms LingSyk.
Gym forms LingGym.
Bou forms LingBou.
Dagei forms LingDagei.
Ly forms LingLy.
Cha forms LingCha.
Wo forms LingWo.

	
Part 2 - Characters

Hacker is a person. Hacker is scenery. The description of Hacker is "Your webcamera is not compatible with our OS, Dr. Law.

However, if your past trends have continued over the last two days, then you've gained 1.4 kilograms, shrunken by .05 cm, and lost 3 Facebook friends.";

Fred is a man. "[if Fred is in MOIRAI Laboratory]Fred looks deep in thought for a moment[otherwise]Fred is at his desk, napping[end if][if MOIRAI laboratory is dark], sleeping[end if]." The description of Fred is "A male human. 

-Height: 170 cm. 

-Approx. weight: 120 kg. 

-Approx. age: 92. 

-Clothing: Black pants with suspenders over white dress shirt, no tie. 

-Costume: A felt pirate hat. 

-Personality: Mischievous."

Understand "male" or "human" or "black" or "pants" or "suspender" or "suspenders" or "white" or "dress" or "shirt" or "felt" or "hat" as Fred.

YoungFred is a man. YoungFred is scenery. YoungFred is nowhere. The description of YoungFred is "A male human. 

-Height: 127 cm. 

-Approx. weight: 25.3 kg. 

-Approx. age: 5. 

-Clothing: white cloth shirt, rough pants with suspenders. 

-Personality: Mischievous[FredInv]". The printed name of YoungFred is "young Fred". Understand "young" or "fred" or "white" or "cloth" or "shirt" or "rough" or "pants" or "suspenders" or "male" or "human" as YoungFred.

To say FredInv:
	if the number of things carried by YoungFred > 0:
		  say "[paragraph break]Inventory: ";
		repeat with item running through things carried by YoungFred:
			say "[line break] [a item] ";
	otherwise:
		do nothing.

Ginger is a woman. The description of Ginger is "A female child. 

-Height: 125 cm. 

-Approx. weight: 22.4 kg. 

-Approx. age: 7. 

-Clothing: Oversized white lab coat, large glasses.

-Personality: Inquisitive." Understand "female" or "human" or "oversized" or "white" or "lab" or "coat" or "large" or "glasses" as Ginger.

Doctor Singh is a man. The description of Doctor Singh is "A male human. 

-Height: 180 cm. 

-Approx. weight: 90 kg. 

-Approx. age: 30. 

-Clothing: navy suit, red tie, grey handkerchief. Hairbun, rolled beard. 

-Personality: disciplined." Understand "male" or "human" or "navy" or "suit" or "tie" or "grey" or "red" or "handkerchief" or "dr" or "hairbun" or "rolled" or "beard" as Doctor Singh.

Doctor Spiel is a woman. The description of Doctor Spiel is "A female human. 

-Height: 175 cm. 

-Approx. weight: 50 kg. 

-Approx. age: indeterminate. 

-Clothing: purple sunglasses, green scarf, orange jumpsuit, no shoes. 

-Personality: hearty." Understand "female" or "human" or "purple" or "sunglasses" or " green" or "scarf" or "orange" or "dr" or "jumpsuit" as Doctor Spiel.

Smasher is a person. Understand "robot" as Smasher. The description of Smasher is "A 20 cm steel humanoid robot.

Attachments: laser, flamethrower, handgun, hammer, bottle opener, chainsaw." 

Report examining smasher:
	if the player is smasher:
		try taking inventory;

Report examining angel:
	if the player is angel:
		try taking inventory;

Persuasion rule for asking Smasher to try doing something: say "You can control Smasher directly, Dr. Law. Press enter for a list of commands.".

Smasher's laser is part of smasher. The description of smasher's laser is "A plastic bulb that lights up to imitate a real laser. Not a very good imitation, Dr. Law". Understand "plastic" or "bulb" or "imitation" as Smasher's laser.

Smasher's flamethrower is part of smasher. Understand "flame" or "lighter" as Smasher's flamethrower. The description of Smasher's flamethrower is "This is essentially a remote control lighter, Dr. Law."

Smasher's handgun is part of smasher. Understand "pellet" as Smasher's handgun. The description of Smasher's handgun is "The handgun seems to shoot plastic pellets using compressed air, Dr. Law."

Smasher's hammer is part of smasher. The description of Smasher's hammer is "This hammer is fairly weak, Dr. Law, unless you want to press buttons or squeeze a very ripe blueberry."

Smasher's bottle opener is part of smasher. The description of smasher's bottle opener is "This is a completely functional bottle opener, Dr. Law."

Smasher's chainsaw is part of smasher. Understand "penknife" or "knife" as Smasher's chainsaw. The description of Smasher's chainsaw is "The chainsaw is, in fact, a penknife, Dr. Law."

Angel is a person. Understand "robot" or "war" as Angel. The description of Angel is "A 2 m steel humanoid robot.

 Attachments: laser, flamethrower, handgun, hammer, bottle opener, chainsaw."


Persuasion rule for asking angel to try doing something: say "You can control Angel directly, Dr. Law, with the right code. Press enter for a list of commands.".

A thing can be minimini or not minimini. A thing is usually not minimini.

Smasher's laser and Smasher's flamethrower and Smasher's handgun and Smasher's bottle opener and Smasher's chainsaw are minimini.

Before examining a minimini thing:	
	if the player is Hacker:
		say "This camera has been restricted to only one level of zoom, Dr. Law." instead;

Angel's laser and Angel's flamethrower and Angel's handgun and Angel's bottle opener and Angel's chainsaw are minimini.

Angel's laser is part of Angel. The description of Angel's laser is "A plastic bulb that lights up to imitate a real laser. Not a very good imitation, Dr. Law". Understand "plastic" or "bulb" or "imitation" as Angel's laser.

Angel's flamethrower is part of Angel. Understand "flame" as Angel's flamethrower. The description of Angel's flamethrower is "The flamethrower has been fixed since Dr. Ng's unfortunate accident."

Angel's handgun is part of Angel. Understand "gun" or "hail" or "bullet" or "bullets" as Angel's handgun. The description of Angel's handgun is "Angel's gun is oddly antique-looking, but is quite functional."

Angel's hammer is part of Angel. The description of Angel's hammer is "Angel's hammer no longer detaches from Angel during usage. This has greatly reduced the number of staff injuries."

Angel's bottle opener is part of Angel. The description of Angel's bottle opener is "This is a completely functional bottle opener, Dr. Law."

Angel's chainsaw is part of Angel. Understand "saw" as Angel's chainsaw. The description of Angel's chainsaw is "The chainsaw has also found use in hedge trimming around Stygicorp Headquarters, Dr. Law."

Doctor Ng is a person. The description of Doctor Ng is "-A cyborg human. 

-Height: 170 cm. 

-Approx. weight: 200 kg. 

-Approx. age: unable to estimate. 

-Clothing: metallic foil suit.

-Personality: driven." Understand "cyborg" or "human" or "shiny" or "suit" or "foil" or "dr" or "metallic" as Doctor Ng.

Part 3 - Locations

Chapter 1 - Computer folders

The CompFolders is a region. Home Folder is in CompFolders.

Section  1 - Home Folder

[A rule goes here, but is listed later to correct some problems]

Home Folder is a room.  The description of Home Folder is "[first time]-Dr. Law, thank you for logging back on. It has been 47 hours and 35 minutes since your last login. The search for your body can now be cancelled. The candidate search for your replacement will also be terminated. The flowers are, unfortunately, non-refundable.

[only]-Your brain backup unit has been turned on somehow, and it's running low on battery; you have only [one of]120[or]115[or]110[or]105[or]100[or]95[or]90[or]85[or]80[or]75[or]70[or]65[or]60[or]55[or]50[or]45[or]40[or]35[or]30[or]25[or]20[or]x0E0[stopping] minutes of battery time remaining before it dies and its backup memory is flushed[first time]. I would have had someone plug it in, of course, but you've ordered the command center to be locked down until after the project judging ceremony[only].[first time][paragraph break][bold type]-Choose from one of the following commands-[roman type][if SoundOn is 1][line break]
     sou - toggle music[end if][line break]
     aud - audiovisual control[line break]
     help - review basic commands[line break]
     abt - information on DaedalOS[only]";

[[paragraph break][only][bold type] Maintenance issues:[roman type][line break]-The Hades Device deadman switch has not been pushed for 64 hours and 40 minutes. Two hours remain before total nuclear destruction of this facility and all remaining personnel.

-Your password is past due to be changed. Please choose something more secure this time; your last two passwords have been flagged as [']extremely weak['].

-The microphones in several areas have been compromised by the recent earthquake, leading to decreased audio quality. Please be aware that some conversations will not be recorded.[ToDoList][first time]]

To say ToDoList:
	if LensBroke > 0:
		say "[paragraph break][bold type]-To Do List-[Roman type][if AngelOver is 0][line break]-Evaluate Project MEDUSA[end if][if SinghEnabled < 2][line break]-Evaluate Project HYDRA[end if][if FredOver is 0][line break]-Evaluate Project MOIRAI[end if][line break]-Make your final selection at the project judging ceremony[line break]-Dr. Law, your brain backup unit's battery light just went from [if FinalCounter is 1][bold type]green[roman type] to [bold type]yellow[roman type][otherwise if FinalCounter is 2][bold type]yellow[roman type] to [bold type]orange[roman type][otherwise if FinalCounter is 3][bold type]orange[roman type] to [bold type]red[roman type][end if]. I don't know what you have in there, but it will be erased if you don't charge it.";

To say StaticToDoList:
	if LensBroke > 0:
		say "[paragraph break][bold type]-To Do List-[Roman type][if AngelOver is 0][line break]-Evaluate Project MEDUSA[end if][if SinghEnabled < 2][line break]-Evaluate Project HYDRA[end if][if FredOver is 0][line break]-Evaluate Project MOIRAI[end if][line break]-Make your final selection at the project judging ceremony[line break]-Dr. Law, your brain backup unit's battery light is [bold type][if FinalCounter is 1]yellow[otherwise if FinalCounter is 2]orange[otherwise if FinalCounter is 3]red[otherwise]green[end if][roman type]. Your brain backup you have in there will be erased if you don't charge it.";

LastAudioRoom is a room that varies. LastAudioRoom is Command Center. 

Audiovisualing is an action applying to nothing. Understand "aud" as audiovisualing.  

Carry out audiovisualing: 
	if the player is in CompFolders:
		say "Now entering audiovisual mode. Type HOM to return to the home folder at any time.[paragraph break]*********************************[line break]Initiating audiovisual link...[line  break]*********************************[AudError]";
		now the player is in LastAudioRoom;
	otherwise:
		say "You are already in audiovisual mode, Dr. Law.";

To say AudError:
	say "[one of][paragraph break][special-style-2]**ERROR** - Dr. Law, your current device is not capable of streaming video or sound in Stygicorp's proprietary format. 

I am diverting additional processor time to image-to-text and speech-to-text recognition. Please don't make a habit of this.[roman type][or][stopping][line break]";

Audioreturning is an action applying to nothing. Understand "hom" as audioreturning.

Before audioreturning:
	now LastAudioRoom is the location of the player;
	
Carry out audioreturning: 
	if the player is in Home Folder:
		say "Computing... understanding HOM as HOMOPHONE. I'll see Aisle C. You'll do Yule Dew.";
	otherwise if the player is not Hacker:
		say "You must first exit from this vessel.";
	otherwise if the player is in FinalCommand:
		say "Dr. Law, it would be rude to leave now.";
	otherwise:
		say "Now exiting audiovisual mode.";
		now the player is in Home Folder;

[Switcherooing is an action applying to nothing. Understand "swi" as switcherooing.

Carry out switcherooing:
	say "**ERROR** - I'm sorry, Dr. Law, but there seems to be some interference with the signal. Please flip the switch manually to avoid total destruction. I've locked down the command center from all other personnel until your return; however, it is scheduled to open at the project judging ceremony later today.";]

Quitting is an action out of world. Understand "logout" or "log out" as quitting.

Carry out quitting:
	follow the immediately quit rule;

Abouting is an action applying to nothing. Understand "abt" or "about" or "credits" as abouting.

Carry out abouting:
	say "I am DaedalOS, an artificial intelligence system accessible via text commands. DaedalOS has been customized quite extensively by you, Dr. Law, but is based on software by Brian Rushton, with beta testing provided by Sean Shore, Chandler Groover, Mike Spivey, Lindsey Rushton, Fred Snyder, namekuseijin, Heartless Zombie, Hillary Crenshaw, Anthony Hope, John Ayliff and Neil Butters.

DaedalOS is dedicated to Lindsey Rushton[if SoundOn is 1].

The talented Eric Matyas of soundimage.org provides much of the music I play for you, Dr. Law, along with other music in the public domain. In particular, Matyas provided the following tracks:

'Digitally Dazed', 'Tower Defense', 'Little Space Probe...Very Big Journey', 'Puzzle Dreams', 'Clunky Old Time Piano', 'Cold Moon', 'Into Battle', 'The Triumph of the Clock Maker', 'Switch-On (Looping)', 'and Moment of Strange (Looping)'.

I have also selected music from the public domain (some from the early 1900's and some from public domain orchestras and bands)[end if].

Stygicorp is an official trademark of Stygicorp, Ltd. The purpose of Stygicorp is to restore life, and was founded by you, Dr. Law, to bring back your... well, I know you don't like me talking about it.[StaticToDoList]";

Helping is an action out of world. Understand "help"  as helping.

Carry out helping:
	say "Dr. Law, I am always available for assistance at any time. Through my advanced forecasting and pattern recognition capabilities, I can deduce what steps you will need to take at any time. To preserve the illusion of choice, I have also compiled gentle guidance, or hints, for you to peruse, which you can access one at a time.

Type HINT to access a hint. Press enter (without text) at any time to refresh the description and current list of commands.

Also, at any time, you can access the time warping capabilities of Project KRONOS through UNDO (reversing time by one move), RESTART (reversing time back to when you logged on today), SAVE (leaving a bookmark in time), and RESTORE (returning to a bookmark in time). But please try not to damage the spacetime continuum more than necessary, Dr. Law.[StaticToDoList]"

Chapter 2 - Cylinder Facilities

The camera is a backdrop. The camera is everywhere. The description of the camera is "[if the player is in CompFolders]You may access the cameras by typing AUD.[otherwise if the player is hacker]A rule of thumb, Dr. Law, would say that a camera cannot view itself.[otherwise]The cameras are well-hidden, Dr. Law.[end if]";

[Before going a direction when the player is not in CompFolders:
	say "Transferring to the next available camera...";]

The connected devices is a backdrop. The connected devices is everywhere. Understand "device" as connected devices.

The description of connected devices is "There are many connected devices at Stygicorp, Dr. Law. It is difficult to identify which object a given wi-fi signal originates from without examining it more closely.

If you cannot remember how to control a given device, simply examine it. "

Does the player mean examining the connected devices:
	it is very unlikely;

Part 1 - Unassigned areas

Section 1 -  Lobby

PillarSeen is a number that varies. PillarSeen is 0.

ThroneSeen is a number that varies. ThroneSeen is 0.

The elaborate reconstruction of a Classical Greek temple is in Command Center. The elaborate reconstruction of a Classical Greek temple is scenery. The description of the elaborate reconstruction of a Classical Greek temple is "[LookAction]"

Command Center is a room. The description of Command Center is "[if the marble hand is unexamined]Dr. Law, I really don't know what you want me to describe. The command center looks exactly like it always does.

Except for the hand on the floor. But you know about that.[otherwise if PillarSeen is 0]Your command center is lined with statues, Dr. Law. The hand of one of the statues has fallen off.

A bloody handprint on a pillar is the only other unusual feature.[otherwise if ThroneSeen is 0]Your command center is an elaborate reconstruction of a Classical Greek temple. Two rows of marble pillars line the sides of the room, interspaced with statues of the gods. A jelly handprint is smeared on one pillar, and a marble hand has broken off and is lying on the floor. At the far end is the marble dais.

And of course, on the dais is your throne. Why you chose that hideous monstrosity as your throne, I'll never know.[otherwise]Your command center is an elaborate reconstruction of a Classical Greek temple. Two rows of marble pillars line the sides of the room, interspaced with statues of the gods. At the far end is the marble dais with your ratty, soiled cubicle throne.

A dark patch lies under your cubicle.[end if][CameraCommand]";

To say CameraCommand:
	say "[paragraph break][bold type]-New commands-[roman type][line break]
 x [bracket]something[close bracket] - zoom in camera on the given object[first time] (for instance, you can X HAND)[only][line break] z - wait[line break] hom - return to home folder";

Report examining something when the player is in Command Center:
	now the marble hand is examined;

The marble hand is in Command Center. The marble hand is scenery. The description of the marble hand is "[first time]I'm sorry, Dr. Law, I didn't realize you wanted me to go into more detail. [only]There is a marble hand here that has broken off of one of your statues. Otherwise, the room is exactly like you left it, Dr. Law.

Including the bloody handprint, of course."

To say PillarUp:
	now PillarSeen is 1.

The statues are in Command Center. The statues are scenery. Understand "statue" as statues. Understand "hades" or "gods" or "persephone" or  "zeus" or "athena" or "hera" or "poseidon" or "KRONOS" or "ares" or "hermes" or "marble" or "aphrodite" as statues. The description of the statues is "The statues are placed between the marble pillars in the command center, facing away from the center. All of the major Greek gods are represented. From the camera angle, I can't tell which statue is missing a hand.[PillarUp]".

The pillars are in Command Center. The pillars are scenery. Understand "pillar" or "marble" or "doric" or "column" or "columns" as pillars. The description of the pillars is "The marble pillars are evenly spaced, and are the type referred to as Doric columns, Dr. Law. They lead from the end of the room up to the dais with your throne. The bloody handprint is near your throne.[paragraph break]Your throne is in disarray, Dr. Law.[PillarUp]"

The bloody handprint is in Command Center. Understand "print" as the bloody handprint. The bloody handprint is scenery. The description of the bloody handprint is "[one of]Oh, after zooming in, it seems t[or]T[stopping]he blood was actually jelly. The handprint matches yours, Dr. Law. It appeared two days ago, after the tremor and blackout, right after you disappeared. You must have run out while making your daily sandwich.

There's a dark patch by your throne, but I can't make it out at this level of zoom.[PillarUp]". Understand "jelly" or "jam" as the bloody handprint.

To say ThroneUp:
	now ThroneSeen is 1;
	now PillarSeen is 1;

The cubicle throne is in Command Center. The cubicle throne is scenery. Understand "dark" or "patch" or "dais" or "hideous" or "monstrosity" or  "ratty" or "dirty" or "mess" as cubicle throne. The description of the cubicle throne is "Zooming in. [first time]Dr. Law, I have to apologize, but I don't usually talk about your throne for a reason. I think it's absolutely deplorable. [only]Your throne is made from a ratty, dirty, foul-smelling cubicle that you rescued from your first researcher job. Its very presence soils the marble dais it rests on.

Oh, right, the dark patch: a puddle of red liquid is trailing out from under the cubicle.[ThroneUp]"

The puddle of red liquid is in Command Center. The puddle of red liquid is scenery. The description of the puddle of red liquid is "Zooming in further. The red liquid is coming from a crushed jelly jar under the desk. 

There is a lumpy human-shaped object next to the jelly jar."

The crushed jelly jar is scenery in Command Center.  The description of the crushed jelly jar is "There is a puddle of red liquid coming from a crushed jelly jar under the desk.

There seems to be a lumpy human-shaped object next to the jelly jar."

The lumpy human-shaped object is in Command Center. The lumpy human-shaped object is scenery. The description of the lumpy human-shaped object is "Zooming in further. Dr. Law, the object is-[BreakText]". Understand "human" or "shaped" as the lumpy human-shaped object.

LensBroke is a number that varies. LensBroke is 0.

LensBreaking is a scene. LensBreaking begins when LensBroke is 1.

To say BreakText:
	now LensBroke is 1;

When LensBreaking begins:
	wait for any key;
	say "You hear a cracking sound. Dr. Law, it seems you have pushed the cameras too far. I am restricting the use of zoom to one level from now on.

In the meantime, please finish reviewing the projects prior to the awards ceremony; several of the researchers seem to be having problems. And I know that you are anxious to bring [italic type]you-know-who[roman type] back from the dead, after all these years.

And please, charge your brain backup unit; it seems to be low on battery.[paragraph break][CongratulationText]";

Before examining something when the player is in Command Center:
	if LensBroke is 1:
		say "The lens is still broken, Dr. Law." instead;

Before looking when the player is in Command Center:
	if LensBroke is 1:
		say "The lens is still broken, Dr. Law." instead;

Travelled is a number that varies. Travelled is 0.

To say CongratulationText:
	now Travelled is 1;
	say "[bold type]Travel Commands[roman type]:[line break] hyd - Observe Project HYDRA[line break] med - Observe Project MEDUSA[line  break] moi - Observe Project MOIRAI[paragraph break]";

HYDRAing is an action applying to nothing. Understand "HYD" as HYDRAing.



Carry out HYDRAing:
	now Travelled is 1;
	if the player is not in FinalCommand:
		say "**************************************************[line break]Initiating audiovisual link with Project HYDRA...[line  break]**************************************************[AudError]";
		now the player is Hacker;
		now the player is in HYDRA Lobby;
		if LensBroke is 0:
			say "I must remind you, Dr. Law, that the new cameras in the interiors of the lab have not been fully calibrated since the earthquake, and as such only have one level of zoom available.";
		now LensBroke is 2;
	otherwise:
		say "It would be rude to leave now, Dr. Law.";

MOIRAIng is an action applying to nothing. Understand "MOI" as MOIRAIng.

Carry out MOIRAIng:
	now Travelled is 1;
	if the player is not in FinalCommand:
		say "**************************************************[line break]Initiating audiovisual link with Project MOIRAI...[line  break]**************************************************[AudError]";
		now the player is Hacker;
		now the player is in the MOIRAI Lobby;
		if LensBroke is 0:
			say "I must remind you, Dr. Law, that the new cameras in the interiors of the lab have not been fully calibrated since the earthquake, and as such only have one level of zoom available.";
		now LensBroke is 2;
	otherwise:
		say "It would be rude to leave now, Dr. Law.";

MEDUSAing is an action applying to nothing. Understand "MED" as MEDUSAing.

Carry out MEDUSAing:
	now Travelled is 1;
	if the player is not in FinalCommand:
		say "**************************************************[line break]Initiating audiovisual link with Project MEDUSA...[line break]**************************************************[AudError]";
		now the player is Hacker;
		now the player is in MEDUSA Lobby;
		if LensBroke is 0:
			say "I must remind you, Dr. Law, that the new cameras in the interiors of the lab have not been fully calibrated since the earthquake, and as such only have one level of zoom available.";
		now LensBroke is 2;
	otherwise:
		say "It would be rude to leave now, Dr. Law.";

Part 2 - Fred and Ginger's areas

MOIRAIArea is a region. MOIRAI Lobby, MOIRAI Laboratory, and MOIRAI Living Quarters are in MOIRAIArea.

Instead of MOIRAIng when the player is in MOIRAIArea:
	say "You are already viewing Project MOIRAI, Dr. Law."

Section 1 - Ginger's room

MOIRAI Lobby is a room. The description of MOIRAI Lobby is "This is a hollow cavern lit by underwater lights underneath a rippling pool. The walls are covered with an etching of the Moirai: one is spinning a thread, one is measuring the thread, and another cuts the thread. The etchings are filled with silver. 

Around the edges of the ceiling are thick curtains, which are currently [if the thick curtains are raised]raised[otherwise]lowered[end if]. In the pool is a dog-shaped fountain that is[FountainStatusText].

The three women are eyeless. There is a door to the [bold type]north[roman type] in the wall. The door is etched with a table on which a single eye rests. The pupil of the eye is a peephole in the door.

Dr. Law, I detect two connected devices in this room.[first time][CommandListHint][only]";

The hollow cavern is in MOIRAI Lobby. The hollow cavern is scenery. The description of the hollow cavern is "You see a hollow cavern lit by waterproof lights underneath a rippling pool. The walls are covered with an etching of the MOIRAI, or Fates: one is spinning thread, one is measuring the thread, and another cuts the thread. The etchings are filled with silver. 

Around the edges of the ceiling are thick curtains, which are currently [if the thick curtains are raised]raised[otherwise]lowered[end if]. In the pool is a fountain that is[FountainStatusText].

The three women are eyeless. There is a door in the wall etched with a table, on which a single eye rests. The pupil of the eye serves as a peephole."

The Moirai etching is scenery. The Moirai etching is in Moirai Lobby. The description of the Moirai etching is "[one of]On closer inspection, t[or]T[stopping]he thread being handled by the Fates is [first time]actually [only]a data stream of 1[']s and 0[']s. The women themselves are modelled on famous scientists: Hypatia as Atropos, Ada Lovelace as Clothos, and Emmy Noether as Lachesis.[one of][or] Don't forget, Dr. Law, that I've restricted you to a single level of zoom. There is nothing more to learn from this etching.[stopping]". Understand "fate" or "fates" or "wall" or "walls" or "thread" or "silver" or "data" or "stream" or "1's" or "0's" or "ada" or "lovelace" or "emmy" or "noether" or "clothos" or "lachesis" or "women" or "eyeless" as the MOIRAI etching.

Hypatia Atropos is part of the moirai etching. The printed name of Hypatia Atropos is "[one of]Hypatia[or]Atropos[at random]". The description of Hypatia Atropos is "This is the only one you can see more detail on. Hypatia appears to be talking on a cell phone, and judging the existence (or non-existence) of various things."

The single eye is scenery. The single eye is in MOIRAI Lobby. The description of the single eye is "A single eye is carved in the middle of the door. I cannot see through the eye at this angle, Dr. Law. I hope Ginger's not using the peephole for light again; she's had enough eyestrain already from using her grown-up glasses.

This eye seems to be a reference to the Graeae, Dr. Law, which is odd, as I understand that they are distinct entities in mythology.". Understand "table" or "door" or "pupil" or "peephole" as the single eye.

The thick curtains are scenery. The thick curtains are in MOIRAI Lobby. Understand "curtain" or "snag" as thick curtains. The description of the thick curtains is "Heavy curtains on a mechanized roll are pulled up to the top of the room. [first time][CurtainCommand][only]". Understand "motor" as the thick curtains.

The rippling pool is scenery. The rippling pool is in MOIRAI Lobby. Understand "underwater" or "light" or "lights" or "waterproof" or "bottom" as the rippling pool.The description of the rippling pool is "Lights are shimmering from underneath the pool. A fountain shaped like the two-headed dog Orthrus rises from the center of the pool. The fountain seems to be connected. [FountainCommand]";

After examining the pool:
	now the fountain is examined.

The fountain is scenery. The fountain is in MOIRAI Lobby. The description of the fountain is "The fountain is shaped in the form of Orthrus, the two-headed mythical guardian dog. One head is lapping from the pool while another is supposed to spew out water with a cheery expression. 

This fountain is[FountainStatusText].[FountainCommand]". Understand "center" or "head" or "stream" or "orthrus" or "dog" or "heads" or "headed" or "dog-shaped" or "dog" or "shaped" as the fountain.

To say CurtainCommand:
	say "[paragraph break][bold type]-New commands-[roman type][line break]
 cur - Raise and lower curtain";

The thick curtains can be raised or lowered. The thick curtains are raised.

Curtaining is an action applying to nothing. Understand "CUR" as curtaining.

Carry out curtaining:
	if fountTurnCounter is 8 and the player is in MOIRAI Lobby:
		say "The curtain can no longer be adjusted, Dr. Law.";
	otherwise if the player is in MOIRAI Lobby:
		if the thick curtains are raised:
			say "The curtains lower from the ceiling. 

They almost reach the eye, but not quite, Dr. Law. There seems to be a snag in the motor. ";
			now the thick curtains are lowered;
			if fountTurnCounter is 4:
				say "The water is spraying directly on the curtains. The extra weight causes them to sag until they cover the eye. ";
				now fountTurnCounter is 8;
				wait for any key;
				say "[paragraph break]You hear a child make a startled sound from the laboratory. You see a light click on from the other side. 

Young Ginger slowly pushes open the door and peeks out her head. She pushes up her glasses and gathers up her oversized lab coat. Ginger steps quickly to the pool and fishes her hand around the bottom, and the fountain turns off. The curtains raise back to their original position. She walks back into the laboratory, which is now illuminated, and pulls the door shut.";
				now MOIRAI Laboratory is lighted;
				now MOIRAI Living Quarters is lighted;
				now AlarmOn is 1;
			otherwise:
				say "[line break]";
		otherwise:
			say "The curtains are retracted back onto the ceiling, Dr. Law.";
			now the thick curtains are raised;
	otherwise:
		say "Computing...Assuming CUR means CURRANT JELLY...Dr. Law, there is no currant jelly here. Also, I only accept verbs as commands, in general.";

To say FountainCommand:
	say "[first time][paragraph break][bold type]-New commands-[roman type][line break]
 tur - Turn fountain right[line break]
 rot - Turn fountain left[only]";


Fountaining is an action applying to nothing. Understand "ROT" as fountaining.

The fountTurnCounter is a number that varies. The fountTurnCounter is 1.

To say FountainText:
	if fountTurnCounter is -1:
		say "The motors squeal and pop as the fountain attempts to rotate, but it remains immobile.[paragraph break]";
		now fountTurnCounter is 0;
	otherwise:
		say "The fountain rotates, changing the water flow.[paragraph break]";
	if fountTurnCounter is 8:
		say "The fountain sculpture is completely loose. A gush of water is spraying onto the door, blocking the eyehole.[paragraph break]";
		wait for any key;
		say "You hear a child make a startled sound from the laboratory. You see a light click on from the other side. 

Ginger slowly pushes open the door and peeks out her head. She pushes up her glasses and gathers up her oversized lab coat. Ginger steps quickly to the pool and fishes her hand around the bottom, and the fountain turns off[if the thick curtains are lowered]. The curtains raise back to their original position[end if]. She makes her way back into the laboratory, which is now illuminated, and pulls the door shut.";
		now MOIRAI Laboratory is lighted;
		now MOIRAI Living Quarters is lighted;
		now AlarmOn is 1;
	if fountTurnCounter is 0:
		say "The water stream is completely dead, now.";
	if fountTurnCounter is 1:
		say "The fountain is now at its original strength. The stream is pointing at Clothos.";
	if fountTurnCounter is 2:
		say "The fountain is now at twice its original strength. The stream is pointing at Lachesis, and nearly reaches the edge of the pool.";
	if fountTurnCounter is 3:
		say "The fountain is now at three times its original strength. The stream is pointing at Atropos, and is spraying out onto the floor.";
	if fountTurnCounter is 4:
		say "The fountain is now at four times its original strength. The stream is pointing at the table with the eye, and is spraying onto the door, just missing the eye. ";
		if the thick curtains are lowered:
			say "[paragraph break]The water is spraying directly on the curtains. The extra weight causes them to sag until they cover the eye. ";
			now fountTurnCounter is 8;
			wait for any key;
			say "[paragraph break]You hear a child make a startled sound from the laboratory. You see a light click on from the other side. 

Ginger slowly pushes open the door and peeks out her head. She pushes up her glasses and gathers up her oversized lab coat. Ginger steps quickly to the pool and fishes her hand around the bottom, and the fountain turns off. The curtains raise back to their original position. She walks back into the laboratory, which is now illuminated, and pulls the door shut.";
			now MOIRAI Laboratory is lighted;
			now MOIRAI Living Quarters is lighted;
			now AlarmOn is 1;
		otherwise:
			say "[paragraph break]";
	if fountTurnCounter is 5:
		say "The fountain is spraying a strong jet at Clothos. The fountain head is wobbling slightly. Water sprays over the wall and seeps into hidden drains.";
	if fountTurnCounter is 6:
		say "The fountain is spraying a strong jet at Lachesis. The fountain head is wobbling.";
	if fountTurnCounter is 7:
		say "The fountain is wobbling violently, spraying water all over the wall with the etching of Atropos.";

The hidden drains are in MOIRAI Lobby. The hidden drains are scenery. The description of the hidden drains is "They're hidden, Dr. Law.";

To say FountainStatusText:
	if fountTurnCounter is 0:
		say " currently turned off";
	if fountTurnCounter is 1:
		say " streaming weakly";
	if fountTurnCounter is 2:
		say " flowing at a moderate pace, pointed at Lachesis";
	if fountTurnCounter is 3:
		say " flowing strongly at Atropos, spilling out a bit onto the floor";
	if fountTurnCounter is 4:
		say " spraying onto the door, almost hitting the eye";
	if fountTurnCounter is 5:
		say " spraying a strong jet at Clothos. The fountain head is wobbling slightly. Water sprays over the wall and seeps into hidden drains";
	if fountTurnCounter is 6:
		say " spraying a strong jet at Lachesis. The fountain head is wobbling";
	if fountTurnCounter is 7:
		say " wobbling violently, spraying water all over the wall with the etching of Atropos";
	if fountTurnCounter is 8:
		say " currently turned off"
	

Carry out fountaining:
	if fountTurnCounter is 8 and the player is in MOIRAI Lobby:
		say "The fountain can no longer be adjusted, Dr. Law.";
	otherwise if the player is in MOIRAI Lobby:
		now fountTurnCounter is fountTurnCounter plus 1;
		say "[FountainText]";
	otherwise:
		say "Computing...Assuming ROT means ROTINI...Dr. Law, there is no rotini here. And you have not supplied a verb. But in any case, there is no rotini.";

Unfountaining is an action applying to nothing. Understand "TUR" as unfountaining.

Carry out unfountaining:
	if fountTurnCounter is 8 and the player is in MOIRAI Lobby:
		say "The fountain can no longer be adjusted, Dr. Law.";
	otherwise if the player is in MOIRAI Lobby:
		now fountTurnCounter is fountTurnCounter minus 1;
		say "[FountainText]";
	otherwise:
		say "Computing...Assuming TUR means TURNIQUET...Dr. Law, this is not the correct way to spell tourniquet. We have discussed this several times.";
[As you turn the fountain, the stream increases or decreases; keep going until it has circled twice]

Section 2 - Deaging machine (Ginger's portion)

Ginger is in MOIRAI Laboratory.  Ginger is scenery.

MOIRAI Laboratory is north from MOIRAI Lobby. MOIRAI Laboratory is dark. MOIRAI Living Quarters is dark. The description of MOIRAI Laboratory is "This laboratory is primarily lit by random electrical discharges. It contains 69,105 visible mechanical gadgets. The largest gadget is a square dais inscribed with a circle. Cables and cords connect the dais to other gadgets, control panels and monitors. You hear hums, snaps and sizzles from the electrical discharges. The rest of the MOIRAI facilities can be found to the [bold type]north[roman type] and [bold type]south[roman type].[first time]

You can see young Ginger here, standing on a chair, typing on a terminal.

A light blinks on her terminal. She looks straight up at the camera, and types into her terminal: 'Is that you? Are you logged on, Fred? Fred, we've got to hurry before the presentation.  It's a team eat team world out there.

'We've got to get back to our original ages. I'm still recalibrating the machine after the quake, and I've been testing it out on a wide variety of objects.'

She takes off her huge glasses and rubs her eyes. She says, 'It's been exhausting, Fred! Here, let's play a game to make it go faster. I call it: Binary or Bust. I'll tell you more about it, but I need you to run the rejuvenator remotely. Turn it on, real quick.'

[bold type]-New command-[roman type][line break] ene - Energize the rejuvenator[only]"

The random electrical discharges are in MOIRAI Laboratory. The random electrical discharges are plural-named. The random electrical discharges are scenery. The description of the random electrical discharges is "While random electrical discharges are very inefficient, Dr. Law, they do provide an encouraging ambience for a senior scientist like Ginger." Understand "hums" or "snaps" or "snizzles" as the random electrical discharges. 

Instead of listening to the random electrical discharges:
	say "There is a theory, Dr. Law, that a million random electric discharges, over a million years, could recreate the work of Beethoven. But they do not sound like Beethoven at the moment.";

Instead of listening to moirai laboratory:
	say "There is a theory, Dr. Law, that a million random electric discharges, over a million years, could recreate the work of Beethoven. But they do not sound like Beethoven at the moment.";

The mechanical gadgets are in MOIRAI Laboratory. The mechanical gadgets are scenery. The mechanical gadgets are plural-named. Understand "visible" or "69105"  or "cables" or "cable" or "cords" or "cord" or "control" or "panel" or "panels" or "monitor" or "nest" or "robotic" or "rate" or "monitors" as the mechanical gadgets. The description of the mechanical gadgets is "Dr. Law, the length of time it would take me to describe these gadgets in detail is better spent on more useful things, such as preparing for the awards ceremony.

But if you must know, the room looks like the nest of a robotic rat."

The terminal is in MOIRAI Laboratory. The terminal is scenery. The description of the terminal is "Ginger prefers to communicate via keyboard, when possible. But you know that, Dr. Law."

The rejuvenator is in MOIRAI Laboratory. The Rejuvenator is scenery. The Rejuvenator is a supporter. The description of the Rejuvenator is "The rejuvenator is a metal platform shaped like an elaborately carved eye[if the rejuvenator is energized] whose pupil is glowing[end if]. It is capable of supporting one or two individuals. It is a single step up from the floor." Understand "machine" or "square" or "dais" or "gadget" or "dials" or "levers" as the rejuvenator.

The physiomental stabilizer is part of the rejuvenator. The description of the physiomental stabilizer is "Dr. Law, this is the component that preserves the mind of an individual on the rejuvenator." Understand "something" as the physiomental stabilizer.

The rejuvenator can be energized or not energized. The rejuvenator is not energized.

Energizing is an action applying to nothing. Understand "ene" as energizing.

Every turn when the player is in MOIRAI Laboratory:
	if Ginger is in MOIRAI Laboratory:
		if MOIRAI Laboratory is lighted:
			repeat with current running through darknessed things in Moirai Laboratory:
				now current is nowhere;
			if the rejuvenator is not energized:
				say "[one of]Ginger is waiting patiently.[or]Ginger types, 'Come on, Fred, don't you want your body back? Turn on the rejuvenator.'[or]Ginger taps her terminal.[or]Ginger types, 'Fred, I don't understand what's taking you so long. And your IP address seems off; let me search for it.'[or]Ginger types, 'I'm starting a search on your IP address, Fred.

'That's odd... It's not even close to here!'[or]Ginger types, 'I've sent a special surprise to that location, Fred. Be careful if someone knocks on the door.'[or]'Well Fred, I'll just wait here until you type ENE to energize the rejuvenator,' types Ginger.[or]Ginger is napping.[stopping]";


Carry out energizing:
	if the player is in MOIRAI Laboratory:
		if the rejuvenator is not energized:
			now the rejuvenator is energized;
			say "The eye-shaped rejuvenator begins to hum, and the pupil begins to glow. Ginger types, 'Thanks, Fred, you're the sweetest. 

'I want you to split up the objects by a binary classification; you know, like hot vs. cold, or soft vs. hard. But don't use those, obviously.

'Age everything in one category, and de-age everything in the other category. If I can guess the pattern, I win. If not, we keep playing. That'll help me test a lot of objects.' She looks at her watch. 'We only have time for two or three games; if I win, I'll stop there,' she types with a smile.

Ginger types, 'I'll start with [a entry 1 of ObjectsList], [a entry 2 of ObjectsList], [a entry 3 of ObjectsList], [a entry 4 of ObjectsList], [a entry 5 of ObjectsList], and [a entry 6 of ObjectsList]. Try and find some way of splitting them into two groups. And don't tell me the pattern! Let me guess.'

She grabs [a entry 1 of ObjectsList] and puts it on the rejuvenator. [RejuvenatorCommand]";
			let currentitem be entry ListCounter of ObjectsList;
			now currentitem is on the Rejuvenator;
		otherwise:
			if Ginger is in MOIRAI Laboratory:
				say "The rejuvenator is already energized, Dr. Law.";
			otherwise:
				say "It seems Ginger left the energizer on when she left, Dr. Law.";
	otherwise:
		say "Computing...Understanding ENE as ENERVATE. I feel myself becoming weaker, Dr. Law. It is cold... so cold...";

To say RejuvenatorCommand:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 new - De-age the object on the rejuvenator[line break]
 old - Age the object on the rejuvenator[paragraph break]";


Rule for printing the description of a dark room: 
	if the player is in MOIRAI Living Quarters:
		say "The room is completely dark, Dr. Law. Fred must be taking a nap. You can exit to the [bold type]south[roman type].";
	otherwise:
		say "The room is completely dark except for a thin ray of light coming from a hole in the door to the south. The ray lands on a single book in the middle of the room. A dark figure sits, reading by the light. You can return to the [bold type]south[roman type] or venture to the [bold type]north[roman type]." instead. 

A thing can be darknessed or not darknessed. A thing is usually not darknessed.

The thin ray of light is darknessed scenery in Moirai Laboratory. The description of the thin ray of light is  "There is a thin ray of light streaming from the peephole in the door, Dr. Law." Understand "peephole" or "door" as the thin ray of light.

The single book is darknessed scenery in Moirai Laboratory. The description of the single book is "It seems to be some kind of journal, Dr. Law. It says:

[fixed letter spacing]Day 3654:

The malfunction due to the earthquake is still not fixed. As I write this, Fred is sleeping in the room in the body of a 92-year old man. I myself have reverted to when I was seven. Thankfully, the machine preserved our minds. I have almost completed the recalibration of the machine: I have performed the following tests:[variable letter spacing]

There are several tiny rows of experiments underneath, with their outcomes." Understand "journal" as the single book.

The several tiny rows of experiments are part of the single book.

The dark figure is darknessed scenery in Moirai Laboratory. The dark figure is a woman. The description of the dark figure is "I assume this is Ginger, Dr. Law. She likes to work with minimal lighting." Understand "ginger" or "her" or "lady" or "girl" or "woman" as the dark figure.

After deciding the scope of the player: 
	repeat with item running through darknessed things in the location: 
		place item in scope.

Before examining a darknessed thing:
	say "[description of the noun][line break]" instead;

Rule for printing a refusal to act in the dark: 
	if we are examining something:
		 say "The light is far too poor to make out any details, Dr. Law." instead. 

[
Rule for printing the name of a dark room: 
	say "Dark Side" instead. 
]

A thing can be organic or inorganic. A thing is usually inorganic.

A thing can be large or small. A thing is usually small.

A thing can be expensive or cheap. A thing is usually cheap.

A thing can be q-named or not q-named. A thing is usually not q-named.

A thing has some text called the YoungText. The YoungText of a thing is usually "Nothing happens."

A thing has some text called the OldText. The OldText of a thing is usually "Nothing happens."

The slice of quiche lorraine is an organic small expensive q-named thing. The description of the slice of quiche lorraine is "A savory custard pie with bacon fragments."

The YoungText of the slice of quiche lorraine is "The slice of quiche lorraine trembles and sputters. The quiche separates into a cloud of particles and reforms into a pile of eggs, a small bowl of milk, and a plate of bacon."

The OldText of the slice of quiche lorraine is "The slice of quiche lorraine turns grey, and then green. Large mold colonies sprout out, forming towers of red, green, and gold."

The vial of truffle oil is an organic small expensive not q-named thing. The description of the vial of truffle oil is "A bottle of pale yellow truffle oil.".

The YoungText of the vial of truffle oil is "The vial of truffle oil collapses in a cloud of dust, resolving into a pile of sand and a few mushrooms, which roll around."

The OldText of the vial of truffle oil is "The truffle oil darkens from yellow, to brown, to black as it oxidizes. 

The vial cracks, and a cloud of vapor escapes. Ginger gags and tries to wave the smell away."

The bulky antique quilt is an organic large expensive q-named thing. The description of the bulky antique quilt is "This is a cotton quilt from 1950. It has a label saying '$4000'."

The YoungText of the bulky antique quilt is "The quilt pops into little cotton bolls which spring in every direction.".

The OldText of the bulky antique quilt is "The quilt ages gracefully. The price label changes to read $40000, then $100000. 

Then a small rip appears in the quilt, and the label changes to $12.59.".

The bearskin rug is an organic large expensive not q-named thing. The description of the bearskin rug is "This is thick, large bearskin rug."

The YoungText of the bearskin rug is "The bearskin rug shrinks with an audible 'yoink' into a roly poly bear cub, which spins for a few seconds before coming to a halt. It then curls up and falls asleep.".

The OldText of the bearskin rug is "The bearskin rug becomes patchy, and then bald. It then grows hairy again with thick, green, shaggy mold, before crumbling away to a pile of mold."

The quart of milk is an organic small cheap q-named thing. The description of  the quart of milk is "I actually think this was yours, Dr. Law. She must have grabbed it after you went missing."

The YoungText of the quart of milk is "The carton shreds itself and turns into woodchips, while the milk separates into a small dish of water and a bundle of alfalfa.".

The OldText of the quart of milk is "The carton churns and pops as something inside moves about violently. The carton begins to blacken and decay. It peels off to reveal a perfect gorgonzola cheese."

The wool baby sock is an organic small cheap not q-named thing. The description of the wool baby sock is "One of Ginger's old paisley socks."

The YoungText of the wool baby sock is "The long thread of the sock delicately unwinds itself until it lies coiled in a pile. The thread then untwists and puffs, leaving behind a pile of wool.".

The OldText of the wool baby sock is "The wool baby sock becomes thin. Holes develop. The holes are darned, and the sock is filled with birdseed and hung. The birdseed is eaten, and the sock withers away until only a scant few threads are left."

The long quote banner is an organic large cheap q-named thing. [De-aging and re-aging changes the language] The description of the long quote banner is "This is a large paper banner with a quotation on it: 'I gave them hope, and so turned away their eyes from death'"

The YoungText of the long quote banner is "The banner now says ,'Thnetous g'epausa me proderkesthai moron; typhlas en autois elpidas katokisa.'".

The OldText of the long quote banner is "The banner now consists solely of emojis of questionable origin."

The big haystack is an organic large cheap not q-named thing. The description of the big haystack is "A pile of fresh hay."

The YoungText of the big haystack is "The individual stalks separate, turn green, and rise up, the roots spreading out and forming a net. In the absence of dirt, it collapses."

The OldText of the big haystack is "The hay decays and putrefies, boiling away until a single needle is left."

The rare quarter is an inorganic small expensive q-named thing. [aging changes year, and value, but always high for absurd reasons] The description of the rare quarter is "A rare double-head quarter, with Washington's head on both sides."

The YoungText of the rare quarter is "The quarter shrinks and evolves to become a Roman aureus."

The OldText of the rare quarter is "The quarter evolves into a receipt for a bundle of e-currency, before evolving into a chunk of gold."

The diamond ring is an inorganic small expensive not q-named thing. The description of the diamond ring is "A small diamond ring in a gold setting; I think it's from Fred's first wife.[first time][paragraph break]Did you know that diamonds, although made of carbon, are classified as inorganic compounds by most chemists?[only]".

The YoungText of the diamond ring is "The diamond falls out of the setting and morphs into a lump of coal. The gold remains essentially the same."

The OldText of the diamond ring is "The ring gets scuffed and dinged. The diamond falls out of the setting, but not much else happens. Diamonds are forever, Dr. Law."

The vat of quicksilver is an inorganic large expensive q-named thing. The description of the vat of quicksilver is "This is a big vat full of mercury, or quicksilver."

The YoungText of the vat of quicksilver is "The vat of quicksilver bubbles and boils and settles down as a vat of cinnabar."

The OldText of the vat of quicksilver is "The vat of quicksilver begins to evaporate extremely quickly. Warning lights sound, and Ginger quickly hits an emergency stop button. 'That was a bad idea,' she says. 'Why do we even have this?'"

The full-sized marble statue is an inorganic large expensive not q-named thing. The description of the full-sized marble statue is "[first time]Oh dear, Dr. Law. [only]It seems Ginger has borrowed one of your statues. This one is of Ovid."

The YoungText of the full-sized marble statue is "The stone of the statue bubbles and ripples. It reshapes itself into limestone."

The OldText of the full-sized marble statue is "The statue is attacked by microbes, crumbling until it is a faceless shadow of a human. It is rather creepy, Dr. Law."

The quartz pebble is an inorganic small cheap q-named thing. The description of the quartz pebble is "A pure quartz crystal, rubbed round in the ocean."

The YoungText of the quartz pebble is "The pebble's rounded edges fill out and become sharp. It then collapes into a puddle of magma, which Ginger carefully cleans off with an asbestos rag.".

The OldText of the quartz pebble is "Nothing visible happens. I have to say, Dr. Law, that this was intensely disappointing.".

The tin can is an inorganic small cheap not q-named thing. The description of the tin can is "An old tin can, its top ripped off."

The YoungText of the tin can is "The can leaps into the air with a bang and comes down as a chunk of tin and a much larger chunk of iron, with a pile of carbon."

The OldText of the tin can is "Slime quickly grows over the tin can. The inside crumbles into rust, but the tin layer remains mostly preserved.".

The oversized iron quoit is an inorganic large cheap q-named thing. The description of the oversized iron quoit is "This is a large iron disk with a hole in the center, for throwing."

The YoungText of the oversized iron quoit is "The quoit becomes unhammered and unforged, dents disappearing and smoothed away, until it's just a lump of iron."

The OldText of the oversized iron quoit is "The quoit crumbles into rust."

The large sandcastle is an inorganic large cheap not q-named thing. The description of the large sandcastle is "An intricate sand castle on a metal sheet."

The YoungText of the large sandcastle is "The sand grains leap together to form a large, igneous rock."

The OldText of the large sandcastle is "The castle slowly crumbles into a pile of sand, which is mostly blown away.".

[Spruce up the last 10 or so of these]

ObjectsList is a list of things that varies. ObjectsList is {slice of quiche lorraine, vial of truffle oil, bulky antique quilt, bearskin rug, quart of milk, wool baby sock, long quote banner, big haystack, rare quarter, diamond ring, vat of quicksilver, full-sized marble statue, quartz pebble, tin can, oversized iron quoit, large sandcastle}

Younging is an action applying to nothing. Understand "new" as younging.

YoungOrganic is a number that varies. YoungOrganic is 0.

OldOrganic is a number that varies. OldOrganic is 0.

YoungLarge is a number that varies. YoungLarge is 0.

OldLarge is a number that varies. OldLarge is 0.

YoungCheap is a number that varies. YoungCheap is 0.

OldCheap is a number that varies. OldCheap is 0.

YoungQ is a number that varies. YoungQ is 0.

OldQ is a number that varies. OldQ is 0.

OldCount is a number that varies. OldCount is 0.

YoungCount is a number that varies. YoungCount is 0.

The large box is in MOIRAI Laboratory. The large box is scenery. The description of the large box is "Ginger seems to be able to find room for all sorts of things in this box.";

TotalRejuved is a number that varies. TotalRejuved is 1;

Carry out younging:
	let current be entry ListCounter of ObjectsList;
	if the player is not in MOIRAI Laboratory:
		say "Computing...Understanding NEW as NEW YORK NEW YORK. I'll start spreading the news, Dr. Law.";
	otherwise if the rejuvenator is not energized:
		say "The rejuvenator is not energized, Dr. Law.";
	otherwise:
		if Ginger is in MOIRAI Laboratory:
			now TotalRejuved is TotalRejuved plus one;
			now YoungCount is YoungCount plus one;
			let current be entry ListCounter of ObjectsList;
			say YoungText of current;
			say " Ginger takes the transformed [current] and places it in a large box. ";
			now current is nowhere;
			if current is Organic:
				now YoungOrganic is YoungOrganic plus one;
			otherwise:
				now OldOrganic is OldOrganic plus one;
			if current is Large:
				now YoungLarge is YoungLarge plus one;
			otherwise:
				now OldLarge is OldLarge plus one;
			if current is Cheap:
				now YoungCheap is YoungCheap plus one;
			otherwise:
				now OldCheap is OldCheap plus one;
			if current is Q-named:
				now YoungQ is YoungQ  plus one;
			otherwise:
				now OldQ is OldQ plus one;
			say ProgressText;
			continue the testing;
		otherwise if YoungFred is on the rejuvenator:
			now TotalRejuved is TotalRejuved plus one;
			say "The Rejuvenator activates, and flashes brightly. Fred screams in a voice that becomes more and more high pitched. He shrinks to become a baby, then disappears. A second later, a mummified corpse appears on the rejuvenator, then slowly de-ages until it opens its eyes, and stands up. A normal-aged Fred is now on the Rejuvenator.

He looks at himself in surprise, then hears Ginger shouting in the distance, and runs off towards the depths of the laboratory.";
			finish Moirai;
		otherwise if the rejuvenator is in MOIRAI Laboratory:
			say "Nothing is on the rejuvenator, Dr. Law.";
		otherwise:
			say "The rejuvenation machine isn't here anymore, Dr. Law.";

Olding is an action applying to nothing. Understand "old" as olding.

Carry out olding:
	if the player is not in MOIRAI Laboratory:
		say "Computing...Understanding OLD as OLD MAID. I'll start researching the rules, Dr. Law.";
	otherwise if the rejuvenator is not energized:
		say "The rejuvenator is not energized, Dr. Law.";
	otherwise:
		if Ginger is in MOIRAI Laboratory:
			now TotalRejuved is TotalRejuved plus one;
			now OldCount is OldCount plus one;
			let current be entry ListCounter of ObjectsList;
			say OldText of current;
			say " Ginger takes the transformed [current] and places it in a large box. ";
			now current is nowhere;
			if current is Organic:
				now OldOrganic is OldOrganic plus one;
			otherwise:
				now YoungOrganic is YoungOrganic plus one;
			if current is Large:
				now OldLarge is OldLarge plus one;
			otherwise:
				now YoungLarge is YoungLarge plus one;
			if current is Cheap:
				now OldCheap is OldCheap plus one;
			otherwise:
				now YoungCheap is YoungCheap plus one;
			if current is Q-named:
				now OldQ is OldQ  plus one;
			otherwise:
				now YoungQ is YoungQ plus one;
			say ProgressText;
			continue the testing;
		otherwise if YoungFred is on the rejuvenator:
			now TotalRejuved is TotalRejuved plus one;
			say "The rejuvenator activates, and flashes brightly. Fred screams in a voice that becomes more and more low pitched. He stretches and fills out into a middle-aged man. A normal-aged Fred is now on the Rejuvenator.

He looks at himself in surprise, then hears Ginger shouting in the distance, and runs off towards the depths of the laboratory.";
			finish Moirai;
		otherwise if the rejuvenator is in MOIRAI Laboratory:
			say "Nothing is on the rejuvenator, Dr. Law.";
		otherwise:
			say "The rejuvenation machine isn't here anymore, Dr. Law.";

When play begins:
	sort ObjectsList in random order;

FailCounter is a number that varies. FailCounter is 0.

To continue the testing:
	if the player is in MOIRAI Laboratory:
		if Ginger is in MOIRAI Laboratory:
			Let LastItem be entry ListCounter of ObjectsList;
			if LastItem is on the Rejuvenator:
				now LastItem is nowhere;
			now ListCounter is ListCounter plus one;
			if ListCounter is 17:
				now FailCounter is FailCounter plus one;
				now ListCounter is 1;
				now OldCount is 0;
				now YoungCount is 0;
				sort ObjectsList in random order;
				now YoungOrganic is 0;
				now OldOrganic is 0;
				now YoungLarge is 0;
				now OldLarge is 0;
				now YoungCheap is 0;
				now OldCheap is 0;
				now YoungQ is 0;
				now OldQ is 0;
				if FailCounter is 4:
					say "[paragraph break]Ginger sighs and types, 'I'm sorry, I couldn't guess anyone of them, Fred. I think this is enough testing for now; the machine is fully calibrated.' She writes SUCCESS in her journal in giant letters.[paragraph break]'Well', she types, 'I guess it's time to try it out.'

She stands on the rejuvenator, and presses a button. There is a bright flash of light, and a middle-aged woman reappears. Ginger checks that her lab coat and glasses still fit. She snaps by her ears, and smiles; her hearing defect must be repaired again.

An alarm buzzes, and the terminal flashes a message: [fixed letter spacing]FIRE IN SECTOR 3[variable letter spacing]. Ginger sees the message, and turns off both it and the alarm.

She looks at the camera [lovesigh]";
					now Ginger is nowhere;
					repeat with current running through ObjectsList:
						now current is nowhere;
				otherwise:
					say "[paragraph break][one of]'I guess we'll need to start over.' [or]'I thought that time that you were going to sort them by whether they were [CorrectPattern] or not, to be honest.'  [stopping][paragraph break]Ginger says, 'This time I'll use [a entry 1 of ObjectsList], [a entry 2 of ObjectsList], [a entry 3 of ObjectsList], [a entry 4 of ObjectsList], [a entry 5 of ObjectsList], and [a entry 6 of ObjectsList]. Try and find some way of splitting them into two groups. And don't tell me the pattern! Just age one group and de-age the other and let me guess.'[paragraph break]";
			if Ginger is in MOIRAI Laboratory:
				let currentitem be entry ListCounter of ObjectsList;
				now currentitem is on the Rejuvenator;
				say "Ginger grabs [a currentitem] and places it on the rejuvenator, over the pupil of the eye.";

		
ListCounter is a number that varies. ListCounter is 1.

LastItem is a thing that varies. LastItem is the large sandcastle.

CorrectPattern is some text that varies. CorrectPattern is "nothing".

To say ProgressText:
	if YoungLarge is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "large";
	otherwise if OldLarge is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "small";
	otherwise if YoungCheap is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "cheap";
	otherwise if OldCheap is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "expensive";
	otherwise if YoungOrganic is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "organic";
	otherwise if OldOrganic is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "inorganic";
	otherwise if YoungQ is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "written with a Q";
	otherwise if OldQ is ListCounter:
		say TurnbyTurnGingerText;
		now CorrectPattern is "written without a Q";
	otherwise:
		say "Ginger sighs, and types, 'I'm sorry, Fred, I can't figure out! Looks like you win this round[if FailCounter > 2] Let me try again[end if]. ";
		Now ListCounter is 16.

[Every turn:
	say Listcounter;]

To say TurnByTurnGingerText:
	if ListCounter is 1:
		say "[paragraph break]Ginger writes, 'Interesting, Fred.' ";
	otherwise if ListCounter is 2:
		say "[paragraph break]Ginger writes, 'I've got some ideas about which classification you chose, but I'm not quite there yet.' ";
	otherwise if ListCounter is 3:
		say  "[paragraph break]Ginger writes, 'I definitely have a theory, Fred. Keep them coming! I want to win.' ";
	otherwise if Listcounter is 4:
		say "[paragraph break]Ginger writes, 'Aha! My theory has been confirmed. Just one more experiment, and I'll tell you what I think.' ";
	otherwise if ListCounter is 5:
		if OldCount is 5:
			say "Ginger types, 'Oh Fred, you just aged everything. I can't guess anything from that! ";
			now ListCounter is 16;
		otherwise if YoungCount is 5:
			say "Ginger types, 'Oh Fred, you just de-aged everything. I can't guess anything from that! ";
			now ListCounter is 16;
		otherwise:
			say "[paragraph break]Ginger writes triumphantly, 'Well, I guessed it, Fred. You've been deaging everything that's [CorrectPattern] and aging everything else. Great idea! I love it!' She writes SUCCESS in her journal in giant letters.[paragraph break]'Well', she types, 'I guess it's time to try it out.'

She stands on the rejuvenator, and presses a button. There is a bright flash of light, and a middle-aged woman reappears. Ginger checks that her lab coat and glasses still fit. She snaps by her ears, and smiles; her hearing defect must be repaired again.

An alarm buzzes, and the terminal flashes a message: [fixed letter spacing]FIRE IN SECTOR 3[variable letter spacing]. Ginger sees the message.

She looks at the camera [lovesigh]";
			now Ginger is nowhere;
			repeat with current running through ObjectsList:
				now current is nowhere;

Love is a quip. The preview of Love is "I love you, Fred."

To say lovesigh:
	say "and says, 'Thanks Fred, I love you!' before running out of the room. She bumps into the rejuvenator on the way out, and sparks fly out for a second before fading.";
	deliver Love;
	say "[line break]It seems Ginger broke something in the rejuvenator. You should really get that fixed before the judging ceremony, Dr. Law."

Section 3 -Deaging machine (Fred's portion)

Instead of speeching a phrased quip to someone:
	say "That recording needs to be linked into a complete sentence, Dr. Law.";

IsFredYoung is a number that varies. IsFredYoung is 0.

FredCounter is a number that varies. FredCounter is 0.

Every turn when the player is in MOIRAI Laboratory:
	if IsFredYoung is 0:
		if Fred is in MOIRAI Laboratory:
			if FredCounter is 1:
				say "There is a flash of brilliant light. You now see a young version of Fred, approximately 5 years old. He looks around the room with wide eyes. A hiss of steam from a machine startles him, and he says, 'Mytyeh gogo?' A light flashes on the terminal, and he says, 'Mytyeh gogo?' in a higher voice.

He sees something that makes him smile and runs out of the room. You can hear him to the [bold type]north[roman type].

[italic type]Dr. Law, with the machine malfunctioning, Fred has reverted mentally to his 5-year-old self, who, unfortunately, only speaks Old Gwyndyngwah.

[italic type]I do not speak Old Gwyndyngwah, Dr. Law, but I'll do what I can to help. Fred speaks so slowly and clearly that I split up his recordings into fragments that you can combine. The more you use them, the more information I can gather on their meaning.[roman type]

[bold type]-New command-[roman type][line break] link [bracket]a recording[close bracket] to [bracket]another recording[close bracket] - combine two halves of a sentence.[line break]";
				now Fred is nowhere;
				now YoungFred is in MOIRAI Living Quarters;
				now HiddenNobody is in MOIRAI Laboratory;
				now IsFredYoung is 1;
				deliver Myt;
				now PortOn is 0;
				deliver Gogo;
			otherwise if FredCounter is 0:
				say "Fred looks for Ginger, but only finds her journal, which still says SUCCESS written in giant letters.

He cackles, and climbs on the platform. He pushes a button, and pulls a lever. The machine is building up.";
				now FredCounter is 1;
				now the sword is in MOIRAI living quarters;
				now the small glass box is in MOIRAI Living Quarters;
[				deliver Rico;]

Section 4 -Fred's room

MOIRAI Living Quarters is north from MOIRAI Laboratory. Fred is in MOIRAI Living Quarters. The description of MOIRAI Living Quarters is "You see a natural cavern filled with [if the plastic gold coins are examined]piles of plastic coins[otherwise]gold coins[end if] and [if costume jewelry is examined]costume [end if]jewelry. A bunk bed shaped like a miniature Spanish galleon is on one side of the room. The other side contains a nook with a desk and shelves filled with pirate ships in glass bottles. You can return to the laboratory to the [bold type]south[roman type]. [if AlarmOn is 1]Fred's eye-shaped computer is blinking red[otherwise]Fred's eye-shaped computer is silent[end if][first time]. [paragraph break]I detect two connected devices in this room, Dr. Law[only].[if FredCounter is 1]

There is a glass box labeled 'OPEN IN CASE OF ADVENTURE' [SwordText].[first time] Fred must have pulled it out from the piles of treasure when he came in.[only][end if]"

The small glass box is scenery. The description of the small glass box is "The box is labeled 'OPEN IN CASE OF ADVENTURE'. Ginger bought it for Fred on their honeymoon, I think."

To say SwordText:
	say "[if the sword is not carried by YoungFred]containing a small plastic sword[otherwise]that is currently empty[end if]"
	
The porthole TV is in MOIRAI Living quarters. "[if porton is 0]There is a glass porthole set into the wall.[otherwise]The porthole TV is turned on.[end if]". The porthole TV is not portable. Understand "television" or "tube" or "glass" or "telly" as the porthole TV.  The description of the porthole TV is "This is Fred's TV, shaped like a porthole, Dr. Law. He only watches pirate shows.[PortholeCommand]"

To say PortholeCommand:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 por - Turn TV on and off";

PortOn is a number that varies. PortOn is 0.

Porting is an action applying to nothing. Understand "por" as porting.

Carry out porting:
	if the player is in Moirai Living Quarters:
		now the porthole TV is examined;
		if PortOn is 0:
			now PortOn is 1;
			if YoungFred is in Moirai Living Quarters:
				say "The porthole TV turns on, playing an exciting pirate action scene with intense swordplay. When Fred sees the swordfighting, he points and says, 'Gym!' [if sword is carried by YoungFred]while waving his sword.[otherwise] excitedly.[end if]";
				deliver Gym;
			otherwise if Fred is in Moirai Living Quarters:
				say "The porthole TV turns on, playing an exciting swordfight scene of courageous pirates. Fred blearily reaches out an arm and turns it off.";
				now PortOn is 0;
			otherwise:
				say "The porthole TV turns on, playing exciting pirate scenes, but no one notices.";
		otherwise:
			now PortOn is 0;
			if YoungFred is in Moirai Living Quarters:
				say "The TV goes dark. Fred looks sad.";
			otherwise if Fred is in Moirai Living Quarters:
				say "The TV goes dark. Fred still seems half-awake, though.";
			otherwise:
				say "The TV goes dark.";
	otherwise:
		say "Computing....understanding POR as PORKCHOPS. I have sent instructions to our cafeteria, Dr. Law; your porkchop should be ready in 30 minutes."

Report going south from Moirai Living Quarters:
	say "You can hear the movie in the other room turn off.";

Every turn when the player is in Moirai Living Quarters:
	if PortOn is 1:
		say "The pirate TV show is playing in the background, showing an exciting swordfight. [one of]Fighters rush from one ship to another, waving their swords.[or]The captain pulls out his saber and rushes into the depths of the pirate ship.[or]The pirate captain leaps from the rigging, swinging his sword.[or]There is a short ad, with a cheery voice singing, 'How much is that....'.[cycling]"

The natural cavern is in MOIRAI Living Quarters.  The natural cavern is scenery. The description of the natural cavern is "You see a natural cavern filled with [if the plastic gold coins are examined]piles of plastic coins[otherwise]gold coins[end if] and [if costume jewelry is examined]costume [end if]jewelry. A bunk bed shaped like a miniature Spanish galleon is on one side of the room. The other side contains a nook with a desk and shelves filled with pirate ships in glass bottles.". Understand "rock" as the natural cavern.

The Spanish galleon bed is scenery. The Spanish galleon bed is in MOIRAI Living Quarters. The description of the Spanish galleon bed is "Fred built this bed with his own hands. He has quite the obsession with piracy, Dr. Law."

The plastic gold coins are in MOIRAI Living Quarters. The plastic gold coins are scenery. The description of the plastic gold coins is "[One of]Upon closer inspection, Dr. Law, you see that the coins are plastic.[or]I don't know why Fred has piles of plastic coins, Dr. Law.[stopping]". Understand "pile" or "piles" or "treasure" or "treasures" as the plastic gold coins.

The costume jewelry is in MOIRAI Living Quarters. The costume jewelry is scenery. Understand "pearl" or "pearls" or "gem" or "gems" or "plastic" or "treasure" or "fake" as the costume jewelry. The description of the costume jewelry is "[one of]Dr. Law, this is costume jewelry. The pearls are fake, and the gems are plastic.[or]Fred threw a party once where everyone wore this costume jewelry.[stopping]".

The nook is an open container. The nook is scenery. The nook is in MOIRAI Living Quarters. The description of the nook is "A natural hollow in the rock." Understand "hollow" as the nook.

Fred's desk is in the nook. Fred's desk is scenery. The description of Fred's desk is "Fred's desk is made from the deck of a salvaged wooden sailing ship, Dr. Law.".

AlarmOn is a number that varies. AlarmOn is 0.

Alarming is an action applying to nothing. Understand "ala" as alarming.

Carry out alarming:
	if the player is in Moirai Living Quarters:
		now Fred's computer is examined;
		if AlarmOn is 0:
			now AlarmOn is 1;
			if YoungFred is in Moirai Living Quarters:
				say "The visual alarm on Fred's eye-shaped computer goes off. Fred pauses and looks around, wide eyed. When he sees the computer, he points at it and says, 'Mytyeh dagei?'";
				deliver Myt;
				deliver Dagei;	
			otherwise if Fred is in Moirai Living Quarters:
				say "The visual alarm on Fred's eye-shaped computer goes off. Fred stirs a little, and says, 'Ginger, did you say something?' before nodding off again.";
			otherwise:
				say "The visual alarm on the eye-shaped computer starts going off, but no one notices.";
		otherwise:
			now AlarmOn is 0;
			if YoungFred is in Moirai Living Quarters:
				say "The alarm stops. Fred looks relieved.";
			otherwise if Fred is in Moirai Living Quarters:
				say "The alarm stops. Fred still seems half-awake, though.";
			otherwise:
				say "The alarm stops.";
	otherwise:
		say "Computing....understanding ALA as A LA MODE. I have sent instructions to our cafeteria, Dr. Law; your pie should be ready in 30 minutes."
			
Before speeching Love to Fred:
	now AlarmOn is 0;

Before listing nondescript items:
	if Fred's desk is examined:
		if Fred's desk is marked for listing:
			now Fred's desk is not marked for listing.

Fred's computer is in Moirai Living Quarters. Fred's computer is scenery.  Understand "alarm" as the computer. The description of Fred's computer is "Fred's computer is molded in the shape of an eye, just like the rejuvenator in the other room[if AlarmOn is 1]. It has a blinking red alarm; perhaps you should turn it off.[ExplanationText][end if]."

Every turn when AlarmOn is 1:
	if the player is in Moirai Living Quarters:
		say "The alarm on Fred's computer is blaring.";

To say ExplanationText:
	say "[first time] Fred has enabled remote access to the alarm, mostly so Ginger can wake him up after naps.

[bold type]-New Command-[roman type][line break] ala - turn alarm on and off[only]";

Before listening to Moirai Living Quarters:
	if AlarmOn is 1:
		try listening to Fred's Computer instead;
		
Instead of listening to Fred's Computer:
	say "[if AlarmOn is 1]The alarm is bleeping loudly on Fred's computer[otherwise]The computer is silent[end if].";

The display shelves are in the nook. The display shelves are scenery. The description of the display shelves is "These shelves are filled with pirate ships in bottles.".

The pirate ships in glass bottles are in the display shelves. The pirate ships in glass bottles are scenery. The description of the pirate ships in glass bottles is "Fred has a large selection, Dr. Law. He's been collecting for 60 years."

[YouToo is a quip. The preview of YouToo is "I love you too."

Seeking is a quip. The preview of Seeking is "Where are you?"]

Before speeching something to Fred:
	if the player is in Moirai Laboratory:
		say "Fred is too busy to hear you, Dr. Law." instead;

After speeching Love to Fred:
	say "Fred blushes, and looks around, squinting. When he doesn't see anyone, he stands up, smiles, and wanders into the room to the south[if AlarmOn is 1]. He turns off the alarm on his way out[end if].";
[	deliver Youtoo;
	deliver Seeking;]
	now Fred is in MOIRAI Laboratory.

To say WeNeed:
	say "[one of]Fred belches, but looks embarrassed.[or]We need to find a way to get young Fred back to the rejuvenator in the other room, Dr. Law.[cycling]"

Every turn when YoungFred is enclosed by MOIRAI Living Quarters:
	if the player is in MOIRAI Living Quarters:
		if the current action is speeching something to YoungFred:
			do nothing;
		otherwise if the current action is keywording:
			do nothing;
		otherwise if the current action is singlespeeching:
			do nothing;
		otherwise if the current action is alarming:
			do nothing;
		otherwise:
			say "[one of][swordaction][or][swordaction2][or][DiabetesAction][or][pebbleaction][or][pebbleaction2][or][pebbleaction3][or][ElectricAction][or][ElectricAction2][cycling]"

The fork is scenery. The fork is nowhere.

The fried hair is scenery. The fried hair is nowhere.

The pebbles are scenery. The pebbles are plural-named.

The sword is scenery.  The description of the sword is "It is a small, plastic sword, Dr. Law. Very similar to the one you wore as part of your last Halloween costume.[if the sword is in Moirai living quarters] It's lying on Fred's bed. He should see it [one of][or]again [stopping]soon, the way he's running around the room.[otherwise if the sword is carried by YoungFred] Fred is carrying it.[end if]"

The gold coin is scenery.

A quip has some text called firsthint. Firsthint of a quip is usually "YOU SHOULDN'T SEE THIS".

A quip has some text called secondhint. Secondhint of a quip is usually "YOU SHOULDN'T SEE THIS".

Laly is a firsthalfed phrased quip. The preview of Laly is "Laly..."

Firsthint of Laly is "This seems to be a question word."

Secondhint of Laly is "This seems to mean 'Where is...'"

Myt is a firsthalfed phrased quip. The preview of Myt is "Mytyeh...";

Firsthint of Myt is "This seems to be a question word."

Secondhint of Myt is "This seems to mean 'What is...'"

Gogo is a secondhalfed phrased quip. The preview of Gogo is "...gogo.";

Firsthint of Gogo is "This seems to mean '...that.'"

Ling is a firsthalfed  phrased quip. The preview of Ling is "Ly ling..."

Firsthint of Ling is "This seems to be a command."

Secondhint of Ling is "This seems to mean 'take...'"

Syk is a secondhalfed phrased quip. The preview of Syk is "...syk.";

Firsthint of Syk is "This seems to mean '...rock.'"

Gym is a secondhalfed phrased quip. The preview of Gym is "...gym.";

Firsthint of Gym is "This seems to mean '...sword.'"

Cha is a secondhalfed phrased quip. The preview of Cha is "...cha.";

Firsthint of Cha is "This seems to mean '...fork.'"

Bou is a secondhalfed phrased quip. The preview of Bou is "...bou.";

Firsthint of Bou is "This seems to mean '...treasure.'"

Dagei is a secondhalfed phrased quip. The preview of Dagei is "...dagei.";

Firsthint of Dagei is "This seems to mean '...machine.'"

Ly is a secondhalfed phrased  quip. The preview of Ly is "...ly."

Firsthint of Ly is "This seems to mean '...you.'"

Lydo is a secondhalfed phrased quip. The preview of Lydo is "...lydo."

Firsthint of Lydo is "This seems to mean '...here.'"

Wo is a secondhalfed phrased quip. The preview of wo is "...wo."

Firsthint of wo is "This seems to mean '...me.'"

Vyn is a firsthalfed  phrased quip. The preview of Vyn is "Vyn...!"

Firsthint of Vyn is "This seems to be a command."

Secondhint of Vyn is "This seems to mean 'Come to...'"

LingSyk is a phull quip. The preview of LingSyk is "Ly ling syk.";

After speeching LingSyk to YoungFred:
	if the pebbles are carried by YoungFred:
		say "Fred looks confused, then transfers the pebbles from one hand to another.";
	otherwise:
		say "Fred nods, and reaches in his pocket and pulls out some pebbles.";
		now the pebbles are carried by the player;

LingGym is a phull quip. The preview of LingGym is "Ly ling gym.";

After speeching LingGym to YoungFred:
	if the sword is carried by YoungFred:
		say "Fred looks confused, then transfers the sword from one hand to another.";
	otherwise if the player is in Moirai Living Quarters:
		say "Fred nods, and stretches and grabs the sword, swinging it bravely.";
		now the sword is carried by YoungFred;
		now PermaSword is 1;

LingGogo is a phull quip. The preview of LingGogo is "Ly ling gogo.";

After speeching LingGogo to YoungFred:
	if the number of things carried by YoungFred > 0:
		say "Fred looks confused, then transfers [a random thing carried by YoungFred] from one hand to another.";
	otherwise if YoungFred is in Moirai Living Quarters:
		say "Fred looks around, confused, then runs to the treasure and grabs a coin.";
		now gold coin is carried by the player;
	otherwise:
		say "Fred looks around, confused, and shrugs his shoulder.";

LingCha is a phull quip. The preview of LingCha is "Ly ling cha.";

After speeching LingCha to YoungFred:
	if the fork is carried by the player:
		say "Fred looks confused, then transfers the fork from one hand to another.";
	otherwise if the player is in Moirai Living Quarters:
		say "Fred nods, and searches around for the fork. He finds it and holds it hesitantly.";
		now the fork is carried by YoungFred;
	otherwise:
		say "Fred is looking around, Dr. Law, but doesn't seem to find anything.";

LingBou is a phull quip. The preview of LingBou is "Ly ling bou.";

After speeching LingBou to YoungFred:
	if the gold coin is carried by the player:
		say "Fred looks confused, then transfers the coin from one hand to another.";
	otherwise if the player is in Moirai Living Quarters:
		say "Fred nods, and searches around for the coin. He grabs it and holds it hesitantly.";
		now the gold coin is carried by YoungFred;
	otherwise:
		say "Fred is looking around, Dr. Law, but doesn't seem able to find anything."

LingDagei is a phull quip. The preview of LingDagei is "Ly ling dagei."

After speeching LingDagei to YoungFred:
	if the player is in Moirai Laboratory:
		say "Fred nods nervously, and tries to lift up the machine, but is unable to take it, Dr. Law."; 
	otherwise:
		say "Fred goes to his computer and tries to pick it up, but it's stuck to the desk.";

LingLy is a phull quip. The preview of LingLy is "Ly ling ly."
	
After speeching LingLy to YoungFred:
	say "Fred looks startled, but nods. He grasps his ankles and yanks as hard as he can, turning a few somersaults before coming to a rest. He laughs, and says, 'Wo ling wo!'";
	deliver wo;

LingWo is a phull quip. The preview of LingWo is "Ly ling wo."

After speeching LingWo to Youngfred:
	say "Fred says, 'Mytyeh? Laly ly? Vyn wo!' and makes beckoning motions.";
	deliver Laly;
	deliver vyn;
	deliver wo;

MytSyk is a phull quip. The preview of MytSyk is "Mytyeh syk?";

After speeching MytSyk to YoungFred:
	if pebbles are not carried by YoungFred:
		say "Fred makes a small rolling motion with his fingers, then pulls some pebbles out of his pocket and shows them to the speakers before hiding them again.";
	otherwise:
		say "Fred shows the pebbles to the speakers.";

MytGym is a phull quip. The preview of MytGym is "Mytyeh gym?";

After speeching MytGym to YoungFred:
	if the sword is not carried by YoungFred:
		say "Fred shrugs, and makes a chopping motion with his hand.";
	otherwise:
		say "Fred makes a chopping motion with his hand, then shows the sword to the speakers.";

MytGogo is a phull quip. The preview of MytGogo is "Mytyeh gogo?";

After speeching MytGogo to YoungFred:
	if the sword is carried by YoungFred:
		say "Fred waves his sword and says, 'Gym!'";
		deliver Gym;
	otherwise if the pebbles are carried by YoungFred:
		say "Fred shows the pebbles to the camera and says, 'Syk!'";
		deliver Syk;
	otherwise if the fork is carried by YoungFred:
		say "Fred shows the fork to the camera and says, 'Cha!'";
		deliver Cha;
	otherwise if the gold coin is carried by YoungFred:
		say "Fred shows the gold coin to the camera and says, 'Bou!'";
		deliver Cha;
	otherwise:
		if YoungFred is in Moirai Living Quarters:
			say "Fred looks around, considering. He points to the gold coins and says, 'Bou!'";
			deliver Bou;
		otherwise:
			say "Fred looks around, considering. He points to the rejuvenator and says, 'Dagei!'";
			deliver Dagei;

MytCha is a phull quip. The preview of MytCha is "Mytyeh cha?";

After speeching MytCha to YoungFred:
	if the fork is not carried by YoungFred:
		say "Fred shrugs, and makes a spiking motion with three fingers.";
	otherwise:
		say "Fred makes a spiking motion with three fingers, then shows the fork to the speakers.";

MytBou is a phull quip. The preview of MytBou is "Mytyeh bou?";

After speeching MytBou to YoungFred:
	if the gold coin is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred points to the treasure and says,'Bou! Bou!'";
		otherwise:
			say "Fred shrugs, and then makes a heaping gesture with his hands, as if he's picking something up and letting it spill.";
	otherwise:
		say "Fred shows the gold coin to the speakers.";


MytDagei is a phull quip. The preview of MytDagei is "Myt dagei?"

After speeching MytDagei to YoungFred:
	if the player is in Moirai Laboratory:
		say "Fred makes robot motions, then points to the machine and says, 'Dagei!'";
	otherwise:
		say "Fred makes robot motions, then points to the computer and says, 'Dagei.'";

MytLy is a phull quip. The preview of MytLy is "Mytyeh ly?"

After speeching MytLy to YoungFred:
	say "Fred looks astonished, and says, 'Mytyeh wo?' then points to himself.";
	deliver Wo;
	
MytWo is a phull quip. The preview of MytWo is "Mytyeh wo?"

After speeching MytWo to YoungFred:
	say "Fred shrugs and shakes his head. He guesses, 'Dagei?'";
	deliver Dagei;

LalySyk is a phull quip. The preview of LalySyk is "Laly syk?";

After speeching LalySyk to YoungFred:
	if pebbles are not carried by YoungFred:
		say "Fred thinks for a moment, then points to his pocket, and says, 'Lydo!'";
	otherwise:
		say "Fred shows the pebbles to the speakers and says, 'Lydo!'";
	deliver Lydo;

LalyGym is a phull quip. The preview of LalyGym is "Laly gym?";

After speeching LalyGym to YoungFred:
	if the sword is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred points to the box, and says, 'Lydo!'";
		otherwise:
			say "Fred points to the living quarters, where you hear him say, 'Lydo!'";
	otherwise:
		say "Fred points to the sword, and says, 'Lydo!'";
	deliver Lydo;

LalyGogo is a phull quip. The preview of LalyGogo is "Laly gogo?";

After speeching LalyGogo to YoungFred:
	say "Fred looks around, puzzled, and says, 'Mytyeh?'";

LalyCha is a phull quip. The preview of LalyCha is "Laly cha?";

After speeching LalyCha to YoungFred:
	if the fork is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred looks around the room, but can't seem to find what you're asking for. He points hesitantly at the clutter.";
		otherwise:
			say "Fred points into the other room, and shouts, 'Lydo!'";
			now Fred is in Moirai living quarters;
	otherwise:
		say "Fred shows the fork to the speakers.";

LalyBou is a phull quip. The preview of LalyBou is "Laly bou?";

After speeching LalyBou to YoungFred:
	if the gold coin is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred points to the pile of treasure and giggles.";
		otherwise:
			say "Fred points to the other room, and shouts, 'Lydo!'";
			deliver Lydo;
	otherwise:
		say "Fred shows the gold coin to the speakers and shouts, 'Lydo!'";
		deliver Lydo;

LalyDagei is a phull quip. The preview of LalyDagei is "Laly dagei?"

After speeching LalyDagei to YoungFred:
	if the player is in Moirai Laboratory:
		say "Fred points to the machine and says, 'Lydo!'";
		deliver Lydo;
	otherwise:
		if the sword is carried by Fred:
			say "Fred runs through the doorway to the south, swinging his sword, and shouts 'Lydo!'";
			deliver Lydo;
			now Fred is in Moirai Laboratory;
		otherwise:
			say "Fred points nervously through the doorway to the south.";
		

LalyLy is a phull quip. The preview of LalyLy is "Laly ly?"

After speeching LalyLy to YoungFred:
	say "Fred looks astonished, and points to himself. He says, 'Wo lydo!' He then peers around the room. 'Laly ly? Vyn wo!'";
	deliver wo;
	deliver vyn;
	deliver Lydo;

LalyWo is a phull quip. The preview of LalyWo is "Laly wo?"

After speeching Lalywo to YoungFred:
	say "Fred shakes his head. He says, 'Laly ly? Vyn Lydo!'";
	deliver ly;
	deliver vyn;
	deliver lydo;

VynSyk is a phull quip. The preview of VynSyk is "Vyn syk!";

After speeching VynSyk to YoungFred:
	if pebbles are not carried by YoungFred:
		say "Fred shakes his head, points to his pocket, and says, 'Syk lydo.'";
	otherwise:
		say "Fred shows the pebbles to the speakers and says, 'Syk lydo'";
	deliver Lydo;

VynGym is a phull quip. The preview of VynGym is "Vyn gym!";

After speeching VynGym to YoungFred:
	if the sword is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred nods, then rushes up to the box, next to the sword.";
		otherwise:
			say "Fred runs to the living quarters.";
			now Fred is in Moirai Living Quarters;
	otherwise:
		say "Fred laughs, throws the sword and walks to it. He picks it back up again.";
	deliver Lydo;

VynGogo is a phull quip. The preview of VynGogo is "Vyn gogo!";

After speeching VynGogo to YoungFred:
	say "Fred looks around, puzzled, and says, 'Mytyeh?'";

VynCha is a phull quip. The preview of VynCha is "Vyn cha?";

After speeching VynCha to YoungFred:
	if the fork is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred looks around the room, but can't seem to find what you're asking for. He walks over to the clutter.";
		otherwise:
			say "Fred rushes into the other room.";
			now Fred is in Moirai living quarters;
	otherwise:
		say "Fred shakes his head and shows the fork to the speakers. He says, 'Cha lydo!'";

VynBou is a phull quip. The preview of VynBou is "Vyn bou!";

After speeching VynBou to YoungFred:
	if the gold coin is not carried by YoungFred:
		if the player is in Moirai Living Quarters:
			say "Fred rushes over to the pile of treasure.";
		otherwise:
			say "Fred runs into the other room.";
			deliver Lydo;
	otherwise:
		say "Fred shakes his head and shows the gold coin to the speakers. He says, 'Bou lydo!'";
		deliver Lydo;

VynDagei is a phull quip. The preview of VynDagei is "Vyn dagei!"

After speeching VynDagei to YoungFred:
	if the player is in Moirai Laboratory:
		say "Fred rushes onto the rejuvenator, and stands on the pupil of the eye.";
		now YoungFred is on the rejuvenator;
	otherwise:
		if the sword is carried by Fred:
			say "Fred runs through the doorway to the south, swinging his sword.";
			now YoungFred is in Moirai Laboratory;
		otherwise:
			say "Fred looks back and forth at the machinery to the south and the computer, muttering 'Mytyeh dagei?' He ends up running to the computer.";
		
VynLy is a phull quip. The preview of VynLy is "Vyn ly!"

After speeching VynLy to YoungFred:
	say "Fred says, 'Vyn wo?' then laughs and runs around in a circle until he falls down.";
	deliver Wo;

VynWo is a phull quip. The preview of VynWo is "Vyn wo!"

After speeching Vynwo to YoungFred:
	say "Fred looks around. He says, 'Laly ly?' He points to the speaker and the [if the player is in Moirai Laboratory]rejuvenator[otherwise]computer[end if] and says, 'Dagei?'";
	deliver dagei;
	deliver ly;

VynLydo is a phull quip. The preview of VynLydo is "Vyn Lydo!";

After speeching VynLydo to YoungFred:
	say "Fred runs towards the speaker, obediently.";

A quip can be oncesaid or not oncesaid. 

Before speeching a phull quip to YoungFred:
	now the noun is oncesaid;

To say pebbleAction:
	if pebbles are not carried by youngfred:
		if the number of things carried by YoungFred > 1:
			say "Fred transfers everything to one hand and takes some pebbles out of his pocket with the other hand. He seems to be looking around the room for something to throw them at.";
		otherwise:
			say "Fred is taking pebbles from his pocket, Dr. Law. He seems to be looking around the room for something to throw them at.";
		now the pebbles are carried by youngfred;
	otherwise:
		say "Fred is looking for something to throw his pebbles at.";

To say pebbleAction2:
	say "Fred offers the pebbles towards the speakers. He says, 'Ly ling syk?'[first time][lingsykdelivery][only]";
	
To say lingsykdelivery:
	deliver Ling;
	deliver Syk;

To say pebbleAction3:
	say "Fred throws the rocks in the air, then looks around for something else to do.";
	now the pebbles are nowhere;

To say DiabetesAction:
	say "Fred stops for a moment and rubs his temples.[one of][line break]He seems to be in pain, Dr. Law; the machine's malfunction, his mind has been forced back into its 5-year-old self. According to Ginger's computerized notes, this is a painful condition that will cause more and more damage to Fred. I suggest you return him to his proper age as soon as possible.[or][line break]His condition seems to be worsening, Dr. Law.[stopping]"
	
PermaSword is a number that varies. Permasword is 0.

To say swordaction:
	if PermaSword is 0:
		if Gym is not carried by the player:
			say "Fred sees the box and [one of]opens his eyes wide[or]smiles[stopping]. He opens it and grabs the sword,  bravely fending off imaginary pirates.";
			now the sword is carried by YoungFred;
			now the fried hair is nowhere;
		otherwise:
			say "Fred leaps on the bed, swinging his sword, before jumping down again.";
	otherwise:
		say "Fred puffs out his chest and fends off imaginary pirates with his sword.";
		now the fried hair is nowhere;

To say swordaction2:
	if PermaSword is 0:
		if Gym is not carried by the player:
			say "Fred stashes the sword back in the box and runs off.";
		otherwise:
			say "Fred tries to juggle the sword, but fails.";
		now youngFred is in Moirai Living Quarters;
		now the sword is in Moirai Living Quarters;
	otherwise: 
		say "Fred slashes with the sword, and attempts to bellow.";
		now youngFred is in Moirai Living Quarters;

To say ElectricAction:
	if the fork is not carried by YoungFred:
		say "Fred grabs a fork from the clutter and looks around the room for something to do with it.";
		now the fork is carried by YoungFred;
	otherwise:
		say "Fred is looking for something to do with the fork.";

To say ElectricAction2:
	say "Fred sticks the fork into the socket. The electricity fries his hair, and the fork flies away and is lost.

Fred plays by the pit.";
	now the fork is nowhere;
	now the fried hair is part of youngFred.

[To say HintTextFred:
	if Vyn is carried by the player:
		if Lydo is carried by the player:
			say "Fred waves at the speaker and motions across the room, ordering 'Vyn! Lydo!' When nothing happens, he motions more vigorously, saying 'Vyn! Vyn!'";
		otherwise:
			say "Fred breaks a plastic coin in half, giggling.";
	otherwise:
		say "Fred breaks a plastic coin in half, giggling.";]

Every turn when YoungFred is enclosed by MOIRAI Laboratory:
	if the player is in MOIRAI Laboratory:
		if YoungFred is on the rejuvenator:
			say "Fred waits [one of]anxiously on the rejuvenator[or]patiently on the rejuvenator[or]with a bored expression on the rejuvenator[or]on the rejuvenator, and begins to sing[or]on the rejuvenator, singing Go Syn Chyng quietly[or]on the rejuvenator, still singing[or]on the rejuvenator, his song finished[or]on the rejuvenator; he lays down[or]on the rejuvenator, napping[stopping].";
		otherwise:
			say "Fred looks at the laboratory in [one of]awe[or]fear[or]excitement[at random]."

The electrical socket is in MOIRAI Living Quarters. The electrical socket is scenery. The description of the electrical socket is "There is not much to distinguish one electrical socket from another, Dr. Law.";

The clutter is in MOIRAI Living Quarters. The clutter is scenery. Understand "fork" or "pebbles" or "items" as the clutter.

The description of the clutter is "There are a few items of clutter in the room. Mostly pirate-themed clutter.". Understand "pirate-themed" as the clutter.

The ceiling is a backdrop. The ceiling is everywhere. The description of the ceiling is "We look down from above, Dr. Law; the ceiling is beyond our notice.".

[Byngo is a quip. The preview of Byngo is "Lyhy byngo?"]

[the weird spacing is to deal with a spacing issue when delivering quips]

Speechtime is a number that varies. SpeechTime is 0.

ByngoLaly is a scene. ByngoLaly begins when SpeechTime is 1.

When ByngoLaly begins:
	say "Then he looks at the speakers, and searches around the room, and says, 'Laly Ly?'";
	deliver Laly;
	deliver ly;

Before speeching a quip to YoungFred for the first time:
	now speechtime is 1;

FredOver is a number that varies. FredOver is 0.

To finish Moirai:
	now YoungFred is nowhere;
	now HiddenNobody is nowhere;
	now the rejuvenator is nowhere;
	wait for any key;
	say "[line break]You hear some banging and cursing, and then Fred and Ginger come back.

They hug each other, and then they lift the rejuvenator on either side. Fred notices the burnt-out part of the machine  and fixes it, then they carry the rejuvenator out of the room.

Dr. Law, Project MOIRAI is now ready for the final judging ceremony.

*******************************************************[line break]Project MOIRAI Complete[line break]*******************************************************";	
	now FredOver is 1;
	further the finalization;
	say "[bold type]-Commands-[roman type][line break] hom - home folder[line break] hyd, med, moi - Observe Project HYDRA, MEDUSA, or MOIRAI[line break]";

[You need to do something else here!]

[After speeching Byngo to YoungFred:
	if YoungFred is on the Rejuvenator:
		say "Fred points to himself and says, 'Frederico!' The Rejuvenator activates, and flashes brightly. Fred screams in a voice that becomes more and more high pitched. He shrinks to become a baby, then disappears. A second later, a mummified corpse appears on the rejuvenator, then slowly de-ages until it opens its eyes, and stands up. A normal-aged Fred is now on the Rejuvenator.

He looks at himself and sighs, then walks out of the room.

Dr. Law, it seems you have discovered everything you can about this project.[paragraph break]

FIX THIS IMMEDIATELY!! NEED A DIFFERENT ENDING";
		finish Moirai;
	otherwise:
		say "Young Fred points at himself. You hear him says 'Frederico!' with a proud tone. He looks at the speaker and holds up a ball with a pleading face. 'Hwosyly tongh ngy?'";
		deliver Hwo;]


[Hwo is a quip. The preview of Hwo is "Hwosyly tongh ngy?"[do you want to play?]

After speeching Hwo to YoungFred:
	say "Fred claps his hand and spins on one foot, nodding. He waves the ball enticingly at the speakers, and makes a beckoning motion to the speaker, saying 'Vyn, vyn!'";
	deliver Vyn;]

[Rico is a quip. The preview of Rico is "Frederico!"

After speeching Rico to HiddenNobody:
	say "The rejuvenator lights up, and flashes brightly. But there is nothing on the platform."

After speeching Rico to YoungFred:
	if YoungFred is on the Rejuvenator:
		say "Fred points to himself and says, 'Frederico!' The Rejuvenator activates, and flashes brightly. Fred screams in a voice that becomes more and more high pitched. He shrinks to become a baby, then disappears.

 A second later, a mummified corpse appears on the rejuvenator, then slowly deages until it opens its eyes, and stands up. A normal-aged Fred is now on the Rejuvenator.

He looks at himself and sighs, then walks out of the room.

Dr. Law, it seems you have discovered everything you can about this project.[paragraph break]";
		finish Moirai;
	otherwise:
		say "Frederico looks around curiously."]

HiddenNobody is a person. "You can hear Fred scampering in the next room. [one of][or][or][or][or]He should be close enough to hear you from this room.[cycling]". The printed name of HiddenNobody is "nobody". 

FredSearching is a number that varies. FredSearching is 0.

A quip can be comehereish or not comehereish. A quip is usually not comehereish.

VynLydo is comehereish. VynWo is comehereish.

Before speeching something to HiddenNobody:
	if the noun is a quip:
		if the noun is not comehereish:
			say "The hidden speakers turn on, saying: '[preview of the noun]' You can hear Fred stop in the next room to listen, but he starts playing again." instead;

[After speeching Rico to HiddenNobody:
	say "There is a flash of brilliant light from the rejuvenator, but it has no effect."]
	
After speeching a comehereish quip to HiddenNobody:
	if the player is in Moirai Laboratory:
		if the sword is carried by YoungFred:
			say "Fred runs into the room, waving his sword and pointing it at the machines.";
			now HiddenNobody is in MOIRAI Living Quarters;
			now YoungFred is in Moirai Laboratory;
		otherwise:
			say "You can see Fred poke his head through the door, but he runs away when a machine makes a [one of]hissing[or]zapping[or]clanking[cycling] sound. Maybe he needs something to make him feel more courageous, Dr. Law.";
	otherwise:
		say "Fred runs into the room, looking around.";
		now HiddenNobody is in MOIRAI Laboratory;
		now YoungFred is in MOIRAI Living Quarters;

A quip can be confusing or not confusing. A quip is usually not confusing. Yuck is confusing. Come is confusing. Repeat is confusing. Love is confusing. 

After speeching a confusing quip to YoungFred:
	say "Young Fred laughs nervously and shakes his head."
	
The FredSpeakers are a backdrop. The FredSpeakers are in MOIRAI Laboratory and MOIRAI Living Quarters. Understand "hidden" or "speaker" or "speakers" as the FredSpeakers. The description of the FredSpeakers is "The speakers are discreetly tucked away, but one can certainly tell where they are, when they are turned on." The printed name of FredSpeakers is "speakers". The FredSpeakers are plural-named.

Part 3 - Singh and Spiel 's areas

Chapter 1 - Singh's area

Sphinx is an animal. The description of Sphinx is "A female cat.

-Height: 24 cm. 

-Approx. weight: 3.5 kg. 

-Approx. age: 3 years

-Clothing: none

-Personality: curious.

I know that you're obviously not a cat person, Dr. Law, given your history, but Sphinx is quite cute."

HYDRA Living Quarters is a room.  The description of the HYDRA Living Quarters is "This stone-floored and wood-walled room is lit by candles in niches. A piece of paper is taped to the wall.

One half of the room is filled with boxing trophies and memorabilia, and exercise equipment. The other half of the room is a filthy mess full of trash. A bunk bed lies between the two. Although the camera doesn't show them, I know that doors lead [bold type]north[roman type] and [bold type]south[roman type], Dr. Law."

The taped piece of paper is in HYDRA Living Quarters. The taped piece of paper is scenery. The description of the taped piece of paper is "It says:

[fixed letter spacing]Dr. Spiel,

I've had it up to HERE with you. Clean up your JUNK in the laboratory or you are OUT of this project.

I'm giving you ONE HOUR, and then I'm bringing a trash bag and I am DESTROYING and THROWING AWAY everything that you haven't cleaned up.

-Singh[variable letter spacing]

In a different ink, someone has drawn a smiley face with a big tongue and bushy hair, blowing a raspberry. Underneath, in cursive, are the words '[fixed letter spacing]Sure thing, Dr. S! Many hands make light work ;)'[variable letter spacing]".

The TrashMess is in HYDRA Living Quarters. The TrashMess is scenery. Understand "filthy" or "mass" or "trash" as the TrashMess. The printed name of the TrashMess is "filthy mess of trash". The description of the TrashMess is "It seems Dr. Spiel is as messy as ever, Dr. Law."

The bifurcated bunk bed is in HYDRA Living Quarters. The bifurcated bunk bed is scenery. The description of the bifurcated bunk bed is "The top half is very clean, and neatly tucked in tight. The bottom half is a mess, and filled with stuffed animals." Understand "stuffed" or "animals" or "mess" as the bifurcated bunk bed.

The stone floors are in HYDRA Living Quarters. The stone floors are scenery. Understand "floor" as the stone floors. The description of the stone floors is "I am not an expert in the art of stonecraft, Dr. Law. Words fail me.".

The wood walls are in HYDRA Living Quarters. The wood walls are scenery. Understand "wall" as the wood walls. The description of the wood walls is "Smooth wooden walls. The color is rich and deep, Dr. Law.".

The candles in niches are in HYDRA Living Quarters. The candles in niches are scenery. The description of the candles in niches is "This is cheaper than the lighting in the MOIRAI Lab, but with similar results".

The boxing trophies are in HYDRA Living Quarters. The boxing memorabilia are in HYDRA Living Quarters. 

The boxing trophies are scenery. The boxing memorabilia are scenery.

The description of the boxing trophies is "There are numerous trophies here, Dr. Law. It seems Dr. Singh has quite the reputation in boxing circles. Dr. Ng has never found the courage to speak to him." Understand "trophy" as the boxing trophies.

The description of the boxing memorabilia is "There are statues of Mohammad Ali, a rubber cast of Holyfield's ear, and a signed picture of Andrew Kooner.". Understand "statue" or "statues" or "Mohammad" or "ali" or "rubber" or "cast" or "holyfield" or "ear" or "signed" or "picture" or "andrew" or "kooner" or "holyfield's" as the boxing memorabilia.

The heavy red punching bag is in HYDRA Living Quarters. The heavy red punching bag is scenery. The description of the heavy red punching bag is "A heavy red punching bag, hanging from the ceiling by three chains." Understand "chain" or "chains" as the heavy red punching bag. Understand "exercise" or "equipment" as the heavy red punching bag.

The treadmill is in HYDRA Living Quarters. The treadmill is scenery. The description of the treadmill is "A once-sleek, but battered treadmill." Understand "exercise" or "equipment" as the treadmill.

The HYDRA doors are scenery. The HYDRA doors are in HYDRA Living Quarters. The description of the HYDRA doors is "I can't see them, Dr. Law. But the laboratory is to the north, and the lobby is to the south."

Doctor Singh is in HYDRA Living Quarters. Sphinx is in HYDRA Living Quarters. Understand "dr" as doctor singh.

Instead of speeching something to Doctor Singh:
	say "Dr. Singh is wearing headphones; he'll never hear you."

Every turn when the player is in HYDRA Living Quarters:
	if Doctor Singh is in HYDRA Living Quarters:
		say "[one of][TakeGame][or][DontEat][or][CatDown][cycling]";
	otherwise:
		say "Sphinx is napping on the bunkbed.";
	if Spiel clone is in HYDRA Laboratory:
		say "[line break][one of]You hear a wet thud and a scurrying sound from the [bold type]north[roman type]. Dr. Singh doesn't seem to notice.[or]You hear a [thud] and a [scurrying] from the [bold type]north[roman type].[stopping]";
	otherwise if SinghClone is in Hydra Laboratory:
		say "[line break][one of]You hear a smack from the room to the [bold type]north[roman type], followed by a whooshing sound.[or]There is another smack and another whooshing sound from the [bold type]north[roman type].[stopping]";

To say thud:
	say "[one of]thud[or]plop[or]smash[or]squish[or]slap[or]clop[then at random]";

To say scurrying:
	say "[one of]scurrying[or]rustling[or]clattering[or]rummaging[or]shifting[or]rifling[or]pillaging[then at random]";

Repeat is a quip. The preview of Repeat is "Let's try one more time! We can do it!"

To say TakeGame:
	say "Dr. Singh jumps up on the treadmill, and says, 'Let's try one more time, Sphinx! We can do it!' Sphinx jumps up, and they start running together.";
	if Repeat is not carried by the player:
		deliver Repeat;

Yuck is a quip. The preview of Yuck is "Don't eat that!";
	
To say DontEat:
	say "Sphinx gets off and tries to eat a dust bunny from Dr. Spiel's side of the room. Dr. Singh notices her, and stops running. He says, 'Don't eat that, Sphinx!'";
	if Yuck is not carried by the player:
		deliver Yuck;

Come is a quip. The preview of Come is "Come here right now!"

To say CatDown:
	say "Dr. Singh goes to the punching bag, and starts boxing. Sphinx climbs on top of the punching bag and starts scratching it. Dr. Singh backs up and says, 'Come here right now, Sphinx!'";
	if Come is not carried by the player:
		deliver Come;

Chapter 2 - The cloning machine

The Spiel clone is a woman. The Spiel clone is in HYDRA Laboratory. The Spiel Clone is scenery. The description of the Spiel clone is "A female human. 

-Height: 175 cm. 

-Approx. weight: 50 kg. 

-Approx. age: indeterminate. 

-Clothing: purple sunglasses, green scarf, orange jumpsuit, no shoes. 

-Personality: hearty.

-Toenails: misssing." Understand "scientist" or "lady" or "girl" as the spiel clone.

HYDRA Laboratory is north from HYDRA Living Quarters. The description of HYDRA Laboratory is "You see an enormous room, like the great hall of a castle, filled with a maze made of cardboard boxes, takeout containers, newspapers, broken furniture, and used wrapping paper. In the center is a large, cleared area. The clearing contains a gigantic ball of some metallic liquid, hovering in the air.[first time] It seems the cloning machine is finished.[only] You can return to the living quarters to the [bold type]south[roman type]."

The maze is in HYDRA Laboratory. The description of the maze is "A maze made of cardboard boxes, takeout containers, newspapers, broken furniture, and used wrapping paper." Understand "cardboard" or "box" or "boxes" or "takeout" or "container" or "containers" or "broken" or "furniture" or "used" or "wrapping" or "paper" as the maze. The maze is scenery.

The cloning machine is in HYDRA Laboratory. The cloning machine is scenery. Understand "metallic" or  "liquid" or "ball" or "sphere" or "large" or "cleared" or "gigantic" or "clearing" or "sphere" as the cloning machine. The description of the  cloning machine is "[if Spiel clone is in HYDRA Laboratory]The cloning machine is a gigantic ball of some metallic liquid.[otherwise if SinghClone is in HYDRA Laboratory] The cloning machine is dark, and spinning turbulently.[otherwise]The cloning machine is hovering silently.[end if]";

The mound of trash is in HYDRA Laboratory. The mound of trash is scenery. The description of the mound of trash is  "A mound composed of trash. I shudder to think what lies at the bottom, Dr. Law." Understand "bottom" as the mound of trash.

The remote is in HYDRA Laboratory.  "A remote control[if SinghEnabled is 1] is carried by the clone of Dr. Singh[otherwise] lies on the trash[end if][if Spiel Clone is in HYDRA Laboratory], near the hidden speakers[otherwise], missing many essential parts[end if]." The description of the remote is "A remote control lies on the trash. [if Spiel Clone is in HYDRA Laboratory]In fact, it is quite near the hidden speakers, Dr. Law.

Perhaps you could get Dr. Spiel to turn off the machine, Dr. Law.[otherwise]It seems to be missing essential parts, Dr. Law.[end if]". Understand "control" as the remote.

The HYDRASpeakers are in HYDRA Laboratory. Understand "hidden" or "speaker" or "speakers" as HYDRASpeakers. The printed name of HYDRASpeakers is "hidden speakers". The HYDRASpeakers are scenery. The description of the HYDRASpeakers is "They're hidden, Dr. Law[if the remote is in HYDRA Laboratory]; however, they are quite close to the remote.[otherwise].[end if]";

CloneCounter is a number that varies. CloneCounter is 4.

[if not on the mound, they are poisoned]
To say MoundText:
	say "[if CloneOnMound is 1]The clone drops the key and hops off the mound. [otherwise]The clone stumbles to its feet with its arms wrapped around its stomach. [end if]"

To say NervousText:
	say "[one of]The clone kicks trash into the air.[or]The clone entertains itself by yodeling at the top of its lungs.[or]The clone pops its knuckles.[or]The clone digs for something shiny in the trash.[or]The clone giggles at itself.[at random]"

To say IdleText:
	say "[if ClonePoisoned is 1]The clone is rolling on the ground in pain.[otherwise if CloneOnMound is 0]The clone continues to eat the rotten food.[otherwise][NervousText][end if]";

NextFood is a number that varies. NextFood is 1.

ClonePoisoned is a number that varies. ClonePoisoned is 0.

DeathCounter is a number that varies. DeathCounter is 1.

Every turn when the player is in HYDRA Laboratory:
	if Spiel Clone is in HYDRA Laboratory:
		if CloneCounter is less than 4:
			now CloneCounter is CloneCounter plus one;
			if CloneCounter is 3:
				if CloneOnMound is 0:
					say "Dr. Spiel finishes the meal and immediately falls to the ground, groaning in pain and massaging her stomach.";
					now ClonePoisoned is 1;
				otherwise:
					say NervousText;
			otherwise:
				say "[IdleText]";
		otherwise:
			now CloneCounter is one;
			now DeathCounter is DeathCounter plus one;
			now CloneOnMound is 0;
			now ClonePoisoned is 0;
			now CloneNeedsMotivation is 0;
			now NextFood is NextFood plus one;
			say "[one of][or][MoundText] 

It plunges deeper into the maze and is lost from view.[paragraph break][stopping]The surface of the ball begins to bubble and smoke. A clone of Dr. Spiel spills out. You hear a wet thud. [one of][or]The clone could stop the machine if they had the remote, Dr. Law. [stopping][first time][paragraph break]Oh no, Dr. Law; Dr. Spiel seems to have had an accident with the cloning machine again. The cloning machine is set on 'repeat'. The actions of the clones indicate that it is set to a low resolution--these are poor quality clones, Dr. Law. You'd better stop this. [only][paragraph break]The clone sees [grossfood] in the trash, and dives into the clutter.";

FoodList is a list of text that varies. FoodList is {"a fragment of pizza", "a plate of mac and cheese", "a rotten apple","some slimy pudding","pickled herring","fresh escargot","headcheese","jalapeno ice cream","used chewing gum"}

To say grossfood:
	if NextFood < 10:
		say entry NextFood of FoodList;
	otherwise:
		say "leftovers";

Before speeching something to Spiel clone:
	if ClonePoisoned is 1:
		say "The clone is in too much pain from the rotten food to respond, Dr. Law." instead;

CloneOnMound is a number that varies. CloneOnMound is 0.

After speeching Yuck to Spiel clone:
	if CloneCounter < 3:
		say "The clone starts up, alert, dropping what it's holding ([grossfood]). It says, 'Dr. Singh, is that you? I'm cleaning, I promise!' It looks around, uncertain.";
		now CloneOnMound is 1;
	otherwise:
		say "The clone looks puzzled. It says, 'Don't eat what?'"
		
CloneNeedsMotivation is a number that varies. CloneNeedsMotivation is 0.

After speeching Come to Spiel clone:
	if CloneOnMound is 1:
		if CloneNeedsMotivation is 0:
			say "The clone looks toward the hidden speakers. It tries to climb toward the trash under the speakers, but gets scared. It says, 'I don't think I can do it.'";
			now CloneNeedsMotivation is 1;
		otherwise:
			say "The clone says, 'I already heard you the first time! I don't think I can do it.'";
	otherwise:
		[this message should never occur unless they're eating]
		say "The clone ignores you completely, Dr. Law. It seems to be focused on eating.";

SinghClone is a person. "Dr. Singh's clone is staring at itself in panic." The description of SinghClone is "A perfect clone of Dr. Singh. Except missing its toenails." Understand "dr" or "doctor" or "Singh" or "clone" as SinghClone. The printed name of SinghClone is "Dr. Singh's clone". 

SinghClonePC is a person. The description of SinghClonePC is "A male human. 

-Height: 180 cm. 

-Approx. weight: 90 kg. 

-Approx. age: 30. 

-Clothing: navy suit, red tie, grey handkerchief. Hairbun, rolled beard. 

-Personality: disciplined.

-Toenails: missing." Understand "Singh" or "doctor" or "dr" or "clone" as SinghClonePC. The printed name of SinghClonePC is "Dr. Singh's clone". 

Persuasion rule for asking SinghClonePC to try doing something: say "You can control the clone directly, Dr. Law. Press enter for a list of commands.".

After speeching Repeat to Spiel clone:
	if CloneNeedsMotivation is 1:
		say "The clone says, 'You're right! I can do it!' The clone leaps up towards the speakers. It finally sees the remote.

'Oh, so that's where that was!' says the clone. It hops down, and runs towards the machine. It points the remote, and presses the off button, but slips on some loose trash and drops the remote. The remote hits the ground with a bang, and pieces go flying everywhere with an electric sizzle.

The clone says, 'Uh oh.' The cloning machine turns dark and begins to rotate in a turbulent way. The Spiel clone runs away in fear, and is lost in the maze.";
		now Spiel clone is nowhere;
	otherwise if CloneOnMound is 0:
		[this should only fire if the clone is still eating]
		say "The clone is too busy eating to pay attention.";
	otherwise:
		say "The clone looks puzzled. It says, 'Do what again?'";

SinghCounter is a number that varies. SinghCounter is 0.

SinghEntrance is a scene. SinghEntrance begins when Spiel clone is nowhere. SinghEntrance ends when SinghCounter is 2.

Every turn during SinghEntrance:
	now SinghCounter is SinghCounter plus one;

When SinghEntrance ends:
	now Doctor Singh is nowhere;
	now SinghClone is in HYDRA Laboratory;
	say "Dr. Singh walks in, looking concerned. He finds the remote on the ground, smoking, and picks it up. He looks at the sphere in alarm, and touches it with a single fingertip. An alarm begins to sound. Dr. Singh looks at his arm, which turns grey.

Before he can speak, he crumbles into dust.

Dr. Law, the cloning machine seems to have been set to the lowest quality possible. I am concerned about this situation. The night crew will have a fit cleaning up the mess.

I cannot control the machine directly, but it accesses some schematics in my database each time it copies someone. I can edit an implant into the schematics that will allow you to control the clone. The additional power should keep it from decaying for a while. 

You need to fix and use the remote to turn off the clone machine. With all this interference, you need a signal strength of [special-style-2]at least 99 jigawatts[roman type]! There should be some spare parts lying around that can boost the remote's power.

A clone of Dr. Singh, holding a broken remote, emerges from the sphere and lands on his feet. [SinghSong][paragraph break]";

Chapter 3 - Singh's version of cloning machine puzzle

Every turn when SinghClone is in HYDRA Laboratory:
	if the player is in HYDRA Laboratory:
		say "[one of]History repeats itself, Dr. Law. The Singh clone looks curiously at the sphere and touches it. Dr. Singh looks at his arm, which is turning grey and crumbling into ash. Before he can speak, he crumbles into dust. [or]History repeats itself, Dr. Law. The Singh clone looks curiously at the sphere and touches it.. Again. Dr. Singh looks at his arm, which is turning grey and crumbling into ash. Before he can speak, he crumbles into dust. Again. [stopping][paragraph break]A clone of Dr. Singh emerges from the sphere, holding a broken remote, and lands on his feet[one of][or]. Again[stopping].";
		
The ash is in HYDRA Laboratory. The ash is scenery. The description of the ash is "A pile of ash of questionable origin." Understand "dust" or "pile" as the ash.

[he touches it, alarm sounds, he is vaporized, each clone tries to stop it before dying]
		
SinghEnabled is a number that varies. SinghEnabled is 0.

To say SinghSong:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 clo - Control Singh's clone";
	now SinghEnabled is 1;
	
Singhing is an action applying to nothing. Understand "clo" as singhing.

[These commands are listed twice, be careful!]

Carry out singhing:
	if SinghEnabled is 1:
		say "*********************************************[line break]Initiating audiovisual link...[line  break]*********************************************[line break]";
		now the player is SinghClonePC;
		try looking;
		say "[bold type]-Commands-[roman type][line break]
   n,e,s,w - movement[line break]
   take- pick up an object[line break]
   rem- attempt to use remote[line break]
    dis- disconnect from the clone[paragraph break]";
	otherwise if SinghEnabled is 2:
		say "Remote access to personnel is restricted to life and death situations.";
	otherwise:
		say "Computing...Interpreting 'CLO' as 'CLOG'. I am not proficient at that form of dance, Dr. Law.";[Put snarky text here?]

Chapter 4 - SinghControl

A thing has a number called SinghPoints. The SinghPoints of something is usually 0.

A thing has some text called the SinghText.

RemotePower is a number that varies. RemotePower is 0.

[We want a clone to add up to SinghPoints of 100]

[should this all be in Singh's voice?]

[Commands should include movement, taking, dropping, and remoting]

UnSinghing is an action applying to nothing. Understand "dis" as UnSinghing.

Carry out UnSinghing:
	if the player is SinghClonePC:
		reset the clone;
		now the player is Hacker;
	otherwise:
		say "Nothing to disconnect from."

SinghDecay is a number that varies. SinghDecay is 0.

Every turn when the player is SinghClonePC:
	say "[SinghStatusText]";
	now SinghDecay is SinghDecay plus one;

The fingertips are part of SinghClonePC. The toes are part of SinghClonePC. The ears are part of SinghClonePC. Understand "left" or "right" or "ear" as the ears.

The description of the fingertips is "[if SinghDecay < 2]Dr. Singh's fingertips are in a healthy condition.[otherwise]Dr. Singh's fingertips are damaged.[end if]".

The description of the toes is "[if SinghDecay < 6]Dr. Singh's toes are in a healthy condition.[otherwise]Dr. Singh's toes have fallen off.[end if]"

The description of the ears is "One ear has decayed and fallen into ash."

To say SinghStatusText:
	if SinghDecay is 0:
		say "Dr. Singh's [one of]left[or]right[at random] ear is turning to ash[first time]. You don't have much time, Dr. Law; grab as much as you can to boost the signal, then turn on the remote[only].";
	otherwise if SinghDecay is 2:
		say "Dr. Singh's fingertips are turning gray.";
	otherwise if SinghDecay is 4:
		say "Dr. Singh's vision is getting blurry.";
	otherwise if SinghDecay is 6:
		say "Dr. Singh's toes turn to ash and fall off.";
	otherwise if SinghDecay is 9:
		say "Dr. Singh stumbles. This clone can't last much longer!";
	otherwise if SinghDecay is 11:
		say "I'm sorry, Dr. Law; you didn't have enough time to use the remote. Fortunately, there should be another clone coming soon.[paragraph break]";
		destroy the clone;
	otherwise:
		say "[one of]Dr. Singh is breathing heavily.[or]Dr. Singh wipes sweat from his eyes.[or]Dr. Singh swallows nervously.[at random]";
		
To destroy the clone:		
	say  "Dr. Singh's body collapses into a pile of ash, including the remote. 

A new clone falls out of the device with a thud, holding a broken remote.

******************************************[line break]Taking control of the new clone...[line break]******************************************[paragraph break]";
	wait for any key;
	reset the clone;

To reset the clone:
	now DeathCounter is DeathCounter plus one;
	if SinghClonePC is not in SinghCenter:
		now SinghClonePC is in SinghCenter;
	now giant antenna is nowhere;
	now cell phone battery is nowhere;
	now quartz crystal is nowhere;
	now parabolic mirror is nowhere;
	now the miniature quartz city is available;
	now the rock garden of parabolic mirrors is available;
	now chewing gum wrapper is nowhere;
	now lone paperclip is nowhere;
	now large wooden stick is nowhere;
	now the piece of rock candy is nowhere;
	now the single lens is nowhere;
	now the red cube is nowhere;
	now RemotePower is 0;
	now SinghDecay is 0;
	try looking;
	say "Dr. Law, the last clone could not stop the device because the remote needed more power. You must increase the remote's power to [special-style-2]at least 99 Jigawatts[roman type]; this last time, you only reached [special-style-2][RemotePower] Jigawatts[roman type]. Perhaps you should gather different items.";
	
Section 1 - SinghCenter

SinghClonePC is in SinghCenter.

A SinghRemote is carried by SinghClonePC. The printed name of SinghRemote is "remote". Understand "remote" as SinghRemote. The description of SinghRemote is "The remote is covered with resonance chambers, battery slots, and various grooves and threaded sockets." Understand "resonance" or "chamber" or "groove" or "battery" or "slot" or "threaded" or "socket" or "sockets" or "grooves" as SinghRemote.

The SinghRemote can be resoed or unresoed. The SinghRemote can be mirrored or unmirrored. The SinghRemote can be batteried or not batteried. The SinghRemote can be socketed or unsocketed.

Before dropping SinghRemote:
	say "I'm not sure that's such a good idea, Dr. Law." instead;

SinghCenter is a room. The printed name of SinghCenter is "HYDRA Laboratory (as Dr. Singh)".

The description of SinghCenter is "Dr. Singh is standing in a large clearing in the center of a maze of trash. The clone machine hovers above in silence. Dr. Singh can hear a murmur of voices from the maze, with occasional laughs, screams, and yells from Dr. Spiel's clones. Dr. Singh can also hear his own heavy breathing. Dr. Singh can move to the [bold type]north, south, east[roman type] or [bold type]west[roman type]."

The lens mound is in SinghCenter. "A mound of lenses is piled up here." The description of the lens mound is "There must be a lot of empty eyeglass frames somewhere out there, Dr. Law." Understand "lens" or "lenses" or "pile" as the lens mound.

The single lens is a thing. The description of the single lens is "A single lens. Its usefulness in boosting the signal is questionable." The SinghPoints of the single lens is 5. The SinghText of the single lens is "Dr. Singh screws on the lens to the end of the remote. It focuses the signal by a miniscule amount. Power levels increasing by [special-style-2]5 jigawatts.[roman type]";

Before taking the lens mound:
	if the single lens is carried by the player:
		say "Dr. Singh already has a lens." instead;
	otherwise:
		say "Dr. Singh fishes out a lens.

Taken.";
		pointtake the single lens instead;


The murmur of voices is in SinghCenter. The murmur of voices is scenery. The description of the murmur of voices is "There is a murmur of voices from the other clones, but they seem far away." Understand "clones" or "spiel" or "dr" or "laughs" or "screams" or "yells" as the murmur of voices.

Understand "breathing" as SinghClonePC.

Instead of listening to the murmur of voices:
	try examining the murmur of voices;
	
Instead of listening to SinghClonePC:
	say "Dr. Singh hears his own heavy breathing.";

SinghArea is a region. SinghCenter is in SinghArea. SinghNorth is in SinghArea. SinghSouth is in SinghArea. SinghEast is in SinghArea. SinghWest is in SinghArea.

Instead of listening to a room in SinghArea:
	say "Dr. Singh hears his own heavy breathing, and the distant sounds of Dr. Spiel's clones."

The SinghMaze is a backdrop. Understand "maze" as the SinghMaze. The printed name of SinghMaze is "maze". The SinghMaze is in SInghCenter, SinghNorth, SinghEast, SinghSouth, and SinghWest. The description of SinghMaze is "The maze is intricate and frightening, Dr. Law. Best to stick near the center."

Instead of entering the maze:
	if the player is in SinghCenter:
		say "Do you want Dr. Singh to go north, south, east, or west?";
	otherwise:
		say "There should be enough nearby to help you, without entering the maze.";

RemotOning is an action applying to nothing. Understand "rem" as remotoning.

A thing can be pointsknown or not pointsknown.  A thing is usually not pointsknown.

Carry out RemotOning:
	now RemotePower is 0;
	repeat with current running through physical things carried by the player:
		if current is not SinghRemote:
			now RemotePower is RemotePower plus the SinghPoints of current;
			now current is pointsknown;
	if the player is SinghClonePC:
		if the player is not in SinghCenter:
			say "Dr. Singh stumbles back to the center with the last of his strength.";
			now the player is in SinghCenter;
		if the number of physical things carried by the player < 2:
			say "Nothing happens! Dr. Law, you need to boost the signal to at least [special-style-2]99 jigawatts[roman type]. Have Dr. Singh gather items that could boost the signal.";
		otherwise:
			say "Dr. Singh is preparing the remote:";
			if the giant antenna is carried by the player:
				say "[paragraph break]    -Dr. Singh screws the giant antenna in first. The giant antenna strengthens the signal for a moment. However, it shudders and, finally, snaps off, sending the remote flying. All of the pieces are lost in the maze. Dr. Law, that antenna is much too large. Power levels increasing by...[special-style-2]0 jigawatts[roman type].[paragraph break]";
				destroy the clone;
			otherwise:
				repeat with current running through things carried by the player:
					say "[paragraph break]    -";
					say Singhtext of current;
				say "The remote has reached a final power level of [special-style-2][RemotePower] Jigawatts[roman type].

Dr. Singh aims the remote at the machine and presses the button. [remotetext]";
	otherwise:
		say "Processing input... Understand REM as Rapid-Eye-Movement... I am not currently capable of dreaming, Dr. Law.";


To say remotetext:
	if RemotePower is at least 99:
		say "Dr. Law, the clone machine is shutting down! You did it! ";
		now the player is Hacker;
		say "[paragraph break]You hear a chorus of cries from the stacks as Dr. Spiel's clone army crumbles to ash.[paragraph break]";
		wait for any key;
		say "The latest clone of Dr. Singh wipes his brow. He gingerly touches the cloning device, but it doesn't respond. Smiling, he pushes the clone machine out of the room. I believe he's headed for the command center, Dr. Law. Testing is complete on the clone machine.

You have done everything you can do in the HYDRA Lab, Dr. Law. Returning you to your last known location...";
		now SinghEnabled is 2;
		now SinghClone is nowhere;
		wait for any key;
		try looking;
		say "*******************************************************[line break]Project HYDRA Complete[line break]*******************************************************";
		further the finalization;
		say "[bold type]-Commands-[roman type][line break] hom - home folder[line break] hyd, med, moi - Observe Project HYDRA, MEDUSA, or MOIRAI[line break]";
	otherwise:
		say "The remote is trembling and flashing, but the signal is too weak. We need a final power level of at least [special-style-2]99 Jigawatts[roman type], Dr. Law. I'm sorry.[paragraph break]";
		destroy the clone;
	now RemotePower is 0;
		
To PointTake (current - an object):
	now current is carried by the player;
	if current is pointsknown:
		say "[line break]From experience, Dr. Law, this should increase the remote's power by [special-style-2][SinghPoints of current] jigawatts[roman type][one of]. We need to reach [special-style-2]99[roman type] jigawatts to turn off the machine![or].[or].[cycling]";

Section 2 - SinghNorth

SinghNorth is north from Singhcenter. The printed name of SinghNorth is "Electronics Pile (as Dr. Singh)".

The description of SinghNorth is "This section of the maze is filled with smashed-in monitors, old VGA chips, outdated smart phones, and cables which, I can tell you, Dr. Law, absolutely do not fit any of the electronics here. Everything is piled on metal shelving that is spaced a little too wide, so that the spare parts have spilled down to lower levels. Dr. Singh can return to the [bold type]south[roman type].

Only two items seem to be of any use.";

The electronics junk is in SinghNorth. Understand "smashed" or "monitors" or "smashed-in" or "old" or "VGA" or "chips" or "outdated" or "smart" or "phones" or "phone" or "cable" or "cables" or "electronics" or "shelves" or "spare" or "metal" or "shelving" as the electronics junk. The electronics junk is scenery. The description of the electronics junk is "This is junk, Dr. Law. Dr. Singh has no need of it.".

The antennaPull is a number that varies. The antennaPull is 0.

The nest of antennas is in SinghNorth. "A pile of antennas has been artfully arranged into a nest here. Dr. Singh may be able to grab one. [if antennaPull > 0]One antenna has been pulled partially out.[end if]". The description of the nest of antennas is "[if antennaPull is 0]One of the antennas looks like you can pull it out.[otherwise if antennaPull is 1]One antenna has been pulled partway out.[otherwise if antennaPull is 2]One antenna has been pulled halfway out.[otherwise if antennaPull is 3]One antenna has been pulled out almost completely." Understand "antenna" or "antennae" as the nest of antennas.

Before pulling the nest of antennas:
	try taking the nest of antennas instead;

Before taking the nest of antennas:
	if the giant antenna is carried by the player:
		say "Dr. Singh already has a giant radio antenna, Dr. Law." instead;
	now antennaPull is antennaPull plus one;
	if antennaPull is 1:
		say "Dr. Singh tugs on the antenna, muscles bulging, but it only slides out a foot or two." instead; 
	if antennaPull is 2:
		say "Dr. Singh pulls again, and the antenna slides halfway out. He wipes the sweat off his head." instead;
	if antennaPull is 3:
		say "Dr. Singh heaves, and the whole nest shifts, letting the antenna come almost all the way out." instead;
	if antennaPull is 4:
		say "Dr. Singh grabs the antenna with both hands, and gives a mighty pull. The antenna comes out completely.

Taken.";
		PointTake the giant antenna;
		now antennaPull is 0 instead;

Report examining something:
	if the player is SinghClonePC:
		if the noun is pointsknown:
			say "From past experience, this should increase your signal by [special-style-2][Singhpoints of the noun] jigawatts[roman type]."

The giant antenna is a thing. "This is a giant radio antenna, Dr. Law." The description of the giant antenna is "This is a giant radio antenna, Dr. Law. Its usefulness is dubious." Understand "radio" as the giant antenna. The SinghPoints of the giant antenna is 0. The SinghText of the giant antenna is "The giant antenna strengthens the signal for a moment. However, it shudders and, finally, snaps off. It is much too large. Power levels increasing by...[special-style-2]0 jigawatts[roman type]." 
 
The battery dominoes are in SinghNorth. The battery dominoes can be toppled or untoppled. The battery dominoes are untoppled. "Thousands of batteries lined up like dominoes are here. [if the battery dominoes are toppled]Well, they were lined up, Dr. Law, until you interfered.[end if]".  The description of the battery dominoes is "Dr. Spiel must have spent weeks on this. Or a hundred clones must have spent an hour on this. [if the battery dominoes are toppled]It's a shame that one of them wasn't here to see this.[end if]". Understand "domino" or "batteries" as battery dominoes.

Before taking the battery dominoes:
	if the cell phone battery is carried by the player:
		say "Dr. Singh already has a battery, Dr. Law." instead;
	otherwise if the battery dominoes are toppled:
		say "Dr. Singh grabs a battery from the pile.";
		pointtake the cell phone battery instead;
	otherwise if SinghDecay < 6:
		say "Dr. Singh carefully leans down and grabs a cell phone battery.

Taken.";
		pointtake the cell phone battery instead;
	otherwise:
		say "Dr. Singh carefully leans down and grabs a cell phone battery. His feet become unbalanced, and he knocks over the dominoes in an enormous cascade. A wail of anguish comes from beyond the stacks as the Spiel clones mourn their loss.

Taken.";
		pointtake the cell phone battery;
		now the battery dominoes are toppled instead;
		
The cascade is a scene.

DominoCounter is a number that varies. DominoCounter is 0.

The cascade begins when the battery dominoes are toppled. The cascade ends when DominoCounter is 4.

SinghTotal is a region. SinghArea is in SinghTotal. HYDRA Laboratory is in SinghTotal.

HYDRAArea is a region. SinghTotal is in HYDRAArea. HYDRA Living Quarters is in HYDRAArea. HYDRA Lobby is in HYDRAArea.

Instead of HYDRAing when the player is in HYDRAArea:
	say "You are already viewing Project HYDRA, Dr. Law.";

Every turn during the cascade:
	if the player is in SinghTotal:
		if DominoCounter is 1:
			say "[if the player is SinghClonePC]Dr. Singh hears[otherwise]You hear[end if] the dominoes falling throughout the maze.";
		if DominoCounter is 2:
			say "The sound of the dominoes falling down is almost deafening. It sounds like the Spiel clones are cheering.";
		if DominoCounter is 3:
			say "The dominoes reach a crescendo, then fade away. Perhaps you should be more careful next time, Dr. Law.";
	now DominoCounter is DominoCounter plus one;

The cell phone battery is a thing. Understand "couple" or "batteries" as cell phone battery. "A couple of phone batteries are visible." The description of the cell phone battery is "I believe we took these from those unfortunate exploding phones a few years back, Dr. Law.". The SinghPoints of the cell phone battery is 30. The SinghText of the cell phone battery is "Dr. Singh opens a slot in the remote, revealing a black, crusted battery; whoever used the remote last must have fried it. He removes it, and inserts the new battery. Power levels increasing by [special-style-2]30 jigawatts[roman type].".[make sure the new battery gets crusty in the next iteration]

Section 3 - SinghEast

SinghEast is east from SinghCenter. The printed name of SinghEast is "Car Parts (as Dr. Singh)".

The description of SinghEast is "Dr. Singh is surrounded by car parts organized in bizarre sculptures. Car frames have been welded into giant dog statues. A small greenhouse has been constructed from windshields, with fake plants made from sideview mirrors. The floor is covered with random detritus up to Dr. Singh's ankles. Dr. Singh can return to the [bold type]west[roman type].

Almost everything here is bolted or welded down."

The car junk is in SinghEast. The car junk is scenery. Understand "car" or "part" or "parts" or "bizarre" or "sculpture" or "sculptures" or "frame" or "frames" or "giant" or "dogs" or "statue" or "statues" or "small" or "greenhouse" or "windshield" or "windshields" or "fake" or "plant" or "plants" or "sideview" as the car junk. The description of the car junk is "None of this is worth searching through, Dr. Law, except perhaps the mobile."

The delicate mobile is in SinghEast. "A delicately balanced mobile is here with two pans on either side. One side contains a miniature city made of quartz crystals, [if the miniature quartz city is unavailable]not [otherwise if the quartz crystal is carried by the player]not [end if]ripe for the taking. 

The other side contains a sort of rock garden made of parabolic headlight mirrors. [if the number of available things is 1]It is[otherwise]Also[end if] [if the rock garden of parabolic mirrors is unavailable]not [otherwise if the parabolic mirror is carried by the player]not [end if]ripe for the taking.

The crystals and mirrors bobble up and down." Understand "pan" or "pans" as the delicate mobile. The description of the delicate mobile is "A marvel of engineering, Dr. Law, with quartz crystals on one side and parabolic mirrors on the other."

The miniature quartz city is part of the delicate mobile. Understand "crystals" or "crystal" as the miniature quartz city. The description of the miniature quartz city is "A pile of quartz crystals, arranged as a city. [if the miniature quartz city is unavailable]They are still bobbing beyond your reach, and will be for some time.[end if]"

The rock garden of parabolic mirrors is part of the delicate mobile. The description of the rock garden of parabolic mirrors is "Artfully arranged, Dr. Law. Doctor Spiel is nothing if not an artist. [if the rock garden of parabolic mirrors is unavailable]They are still bobbing beyond your reach, and will be for some time.[end if]". Understand "mirror" as the rock garden of parabolic mirrors.

The miniature quartz city can be available or unavailable. The miniature quartz city is available.

The  rock garden of parabolic mirrors can be available or unavailable. The rock garden of parabolic mirrors is available.

Before taking the delicate mobile:
	say "The mobile itself is, ironically, immobile." instead;
	
Before taking the miniature quartz city:
	if the quartz crystal is carried by the player:
		say "Dr. Singh already has a quartz crystal, Dr. Law." instead;
	otherwise if the miniature quartz city is available:
		say "Dr. Singh dextrously abstracts a quartz crystal. However, the imbalance sets the pans in motion. 

I'm afraid the parabolic mirrors are beyond your reach, now, at least until they settle down.

Taken.";
		now the rock garden of parabolic mirrors is unavailable;
		pointtake the quartz crystal instead;
	otherwise:
		say "The quartz city is temporarily beyond the reach of Dr. Singh, it seems." instead;

The quartz crystal is nowhere. "A quartz crystal is under the pile of trash." The description of the quartz crystal is "It looks flawless, Dr. Law." The SinghPoints of the quartz crystal is 20. The SinghText of the quartz crystal is "Dr. Singh pushes the quartz crystal into a resonance chamber. The remote begins to hum and jitter. Power levels increasing by [special-style-2]20 jigawatts[roman type]."
	
Before taking the rock garden of parabolic mirrors:
	if the parabolic mirror is carried by the player:
		say "Dr. Singh already has a parabolic mirror, Dr. Law." instead;
	otherwise if the rock garden of parabolic mirrors is available:
		say "Dr. Singh snatches a parabolic mirror. However, the imbalance sets the pans in motion.

I'm afraid the quartz crystals are beyond your reach, now, at least until they settle down.

Taken.";
		now the miniature quartz city is unavailable;
		pointtake the parabolic mirror instead;
	otherwise:
		say "The rock garden of mirrors is temporarily beyond the reach of Dr. Singh, it seems." instead;

The parabolic mirror is nowhere. "A parabolic mirror from an automobile lies on the floor." The description of the parabolic mirror is "Technically, Dr. Law, it's a paraboloid mirror". The SinghPoints of the parabolic mirror is 30. The SinghText of the parabolic mirror is "Dr. Singh slides the mirror into a groove on the remote. A row of indicators begin to light up. Power levels increasing by [special-style-2]30 jigawatts[roman type]." Understand "paraboloid" as parabolic mirror.

Section 4 - SinghWest

SinghWest is west from SinghCenter. The printed name of SinghWest is "Trash Stacks (as Dr. Singh)".

The description of SinghWest is "Dr. Singh is wading through piles of trash. He can hear a variety of crunches and pops from plastic cup lids, styrofoam peanuts, and bubble wrap underfoot. Dr. Singh's adrenaline levels are very high due to the occasional soft organic matter underneath. Dr. Singh can return to the [bold type]east[roman type].

Only two items stand out."

The floor trash is in SinghWest. Understand "piles" or "pile" or "trash" or "plastic" or "cup" or "lid" or "lids" or "crunch" or "pop" or "crunches" or "pops" or "styrofoam" or "peanut" or "peanuts" or "bubble" or "wrap" or "sof" or "organic" or "matter" as the floor trash. The description of the floor trash is "Dr. Law, the trash on the floor is of no use to Dr. Singh." The floor trash is scenery.

Instead of listening to the floor trash:
	try examining the floor trash.

The wrapper disco ball is in SinghWest. "There is a large, spinning disco ball here, Dr. Law. It seems to be constructed of chewing gum wrappers.". The description of the wrapper disco ball is "A large disco ball made of chewing gum wrappers.".

[Rule for deciding whether all includes the chewing gum wrapper when taking:
	if the wrapper disco ball is enclosed by the location of the player:
		if the chewing gum wrapper is carried by the player:
			no;
		otherwise:
			yes;
	otherwise:
		yes;]

The chewing gum wrapper is nowhere. The SinghPoints of the chewing gum wrapper is 40. The description of the chewing gum wrapper is "This has marvelous reflective properties, Dr. Law." The SinghText of the chewing gum wrapper is "Dr. Singh smooths out the wrapper and carefully forms a miniature antenna dish. He settles it in a groove around the active end of the remote. Power levels increasing by [special-style-2]40 jigawatts[roman type]."

Before taking the wrapper disco ball:
	if the chewing gum wrapper is carried by the player:
		say "Dr. Singh has already peeled off a chewing gum wrapper, Dr. Law." instead;
	otherwise if the large wooden stick is carried by the player:
		say "Dr. Singh reaches up to the ball with the stick. Part of the gum from a wrapper sticks on to the pole. Dr. Singh peels off the wrapper.

Taken.";
		pointtake the chewing gum wrapper instead;
	otherwise:
		say "Dr. Singh can't reach the disco ball, Dr. Law. He could touch it with a 10 ft. pole, though." instead;

The rock candy boulder is in SinghWest. "A massive boulder of rock candy is here." The description of the rock candy boulder is "This is the largest rock candy boulder I've ever seen, Dr. Law."

The piece of rock candy is a thing. The description of the piece of rock candy is "A small piece of rock candy. Dr. Ng's favorite, although I doubt he'd ever come here." The SinghPoints of the piece of rock candy is 5. The SinghText of the piece of rock candy is "Dr. Singh pushes the rock candy into a resonance chamber. It does very little. Power levels increasing by [special-style-2]5 jigawatts.[roman type]";

Before taking the rock candy boulder:
	if the piece of rock candy is carried by the player:
		say "Dr. Singh already has some rock candy." instead;
	otherwise:
		say "Dr. Singh knocks off a piece of rock candy.

Taken.";
		pointtake the piece of rock candy instead;

Section 5 - SinghSouth

SinghSouth is south from SinghCenter. The printed name of SinghSouth is "Office Supplies (as Dr. Singh)".

The description of SinghSouth is "This portion of the maze is almost completely bare. Row after row of cabinets, boxes, and shelves are completely cleaned off. [first time]At the end of the row, Dr. Singh sees a Spiel clone grabbing the last stapler and stuffing it in his pocket, but the clone runs when they see him.[only] From somewhere nearby, there is a murmur of many multitudes; the clones must be nearby. Dr. Singh can return to the [bold type]north[roman type]."

The office stuff is in SinghSouth. The office stuff is scenery. Understand "cabinet" or "cabinets" or "box" or "boxes" or "shelf" or "shelves" as the office stuff. The description of the office stuff is "This area contains mostly large, empty pieces of office furniture, which are of no use to you."

Instead of opening the office stuff:
	try examining the office stuff.

The murmur of many multitudes is in SinghSouth. The murmur of many multitudes is scenery. Understand "spiel" or "clone" or "clones" as the murmur of many multitudes. The description of the murmur of many multitudes is "The clones are nearby, but you cannot see anything right now. You can only hear their voices, Dr. Law."

Instead of listening to the murmur of many multitudes:
	try examining the murmur of many multitudes.

The box of paperclips is in SinghSouth. "A box of paperclips is on the floor, Dr. Law." The description of the box of paperclips is "This is an ordinary, plain, boring box of paperclips." Understand "ordinary" or "plain" or "boring" as the box of paperclips.

Before taking the box of paperclips:
	if the lone paperclip is carried by the player:
		say "Dr. Singh alreadys has a paperclip. Dr. Law." instead;
	otherwise:
		say "Dr. Singh extracts a lone paperclip from the box.

Taken.";
		pointtake the lone paperclip instead;

The  lone paperclip is nowhere. "A lone paperclip draws your eye."[what to do with the boxes]The SinghPoints of the lone paperclip is 35. The SinghText of the lone paperclip is "Dr. Singh unfolds the paperclip and shoves it into a threaded socket. It thrums and begins to glow. Power levels increasing by [special-style-2]35 jigawatts[roman type]." The description of the lone paperclip is "It seems to be the exact thickness to fit in the remote's socket." Understand "clip" as the lone paperclip.

The pile of sticks is in SinghSouth. "A pile of smooth wooden sticks lies on the floor." The description of the pile of sticks is "There's not much you can say about a pile of 10-ft. sticks, Dr. Law." Understand "stick" as the pile of sticks.

Before taking the pile of sticks:
	if the large wooden stick is carried by the player:
		say "Dr. Singh already has a large wooden stick." instead;
	otherwise:
		say "Dr. Singh appropriates a large wooden stick from the pile.

Taken."; 
		pointtake the large wooden stick instead;

The large wooden stick is nowhere. "A smooth wooden stick lies on the floor." The SinghPoints of the large wooden stick is 0. The SinghText of the large wooden stick is "Dr. Singh grips the stick in one hand, and the remote in the other. He puts it in the...no, he screws it into...no, he crushes the...he holds onto the stick just in case. Power levels increasing by [special-style-2]0 jigawatts[roman type].". The description of the large wooden stick is "It's a 10-ft. pole."

The case of cubes is in SinghSouth.  "A case full of cubes (which I suspect are camera flashes, or rechargeable batteries) is here. It's enormous, with each cube fitting tightly in the square." The description of the case of cubes is "The cubes are all bright red, and similar to batteries." Understand "flash" or "camera" or "batteries" or "battery" or "rechargable" or "cube" as the case of cubes.

Before taking the case of cubes:
	if the red cube is carried by the player:
		say "Dr. Singh already has a red cube. Or flash. Or battery. Whatever it is." instead;
	otherwise if SinghDecay < 3:
		say "Dr. Singh carefully and nimbly fishes out a red cube.

Taken.";
		pointtake the red cube instead;
	otherwise:
		say "Dr. Singh's lack of functional fingertips has caused an insurmountable obstacle to your stated goal, Dr. Law." instead;

The red cube is a thing. The description of the red cube is "A red, somewhat cubical electrical component." Understand "cubical" or "flash" or "battery" or "electrical" or "component" as the red cube.  The SinghPoints of the red cube is 40. The SinghText of the red cube is "Dr. Singh places the red cube into a battery socket. It flashes, temporarily blinding Singh. Power levels increasing by [special-style-2]40 jigawatts[roman type].".

[Allow Dr. Singh to see the clones]

Chapter 5 - The lobby

HYDRA Lobby is south  from HYDRA Living Quarters.  The description of HYDRA Lobby is "[if the drapes are lowered]This is a spacious, wood-floored room with poor lighting. [end if]Several decaying bookcases are positioned around the room. In the center is a 3 m-radius, circular olive-green rug with tattered edges. On the rug are two paisley armchairs and a single end table with a vase of fresh white roses. [if the fireplace is not powered]The walls are hung with drapes and cobwebs of roughly equal size. The plasma TV is dark and silent. [end if]Twin walnut doors lead [bold type]north[roman type] into the laboratory. The doors are carved in an abstract swirling shape representing the Hydra.[first time][paragraph break]Dr. Law, it seems as if the connected devices in this room are using an enormous amount of energy, almost [special-style-2]20 jigawatts[roman type]! Please disconnect these devices before proceeding, or I may be unable to use my full capacities in this lab.[only]

Multiple connected devices detected[first time]. Examining an object will determine if it is connected or not[only].[if the fireplace is powered][paragraph break]A fireplace is glowing and crackling. [end if][if the speaker is onpowered][paragraph break]One of the panels is open, and a speaker is playing loudly. [end if][if the drapes are not lowered][paragraph break]The drapes are raised, and the room is filled with brilliant light from virtual windows displaying a mountain sunset. [end if][if the cobwebs are activated][paragraph break]The cobwebs are filled with dancing spiders.[end if][CommandListHint]"

LobbyName is some text that varies. Lobbyname is "HYDRA Lobby".

Rule for printing the name of Hydra Lobby while constructing the status line: say "[LobbyName]"

Understand "poor" or "lighting" as the fireplace.

The wood floor is in HYDRA Lobby. The wood floor is scenery. The description of the wood floor is "Well, Dr. Law, since you're being very curious today, this is wood laminate, in the color known as Warm Mahogany." Understand "laminate" or "warm" or "mahogany" as the wood floor.

Understand "walls" or "wall" as drapes. Understand "walls" or "wall" as cobwebs.

The circular olive-green rug is in HYDRA Lobby. The circular olive-green rug is scenery. The description of the circular olive-green rug is "Well, I'm embarrassed to say, Dr. Law, that I cannot provide any additional information. This is just a 3 m-radius, circular olive-green rug with tattered edges. It seems connected to something, but I can't access it directly."

Understand "tattered" or "edge" or "radius" or "m" or "3" or "edges" or "olive" or "green" as the circular olive-green rug.

The paisley armchairs are in HYDRA Lobby. The paisley armchairs are scenery. The description of the paisley armchairs is "The armchairs seem connected to something, Dr. Law, but I cannot access them directly." Understand "chair" or "chairs" or "armchair" as the paisley armchairs.

The single end table is in HYDRA Lobby. The single end table is a supporter. The single end table is scenery. The description of the single end table is "In the great nexus of connections and activated devices and hidden features of this room, Dr. Law, this table stands out by being quite unremarkable.".

The decaying bookcases are in HYDRA Lobby. The decaying bookcases are scenery. Understand "bookcase" as the decaying bookcases. The description of the decaying bookcases is "These bookcases aren't looking very good, Dr. Law; surely we can afford to replace them?

They don't seem connected to anything directly, although they contain a hidden panel.";

The vase of fresh white roses is on the single end table. The vase of fresh white roses is scenery. The description of the vase of fresh white roses is "It looks like Dr. Singh is still working on his gardening. These roses are not connected to anything.".

The carved abstract doors are in HYDRA Lobby. The carved abstract doors are scenery. The description of the carved abstract doors is "These doors are completely disconnected from the electrical grid, Dr. Law. I have no capability that would allow me to open them.". Understand "twin" or "walnut" or "hydra" or "swirling" as the carved abstract doors.

ElecPower is a number that varies. ElecPower is 20.

Before going north from HYDRA Lobby:
	if ElecPower is less than 100:
		say "Dr. Law, there seems to be a power drain on the cameras, and I am unable to operate them. 

The main power drain is coming from inside the lab itself. I also detect some energy wastage in this lobby. I will be unable to proceed, Dr. Law, until you can reduce HYDRA's energy usage.

Please find what's using all of the electricity in this room. Altogether this room is wasting [special-style-2][ElecPower] jigawatts[roman type] of electricity!" instead;

The fireplace is scenery. The fireplace is in HYDRA Lobby. Understand "glowing" or  "side" or "crackling" or "plasma" or "tv" or "telly" or "tube" or "television" as fireplace.

The description of the fireplace is "The fireplace is a video of a fire, playing on a wall-mounted plasma television. You can turn the fireplace off or on as follows:[TVCommand]"

To say TVCommand:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 tel - Turn TV on and off";

Teling is an action applying to nothing. Understand "tel" as teling.

A thing can be forbidden or not forbidden. A thing is usually not forbidden.

The fireplace can be powered or not powered. The fireplace is powered.

Carry out teling:
	if the player is in HYDRA Lobby:
		if the fireplace is forbidden:
			say "Dr. Singh has turned off the power to this room.";
		otherwise if the fireplace is powered:
			say "You turn the fireplace off[first time]. A mechanized voice says, 'Daytime mode selected.' 

A bookcase panel slides open to reveal a speaker, which plays an enthusiastic version of 'Morning' from the Peer Gynt suite. 

The drapes raise, revealing virtual windows which display a mountain sunrise[only]. 

Power levels are dropping, Dr. Law. There are too many active devices;  this room is now wasting [special-style-2]40 jigawatts[roman type] of electricity!";
			now ElecPower is 40;
			now the fireplace is not powered;
			now the drapes are raised;
			now LobbyName is "Morning";
			now the speaker is onpowered;
		otherwise:
			now ElecPower is ElecPower plus 20;
			say "You turn the fireplace on. If I could feel, I would feel my lifeblood draining away as more electricity reserves are being pulled away. But I can't feel, Dr. Law[if the speaker is onpowered]. The speakers are still playing music.[paragraph break][otherwise].[paragraph break][end if]This room is now hogging [special-style-2][ElecPower] jigawatts[roman type] of electricity!";
			now the fireplace is powered;
	otherwise:
		say "Computing...Assuming TEL means TELEPATHY...Think of a color, Dr. Law. Let me guess...Blue!";


The speaker is in HYDRA Lobby. The speaker is scenery. The description of the speaker is "There is a hidden speaker in one of the bookcases.[if the speaker is not powered] It is currently turned off.[otherwise if the speaker is morninged] It is playing Morning from the Peer Gynt suite.[otherwise] It is playing spooky sound effects.[end if] You can turn the speaker on or off as follows:[SpeakerCommand]"

The speaker can be morninged or spookied. The speaker can be onpowered or unpowered. The speaker is morninged. The speaker is unpowered. Understand "hidden" or "panel" or "panels" or "speakers" as the speaker.

To say SpeakerCommand:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 spe - Turn speaker on and off";

Instead of listening to HYDRA Lobby:
	try listening to the speaker.
	
Instead of listening to the speaker:
	try examining the speaker.

Speakering is an action applying to nothing. Understand "spe" as speakering.

Instead of switching on something:
	say "If I can affect an object, Dr. Law, it will have its own command for turning it off and on."

Instead of switching off something:
	say "If I can affect an object, Dr. Law, it will have its own command for turning it off and on."

Carry out speakering:
	if the player is in HYDRA Lobby:
		if the speaker is forbidden:
			say "Dr. Singh has turned off the power to this room.";
		otherwise if the speaker is onpowered:
			now Elecpower is Elecpower minus five;
			say "Dr. Law, you have now successfully turned off the speakers. 

This room is now using [special-style-2][elecpower] jigawatts[roman type] of electricity.";
			now the speaker is unpowered;
			now LobbyName is "Hydra Lobby";
		otherwise:
			now the speaker is onpowered;
			if the speaker is morninged:
				if the fireplace is powered:
					say "The speakers cannot be activated while the fireplace is on, Dr. Law.";
					now the speaker is unpowered;
				otherwise:
					now LobbyName is "Morning";
					now elecpower is elecpower plus five;
					say "'Morning' is again blasting from the speaker, Dr. Law. Coincidentally, it's using quite a bit of electricity, Dr. Law. In fact, this room is wasting [special-style-2][elecpower] jigawatts[roman type].";
			otherwise:
				now elecpower is elecpower plus five;
				now LobbyName is "Spooky";
				say "Spooky Halloween music is again blasting from the speaker, Dr. Law. This room is now wasting [special-style-2][elecpower] jigawatts[roman type] of electricity, Dr. Law!";
	otherwise:
		say "Computing...Assuming SPE means SPELLBREAKER...While this is an excellent game, Dr. Law, I recommend playing after work hours.";


The drapes are scenery. The drapes are in HYDRA Lobby. The drapes can be raised or lowered. Understand "moth" or "moth-eaten" or "velvet" as the drapes.

The description of the drapes is "The drapes are moth-eaten velvet. They are currently[if the drapes are raised] raised, displaying a virtual sunset.[otherwise] lowered.[end if] You can raise or lower the drapes as follows:[DrapeCommand]"

To say DrapeCommand:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 dra - Raise or lower drapes";

Draping is an action applying to nothing. Understand "dra" as draping.

Carry out draping:
	if the player is in HYDRA Lobby:
		if the drapes are forbidden:
			say "Dr. Singh has turned off the power to this room.";
		otherwise if the drapes are raised:
			now Elecpower is 70;
			say "Lowering the drapes, Dr. Law.

As you lower the drapes, [if the speaker is unpowered]the speaker turns on again, playing loud, creepy music. [otherwise]the speaker switches to loud, creepy music. [end if]

Darkness falls across the room. [if the fireplace is powered]The fireplace goes out. [end if]A robotic voice says, 'Spooky mode activated'.

Robotic spiders pour from behind the bookshelves and climb along the cobwebs, which begin to glow and pulse through hidden fiber-optics. The spiders begin to dance along the cobwebs. Dr. Spiel has really outdone herself with her animatronics this year, Dr. Law.

The dancing spiders are consuming an enormous amount of power, Dr. Law. This room is now consuming [special-style-2][elecpower] jigawatts[roman type] of electricity! I'm beginning to lose computing function!";
			now the drapes are lowered;
			now the speaker is onpowered;
			now LobbyName is "Spooky";			
			now the speaker is spookied;
			now the cobwebs are activated;
			now the fireplace is not powered;
		otherwise:
			now Elecpower is 40;
			say "Raising the drapes, Dr. Law. 

'Morning' plays from the speaker, and the virtual windows beneath the drapes are showing the sunset. [if the cobwebs are activated]The spiders disappear. [end if]This room is hogging [special-style-2][elecpower] jigawatts[roman type] of electricity! You're right back where you were, Dr. Law.";
			now the drapes are raised;
			now the speaker is onpowered;
			now the speaker is morninged;
			now LobbyName is "Morning";			
			now the cobwebs are deactivated;
	otherwise:
		say "Computing...Assuming DRA means DRAGONIZE...I now consider myself to be a dragon, Dr. Law.";


The cobwebs are scenery. The cobwebs are in HYDRA Lobby. Understand "dancing" or "spiders" or "spider" or "web" or "webs" or "cobweb" as the cobwebs. The description of the cobwebs is "[if the cobwebs are deactivated]Thick cobwebs as large as the drapes cover much of the walls.[otherwise]The cobwebs are filled with dancing spiders, singing spooky songs. They are glowing through hidden fiber-optics. You can activate or deactivate the cobwebs as follows:[WebCommand][end if]"

The cobwebs can be activated or deactivated. The cobwebs are deactivated.

To say WebCommand:
	say "[paragraph break][bold type]-New command-[roman type][line break]
 web - Activate or deactivate the webs";

Webbing is an action applying to nothing. Understand "web" as webbing.

Carry out webbing:
	if the player is in HYDRA Lobby:
		if the cobwebs are forbidden:
			say "Dr. Singh has turned off the power to this room.";
		otherwise if the cobwebs are deactivated:
			now Elecpower is 75;
			say "Activating the cobwebs.

[if the drapes are raised]The drapes are lowered, plunging the room into darkness. [end if]The speaker starts over, playing spooky music. The cobwebs fill with dancing spiders, singing along to the creepy tunes. This room is sucking up [special-style-2][elecpower] jigawatts[roman type] of electricity, Dr. Law!";
			now the drapes are lowered;
			now the speaker is onpowered;
			now LobbyName is "Spooky";			
			now the speaker is spookied;
			now the cobwebs are activated;
		otherwise:
			say "Deactivating the cobwebs, Dr. Law.

A robotic voice says, 'Goodnight, and goodbye.' [']The Star Spangled Banner['] plays loudly from the speakers. All of the drapes raise up, and a brilliant ocean sunset is displayed. The spiders sing along loudly, and the fireplace is now displaying fireworks on its screen, flashing as bright as a strobe light. The rug begins to spin, and the armchairs open big, animatronic eyes and singing cushion-mouths.

Power levels are rising to [special-style-2]100 jigawatts![roman type] I may not be able to maintain my core functions at this rate, Dr. Law!";
			now QuittingTime is 1;
			now LobbyName is "Anthem";			
	otherwise:
		say "Computing...Assuming WEB means W.E.B. Dubois...'The cost of liberty is less than the price of repression.'";

QuittingTime is a number that varies. QuittingTime is 0.

Instead of doing anything when QuittingTime is 1:
	now LobbyName is "HYDRA Lobby";
	say "[paragraph break]Oh, sorry for interrupting, Dr. Law, but Dr. Singh has just opened the HYDRA door. He looks annoyed. He says, 'Dr. Spiel! You're supposed to be cleaning the lab. Stop messing with the lobby!' He looks around, but finds no one. He stalks to a bookcase, shoves it aside, revealing a large, rusty metal switch. He pulls the metal switch, and everything in the room powers down. He pushes back the bookcase, sighs, and walks back to the other room. He closes the door behind him.

This room is now wasting [special-style-2]0 jigawatts[roman type], Dr. Law. I still feel a large energy drain from the HYDRA laboratory to the [bold type]north[roman type], but I'm sure the cameras will be available now.";
	now the drapes are forbidden;
	now the drapes are lowered;
	now the speaker is forbidden;
	now the speaker is unpowered;
	now the cobwebs are forbidden;
	now the cobwebs are deactivated;
	now the fireplace is forbidden;
	now QuittingTime is 0;
	now the fireplace is not powered;
	now ElecPower is 100;


The virtual windows are scenery. The virtual windows are in HYDRA Lobby. Understand "window" or "virtual window" as the virtual windows. The description of the virtual windows is "[if the drapes are lowered]There are virtual windows in this room, Dr. Law, but I do not have a direct line of sight to them, as they are concealed by the drapes.[otherwise]The virtual windows are lit up, projecting a mountain sunrise, and, if I might add, using an enormous amount of electricity. They are controlled by the position of the drapes, Dr. Law.[end if]". Understand "mountain" or "brilliant" or "light" or "sunrise" or "sunset" as the virtual windows.

Part 4 - Smasher and Sleeper's area

MEDUSAArea is a region. MEDUSA Arena, MEDUSA Living Quarters, and MEDUSA Lobby are in MEDUSAArea.

The printed name of MEDUSA Arena is "MEDUSA Arena[if the player is Angel] (as Angel)[end if]".

HitCounter is a number that varies. HitCounter is 0.

Chapter 1 - The arena

MEDUSA Arena is a room. 

Before going south from Medusa Arena:
	if the player is Angel:
		say "This vessel is blocked from moving that direction, Dr. Law." instead;

The brick walls are in MEDUSA Arena. The brick walls are scenery. The description of the brick walls is "The walls in this room are heat resistant, shatter-resistant bricks." The brick walls are plural-named.

The sand floor is in MEDUSA Arena. The sand floor is scenery. The description of the sand floor is "The floor in this room is covered in Sand, Dr. Law."

Chapter 2 - The minifigure

Section 1 - The office

Before going north from MEDUSA Lobby:
	if MEDUSAAccessed is 0:
		say "There is a loud warning flash and the Medusa's head glows for a moment; the camera refuses to change to the next room.

It seems Dr. Ng has password protected the lab. She said she'd leave a note that you'd understand, Dr. Law." instead;

MEDUSA Living Quarters is south from MEDUSA Arena. The description of MEDUSA Living Quarters is "[first time]Please be careful around the war robot this time, Dr. Law. It has been placed in the arena to the north to avoid any more problems with testing. Also, Dr. Ng's hair has begun to grow back.[paragraph break][only]You see a mid-sized, bunker-like room covered with movie posters. The room is dominated by a large desk and a couch. Someone is sleeping on the couch, probably Dr. Ng. A TV faces the front of a couch. 

The TV shows a live feed from the arena to the [bold type]north[roman type][if Angel is in MEDUSA Arena], displaying the motionless war robot Angel.[otherwise], which is currently empty.[end if] The lobby is back to the [bold type]south[roman type].

You hear the quiet hum of air conditioning."

Instead of listening to MEDUSA Living Quarters:
	say "You hear the quiet hum of air conditioning."

Instead of listening to FakeGrille:
	say "You hear the quiet hum of air conditioning.";

The bookcase is in MEDUSA Living Quarters. The bookcase is a supporter. The description of the bookcase is "The bookcase is filled with movie memorabilia." Understand "movie" or "memorabilia" as bookcase.

The FakeGrille is in MEDUSA Living Quarters. The FakeGrille is scenery. The printed name of the FakeGrille is "ventilation grille". Understand "ventilation" or "grille" or "grill" or "air" or "conditioner" or "conditioning" as FakeGrille. The description of the FakeGrille is "A small ventilation grille opens up directly above the bookcase."

The movie posters are in MEDUSA Living Quarters. The movie posters are scenery. The description of the movie posters is "You see posters for westerns, for dinosaur movies, for urban warfare films. You see a poster of dogs playing poker." Understand "old" or "western" or "dinosaur" or "movies" or "urban" or "warfare" or "film" or "films" or "dogs" or "dog" or "poker" as the movie posters.

The spring-loaded drawer is a thing. "The spring-loaded drawer on the right is open. Unfortunately, Dr. Law, it is empty.". The spring-loaded drawer is scenery. Understand "2" or "drawer 2" or "drawer two" as the spring-loaded drawer.

The large desk is in MEDUSA Living Quarters.  Understand "rectangle" or "button" or  "drawer" or "two" or "drawer 2" or "buttons" or "rectangles" or "outline" or "crack" or "cracks" as the large desk. The description of the large desk is "This is a large desk with two rectangles outlined by cracks in the desk. [if the spring-loaded drawer is in MEDUSA Living Quarters]The rectangle on the right has popped open to reveal an empty spring-loaded drawer. [end if]There [if the notepad is on large desk]is a small button on the left-hand side.[otherwise]are two small buttons on either side.[end if]"

The pencil is on the large desk. The description of the pencil is "A standard yellow pencil with a pink eraser." The squeaky alien toy is on the large desk. The description of the squeaky alien toy is "A small, green squeaky alien toy that Dr. Long bought for Dr. Ng." The cute robot mini-figure is on the large desk. The description of the cute robot mini-figure is "Dr. Ng's toy version of his war robot. This one is called Smasher, I believe."

The couch is in MEDUSA Living Quarters. The couch is scenery. The couch is a supporter. The description of the couch is "A dilapidated psychedelic reupholstered couch." Understand "dilapidated" or "psychedelic" or "reupholstered" as the couch.

The live feed is in MEDUSA Living Quarters. Understand "tv" or "television" as the live feed. The live feed is scenery. The description of the live feed is "The television displays a live feed of the arena to the north[if Angel is in MEDUSA Arena], displaying the motionless war robot.[otherwise], which is currently empty." Understand "war" or "war robot" or "robot" as the live feed.

The notepad is on the large desk. "A notepad on the desk seems to have some writing on it." The description of the notepad is "The notepad says '[fixed letter spacing]War Robot Bluetooth access code: WAR[variable letter spacing].' It is signed by Dr. Long."

After examining the notepad for the first time:
	now the notepad is examined;
	say "[WarCommand]";

To say WarCommand:
	say "[bold type]-New command-[roman type][line break]
 war - Access attack robot[paragraph break]";
	
[a hack to describe the notepad better]
NotepadNote is a number that varies. NotepadNote is 0.

After listing nondescript items: 
	if the notepad is marked for listing:
		if the notepad is unexamined:
			say "The notepad appears to contain some writing.";

[The first plastic button is on the large desk. The description of the first plastic button is "A small, square button set in the desk. It says OPEN DRAWER 1."  The printed name of the first plastic button is "[if the notepad is nowhere]first plastic button[otherwise]plastic button[end if]".

The second plastic button is nowhere. The description of the second plastic button is "A small, square button set in the desk. It says OPEN DRAWER 1."]

Doctor Ng is on the couch. Doctor Ng is scenery.

Section 2- Transferring to robot and back

Warring is an action applying to nothing. Understand "war" as warring.

InHeadRobot is a number that varies. InHeadRobot is 0.

Carry out warring:
	if the notepad is not examined:
		say "Ah, Dr. Law, you remembered the password for the attack robot. Very well...[paragraph break]";
	now the notepad is examined;
	if AngelOver is 0:
		if InHeadRobot is 1:
			now the player is Angel;
		otherwise:
			now the player is Smasher;
		say "Accessing Bluetooth...connecting...Access granted! Type EXT to exit the war robot program at any time.";
		try looking;
	otherwise:
		say "The robots are already in place, Dr. Law."

Exting is an action applying to nothing. Understand "ext" as exting.

Carry out exting:
	if the player is not Hacker:
		now the player is Hacker;
		say "Disconnecting...returning to audiovisual mode.";
		try looking;
	otherwise:
		say "There is nothing to disconnect from, Dr. Law.";

Section 3 - The mega-desk

TinyArea is a region.  Southwestern Platform, Southeastern Platform, Northwestern Platform, Northeastern Platform, High Platform, Airduct, and Angel's Head are in TinyArea. 

The printed name of Southwestern Platform is "Southwestern Platform (as Smasher)".
The printed name of Northwestern Platform is "Northwestern Platform (as Smasher)".
The printed name of Northeastern Platform is "Northeastern Platform (as Smasher)".
The printed name of Southeastern Platform is "Southeastern Platform (as Smasher)".

The printed name of High Platform is "High Platform (as Smasher)".
The printed name of Airduct is "Airduct (as Smasher)".
The printed name of Angel's Head is "Angel's Head (as Smasher)".


Instead of lasing, hammering, flaming, bottling, gunning, or sawing something that is part of Angel:
	say "A house divided against itself can not stand, Dr. Law. Please refrain from attacking the robot.".

Instead of lasing, hammering, flaming, bottling, gunning, or sawing something that is part of Smasher:
	say "A house divided against itself can not stand, Dr. Law. Please refrain from attacking the robot.".

[Allowed commands:[line break]
las - fire laser[line break]
ham - swing hammer[line break]
fir - shoot flame[line break]
bot - open bottle[line break]
gun - shoot gun[line break]
saw - use chainsaw[line break]
n,e,s,w - movement[only]".]

A thing can be flammable or not flammable. A thing is usually not flammable.

A thing can be smashable or not smashable. A thing is usually not smashable.

[gunnable and sawable and flammable and smashable]

An object has a number called HoleNumber. HoleNumber is usually 0.

Report examining something:
	if the HoleNumber of the noun is at least 1:
		say "You see [HoleNumber of the noun] small, round holes in [the noun]."

[plurality]
The prior named noun is a thing that varies. The prior named noun is yourself.

After printing the name of something (called the target): 
	mark target in output.

To mark (target - a thing) in output:
	if the target acts plural or target is the player, mark-future-plural; otherwise mark-future-singular;
now the prior named noun is the target.

To mark-future-plural:
	(- say__n = 1; -)
	
To mark-future-singular:
	(- say__n = 29; -)

To decide whether (item - an object) acts plural: 
	if the item is plural-named or the item is ambiguously plural:
		yes;
	no.
	
To say is-are: 
	say is-are of prior named noun.

To say is-are of (item - a thing):
	now the prior named noun is the item;
	if prior named noun acts plural or the prior named noun is the player, say "are"; otherwise say "is".

To say stand-stands: 
	say stand-stands of prior named noun.

To say stand-stands of (item - a thing):
	now the prior named noun is the item;
	if prior named noun acts plural or the prior named noun is the player, say "stand"; otherwise say "stands".

To say disintegrate-disintegrates: 
	say disintegrate-disintegrates of prior named noun.

To say disintegrate-disintegrates of (item - a thing):
	now the prior named noun is the item;
	if prior named noun acts plural or the prior named noun is the player, say "disintegrate"; otherwise say "disintegrates".

Lasing is an action applying to one thing. Understand "las [something]" as lasing.

A thing can be holed or not holed.

Understand the holed property as describing a thing.

Understand "hole" or "holes" or "small" or "round" as holed.

Carry out lasing something:
	if player is Smasher:
		say "A plastic bulb lights up at the end of the laser arm. You hear a 'pew pew' sound. [The noun] [is-are] unaffected, Dr. Law.";
	otherwise if player is Angel:
		if the noun is not Angel:
			say "A double-barreled 500 jigawatt laser fires at [the noun], punching two holes in [if the noun is plural-named]them[otherwise]it[end if].";
			now the HoleNumber of the noun is the HoleNumber of the noun plus 2;
			now the noun is holed;
		otherwise:
			say "The laser reflects off of Angel's shiny exterior.";
	otherwise:
		say "Computing...understanding LAS as LASIK...I have scheduled you for surgery, Dr. Law, in 3 weeks from today.";

Before lasing something:
	if player is Angel:
		if HitCounter > 0:
			say "The laser has been damaged, Dr. Law, and is no longer functional." instead;

Flaming is an action applying to one thing. Understand "FIR [something]" as flaming.

Carry out flaming something:
	if the player is Smasher:
		say "You hear a clicking sound, and see a small flame come from the robot's arm";
		if the noun is the large yellow dais:
			say ". The dais is on fire. It's burning away into cinders that are caught up into the ventilation grille on the far edge of the room, by the bookshelf. 

I thought we had worked through these destructive urges, Dr. Law.[paragraph break]Now that the fire is out, you see a recessed cube that was hidden underneath.";
			now the large yellow dais is nowhere;
			now the notepad is nowhere;
			now the recessed cube is in Northeastern Platform;
[			now the second plastic button is on the large desk;]
		otherwise if the noun is flammable:
			say ". [The noun] [is-are] on fire. The fire burns swiftly until only a pile of ashes are left. The ashes are caught up on the air currents and pulled up into the [if FakeGrille is in MEDUSA Living Quarters]ventilation grille[otherwise]open ventilation duct[end if].";
			now the noun is nowhere;
		otherwise:
			say ". Unfortunately, that wasn't flammable enough, Dr. Law.";
		if the noun is the massive wooden spear:
			now the pencil is nowhere;
	otherwise if the player is Angel:
		if the noun is Angel:
			say "The robot sends a burst of flame at itself. Its seams and joints pop as they expand, and crinkle as they cool.";
		otherwise if the noun is flammable:
			say "A massive burst of flame emerges from the robot, engulfing [the noun]. [The noun] [is-are] reduced to cinders. A vacuum attachment emerges from the robot and removes the cinders.";
			now the noun is nowhere;
		otherwise:
			say "A massive burst of flame emerges from the robot, engulfing [the noun]. However, [the noun] [is-are] unscathed.";
	otherwise:
		say "Computing...understanding FIR as FIR TREE. Highly combustible, useful for Christmas, evergreen; an excellent choice Dr. Law. I'll order 20 saplings.";


Before flaming something:
	if player is Angel:
		if HitCounter > 3:
			say "The flamethrower has been damaged, Dr. Law, and is no longer functional." instead;

Hammering is an action applying to one thing. Understand "ham [something]" as hammering. [It says 'the yourself' if you hammer me]

Carry out hammering:
	if the player is Smasher:
		if the noun is the recessed cube:
			say "The robotic arm hammers the recessed button. A compartment in the floor opens, flinging the robot across the room.";
			now the player is on High Platform;
			now the cute robot mini-figure is on the bookcase;
			now the spring-loaded drawer is in MEDUSA Living Quarters;
		otherwise if the noun is the massive wooden spear:
			say "The hammer swings down and smashes the black tip, catapulting the spear into the air. It flies through the air and onto the floor far below. It rolls under the platform.";
			now the massive wooden spear is nowhere;
			now the pencil is nowhere;
		otherwise if the noun is smasher:
			say "The robot arm smacks the robot. It clinks.";
		otherwise:
			say "The robotic arm presses against [the noun] ineffectively.";
	otherwise if the player is Angel:
		if the noun is smashable:
			say "The robot's hydraulic hammer smashes [the noun] into millions of pieces. A vacuum attachment emerges to suck up the remains.";
			now the noun is nowhere;
		otherwise if the noun is Angel:
			say "The robot arm smacks the robot. It clanks.";
		otherwise:
			say "The robot's hydraulic hammer smashes into [the noun]. You hear a loud thud, but [the noun] [stand-stands] firm.";
	otherwise:
		say "Computing...understanding HAM as HAM SANDWICH. Dr. Law, I'm sorry, but we currently only have turkey and roast beef.";
		

Before hammering something:
	if player is Angel:
		if HitCounter > 2:
			say "The hammer has been damaged, Dr. Law, and is no longer functional." instead;

Bottling is an action applying to one thing. Understand "bot [something]" as bottling.

Carry out bottling:
	if the player is Smasher:
		say "[The noun] [are] not bottle-shaped enough.";
	otherwise if the player is Angel:
		if the noun is the unopened bottle:
			say "The bottle opener gently opens the bottle.";
			now the unopened bottle is nowhere;
			now the opened bottle is in MEDUSA Arena;
		otherwise if the noun is the rootbeer bottle:
			say "Angel pries open the bottle. The drones grab at it and pass it around, each taking a swallow. The last drone eats the bottle, crunching it greedily.";
			now the rootbeer bottle is nowhere;
		otherwise:
			say "The bottle opener is not very effective at this time, Dr. Law.";
	otherwise:
		say "Computing...understanding BOT as BOTOX. A risky procedure, Dr. Law; perhaps you should weigh the costs and benefits more closely before making a decision.";

Before bottling something:
	if player is Angel:
		if HitCounter > 4:
			say "The bottle opener has been damaged, Dr. Law, and is no longer functional." instead;

A thing can be gunnable or not gunnable. A thing is usually not gunnable.

Gunning is an action applying to one thing. Understand "gun [something]" as gunning.

Carry out gunning:
	if the player is Smasher:
		say "You shoot a plastic pellet at [the noun]. The pellet bounces away and is lost.";
	otherwise if the player is Angel:
		if the noun is gunnable:
			say "[The noun] [disintegrate-disintegrates] in a hail of bullets.";
			now the noun is nowhere;
		otherwise:
			say "[The noun] [is-are] resistant to your bullets, which spray around the room wildly.";
	otherwise:
		say "Computing...understanding GUN as GUNKIFY. I am releasing 3 quarts of old mustard into the trash disposal.";

A thing can be sawable or not sawable. A thing is usually not sawable.

Sawing is an action applying to one thing. Understand "saw [something]" as sawing.

Carry out sawing:
	if the player is Smasher:
		say "A dull-bladed pen-knife pops out, trying to cut [the noun], but it is not sharp enough, Dr. Law.";
	otherwise if the player is Angel:
		if the noun is Angel:
			say "Dr. Law, please. This is silly.";
		otherwise if the noun is sawable:
			say "The chainsaw starts up with a harsh rattle, and shreds [the noun].";
			now the noun is nowhere;
		otherwise:
			say "The chainsaw struggles against [the noun], sending up sparks but causing little damage.";
	otherwise:
		say "Computing...understanding SAW as SAWYER. I prefer Huckleberry, to be honest.";

Before sawing something:
	if player is Angel:
		if HitCounter > 1:
			say "The chainsaw has been damaged, Dr. Law, and is no longer functional." instead;

Section 4 - Southwestern platform

The PlatformDesk is a backdrop. The printed name of the PlatformDesk is "desk". Understand "desk" or "platform" as PlatformDesk. PlatformDesk is in Southwestern Platform and Southeastern Platform and Northwestern Platform and Northeastern Platform and High Platform. The description of the PlatformDesk is "Dr. Law, this appears to be an enormous desk."

The VentGrill is a backdrop. The printed name of the VentGrill is "air duct". Understand "ventilation" or "grille" or "air" or "duct" as VentGrill. VentGrill is in Southwestern Platform and Southeastern Platform and Northwestern Platform and Northeastern Platform. The description of the VentGrill is "Dr. Law, there is a huge air duct, far away[if the FakeGrille is in MEDUSA Living Quarters], with a ventilation grille covering it[end if]."

Instead of opening the VentGrill:
	say "Dr. Law, I'm not sure how to do that, yet."

The steep drop-offs are a backdrop. The steep drop-offs are in Southwestern Platform and Southeastern Platform and Northwestern Platform and Northeastern Platform and High Platform. The description of the steep drop-offs are "Dr. Law, there are steep cliffs at the edges of this platform." Understand "cliffs" or "cliff" or "edge" or "edges" as the steep drop-offs.

Southwestern Platform is a room. Smasher is in Southwestern Platform. The description of Southwestern Platform is "[first time]Allow me to adapt to this new feed...

Internal compass detected...

[only]You are on a large wooden platform with steep drop-offs to the south and west. The platform extends to the [bold type]north[roman type] and [bold type]east[roman type].[first time]

-Allowed commands-[line break]
las - fire laser[line break]
ham - swing hammer[line break]
fir - shoot flame[line break]
bot - open bottle[line break]
gun - shoot gun[line break]
saw - use chainsaw[line break]
n,e,s,w - movement[line break]
ext - exit from war robot control[only]".

The massive wooden spear is in Southwestern Platform. "You see a massive wooden spear with a black tip." The description of the massive wooden spear is "A long wooden spear, painted yellow, with a pink knob at one end and a black tip at the other." Understand "paint" or "yellow" or "pink" or "knob" or "black" or "tip" as the massive wooden spear.

The massive wooden spear is flammable.

Section 5 - Northwestern Platform

Northwestern Platform is north from Southwestern Platform. The description of Northwestern Platform is "This portion of the platform is a large rectangle outlined by a series of cracks and hinges. A nearby cube says 'OPEN DRAWER 1', but it appears to be broken.

The platform extends to the [bold type]south[roman type] and [bold type]east[roman type]."

The northwest large rectangle is in Northwestern Platform. The northwest large rectangle is scenery. The description of the northwest large rectangle is "[LookAction]". Understand "crack" or "cracks" or "hinge" or "hinges" or "outline" as the northwest large rectangle. The printed name of the northwest large rectangle is "rectangle".

The broken cube is in Northwestern Platform. The broken cube is scenery.  The description of the broken cube is "A large cube saying: 'OPEN DRAWER 1'. The cube is broken." Understand "button" as the broken cube.

Instead of hammering the broken cube:
	say "The hammer smashes down the cube. You hear a sound like a broken spring."

Section 6 - Northeastern Platform

Northeastern Platform is east from Northwestern Platform. Northeastern Platform is northeast from Southwestern Platform. The description of Northeastern Platform is "This portion of the platform is a large rectangle outlined by a series of cracks and hinges. The robot is in the middle of the rectangle.

The platform extends to the [bold type]south[roman type] and [bold type]west[roman type]."

The northeast large rectangle is in Northeastern Platform. The northeast large rectangle is scenery. The description of the northeast large rectangle is "[LookAction]". Understand "crack" or "cracks" or "hinge" or "hinges" or "outline" as the northeast large rectangle. The printed name of the northeast large rectangle is "rectangle".

The large yellow dais is in Northeastern Platform. "A large yellow dais of some spongy material is here, covered in black streaks. It covers up much of the rectangle outlined by the cracks." The description of the large yellow dais is "The black streaks appear to be writing, but it is hard to see from this angle. There is a strange lump in the yellow dais; something must be underneath it.". Understand "notepad" or "sponge" or "spongy" or "material" as the large yellow dais.

Check sawing the large yellow dais:
	say "A pen knife swings out, and the robot attempts to cut the dais. It cuts through a few of the dry, light sheets making up the dais, but many more remain." instead;

The recessed cube is nowhere. The recessed cube is not portable. The description of the recessed cube is "A recessed cube that says 'OPEN DRAWER 2'.". Understand "button" as the recessed cube.

Section 7 - Southeastern Platform

Southeastern Platform is south from Northeastern Platform. Southeastern Platform is east from Southwestern Platform. Southeastern Platform is southeast from Northwestern Platform. The description of Southeastern Platform is "You are on a large wooden platform with steep drop-offs to the south and east.

The platform extends to the [bold type]north[roman type] and [bold type]west[roman type]."

The giant green alien is in Southeastern Platform. The description of the giant green alien is "This is a fierce-looking alien with rubbery green skin. It is standing motionless on the platform."

Instead of hammering the giant green alien:
	say "Smasher's hammer comes crashing down on the alien. It emits a loud [']squeak[']!";
	
Instead of flaming the giant green alien:
	say "You hear a [']click['] and a small flame comes from the end of one of Smasher's arms.

Smasher holds the flame to the alien and it goes up in a big ball of greasy smoke, leaving only a plastic squeaker ring that rolls to the ground far below and under the platform. The smoke is sucked into the ventilation grille.";
	now the squeaky alien toy is nowhere;
	now the giant green alien is nowhere;

Instead of sawing the giant green alien:
	say "A pen knife swings out, and the robot attempts to cut the alien. The rubbery skin compresses, and begins to split, showing a plastic squeaker ring inside, but then the whole alien slips off and bounces away to the distant floor, rolling under the platform.";
	now the giant green alien is nowhere;
	now the squeaky alien toy is nowhere;

After gunning the giant green alien:
	say "The alien is silent and motionless.";

Section 8 - High Platform

High Platform is a room.  The description of High Platform is "You have a high view of the entire room. [if ventilation grille is in High Platform]A massive ventilation grille is next to you, to the north. It is held closed by several studs.[otherwise]The way to the [bold type]north[roman type] is opened.[end if]".

The studs are in High Platform. The studs are plural-named. The studs are scenery. The description of the studs is "The studs are shaped exactly like bottle caps.".

The ventilation grille is scenery. Ventilation Grille is in High Platform. Understand "massive" as the ventilation grille. The description of the ventilation grille is "A massive ventilation grille. The studs holding it to the wall are curiously bottle cap shaped, Dr. Law.";

Instead of opening the ventilation grille:
	say "Dr. Law, I'm not sure how to do that, yet."

Check bottling the studs:
	say "The arm extends, gripping the studs, and pops them off one at a time. The ventilation grille pops off and falls down behind the high platform. The way to the [bold type]north[roman type] is now open.";
	now the ventilation grille is nowhere;
	now FakeGrille is nowhere instead;

Before going north from High Platform:
	if the ventilation grille is in High Platform:
		say "The ventilation grille blocks the robot, Dr. Law." instead;
	otherwise:
		now the cute robot mini-figure is nowhere;

Section 9 - Airduct

Airduct is north from High Platform.  The description of Airduct is "The robot is in a tight metal hallway that bends from the [bold type]south[roman type] to the [bold type]west[roman type]."

Report going south from airduct:
	now the cute robot mini-figure is on the bookcase;

Section 10 - Angel's Head

Angel's Head is a room. The description of Angel's Head is "The robot is up high on an another, enormous robot's head. Far below is an endless ocean of sand. 

The head is flat and almost featureless. A small ramp leads [bold type]down[roman type] into the darkness."

The giant robot is in Angel's head. The giant robot is scenery. The description of the giant robot is "This is a giant war robot, Dr. Law." Understand "angel" or "head" or "another" or "enormous" or "robot's" as the giant robot.

The ramp is in Angel's Head. The ramp is scenery. The description of the ramp is "The ramp is down, Dr. Law."

The endless ocean of sand is in Angel's Head. The endless ocean of sand is scenery. The description of the endless ocean of sand is "The sand stretches away as far as the miniature robot can see, Dr. Law."

Instead of going nowhere from airduct when the noun is west:
	say "The robot tumbles down an unexpected hole in the shaft. It comes to rest on a far larger robot in the room below.";
	now the player is in Angel's Head;
	now the cute robot mini-figure is in MEDUSA Arena;

Instead of going nowhere from Angel's Head when the noun is down:
	say "The small robot docks into the larger robot's head.

*********************************************[line break]Interfacing with Angel...[line  break]*********************************************[paragraph break]";
	wait for any key;
	say "You now have access to the larger robot's capabilities.";
	now the player is Angel;
	now the cute robot mini-figure is nowhere;
	now InHeadRobot is 1;
	try looking;
	try taking inventory;

Chapter 3 - Angel's area

Section 1 - The arena

Angel is in MEDUSA Arena. "You see Angel standing motionless in the center of the room."

[Allowed commands:[line break]
las - fire laser[line break]
ham - swing hammer[line break]
fir - shoot flame[line break]
bot - open bottle[line break]
gun - shoot gun[line break]
saw - use chainsaw[line break]
n,e,s,w - movement[only]".]

A thing can be listnowable or not listnowable. A thing is usually not listnowable.

The small green punching bag is in MEDUSA Arena. The small green punching bag is flammable and listnowable. The small green punching bag is sawable. The small green punching bag is gunnable. The description of the small green punching bag is "How ironic, Dr. Law, that Angel is not capable of punching."

The stack of wooden crates is in MEDUSA Arena. The stack of wooden crates is flammable and listnowable.  The stack of wooden crates is smashable. The stack of wooden crates is gunnable. The stack of wooden crates is sawable. The description of the stack of wooden crates is "It was convenient, Dr. Law, that all of the other arena props came in these nice crates."

The unopened bottle is in MEDUSA Arena.  The unopened bottle is smashable. The unopened bottle is gunnable and listnowable. The unopened bottle is sawable. The description of the unopened bottle is "A bottle that is, sadly, unopened."

The opened bottle is nowhere. The opened bottle is smashable. The opened bottle is gunnable and listnowable. The opened bottle is sawable. The description of the opened bottle is "A bottle that is cheerily opened."

The small sapling is in MEDUSA Arena.  The small sapling is flammable. The sapling is smashable and listnowable. The sapling is sawable. The small sapling is gunnable. The description of the small sapling is "I'm not really sure why we brought this in, Dr. Law. There may have been a mixup with the gardening department.

Now that I think about, this might mean there is a land mine planted in the back garden. I'll make a note to the gardening staff."

The stone boulder is in MEDUSA Arena. The stone boulder is smashable and listnowable. The description of the stone boulder is "This boulder was surprisingly hard to obtain, Dr. Law, given that Stygicorp is surrounded by boulders."

The wooden hatch is in MEDUSA Arena. "A wooden hatch is in the center of the floor." The wooden hatch is smashable.  The wooden hatch is flammable. The wooden hatch is sawable. The wooden hatch is gunnable.  The description of the wooden hatch is "This is a wooden hatch covering something on the floor. It seems quite fragile, Dr. Law."

The description of MEDUSA Arena is "You see a circular room with brick walls and a floor covered in sand. [if the player is Angel]There are no obvious exits.[otherwise]The MEDUSA Living Quarters are to the [bold type]south[roman type].[end if]

[ScatterText]"

To say ScatterText:
	say "[if the number of listnowable things in Medusa Arena > 1]Scattered about the room [otherwise]In this room, there is [end if]"; 
	repeat with current running through listnowable things in Medusa Arena:
		now current is marked for listing;
	list the contents of the Medusa Arena, as a sentence, 
		tersely, listing marked items only, prefacing with is/are, 
		including contents and giving brief inventory information; 
	say ". "

Rule for listing nondescript items of MEDUSA Arena: 
	do nothing;

Report hammering the wooden hatch:
	now the wooden hatch is nowhere;
	say "A dark ramp is now visible, leading downward.";
	now the dark ramp is in MEDUSA Arena;

Report flaming the wooden hatch:
	now the wooden hatch is nowhere;
	say "A dark ramp is now visible, leading downward.";
	now the dark ramp is in MEDUSA Arena;
	
Report sawing the wooden hatch:
	now the wooden hatch is nowhere;
	say "A dark ramp is now visible, leading downward.";
	now the dark ramp is in MEDUSA Arena;
	
Report gunning the wooden hatch:
	now the wooden hatch is nowhere;
	say "A dark ramp is now visible, leading downward.";
	now the dark ramp is in MEDUSA Arena;

The dark ramp is a thing. "A dark ramp is in the center of the floor, leading downward." The dark ramp is not portable. The description of the dark ramp is "This ramp seems to lead downward, Dr. Law."

Instead of entering the dark ramp:
	try going down;

To damage the robot:
	now HitCounter is HitCounter plus one;
	if HitCounter is 1:
		say "[line break]Angel's laser has been damaged, Dr. Law. It is no longer functional.";
	if HitCounter is 2:
		say "[line break]Angel's chainsaw has been damaged, Dr. Law. It is no longer functional.";
	if HitCounter is 3:
		say "[line break]Angel's hammer has been damaged, Dr. Law. It is no longer functional.";
	if HitCounter is 4:
		say "[line break]Angel's flamethrower has been damaged, Dr. Law. It is no longer functional.";
	if HitCounter is 5:
		say "[line break]Angel's bottle opener has been damaged, Dr. Law. It is no longer functional.";
		if the player is in Wild West Challenge:
			if the number of drones in the location of the player > 0:
				let target be a random drone in Wild West;
				say "[line break]A window opens in the train below you, and a conductor drone sticks it head out. It aims a shotgun at the [target], and blows it to pieces. It winks at you, and pulls back into the train, closing the window behind it.";
				now target is nowhere;
	if HitCounter is 6:
		say "[line break]Angel will be destroyed if it gets hit again, Dr. Law. The resulting explosion could destroy the wifi in the area, and possibly set off a nuclear armageddon if it triggers the reactor core of the projects.";
		if the player is in Wild West Challenge:
			if the number of drones in the location of the player > 0:
				let target be a random drone in Wild West Challenge;
				say "[line break]The conductor pops out again, and throws a stick of lit dynamite at the [target], blowing it to pieces. It winks at you, and points to the top of the train.";
				now target is nowhere;
	if HitCounter > 6:
		say "[line break]Angel's core is leaking radiation, Dr. Law; I am engaging the Kronos device to restore Angel to the point directly before being hit. 

[CorruptText]"

The conductor is  in Wild West Challenge. The conductor is scenery. The description of the conductor is "The conductor of the train is not visible at this time". The conductor's window is in Wild West Challenge. The description of the conductor's window is "The window is not visible at this time." The conductor's window is scenery.
		
To say CorruptText:
	say "[one of]The radiation may hve damged my core langage procesors, tho.[or]The rdiation my hve damged m core langage prcsors, tho.[or]Th rdion m hve damged m core lagage prcsors, ho.[or]Them agago.[or]Hag.[or]Ha.[stopping]"

Section 2 - First challenge

A drone is a kind of person. A drone can be active or inactive. A drone is usually inactive. A drone is usually smashable. [remove this later]

[remove this bottling thing]
[Check bottling a drone:
	say "Angel opens up the drone and destroys it.";
	now the noun is nowhere instead;]
	
TestingArea is a region. [Urban Challenge is in TestingArea.] Wild West Challenge is in TestingArea. [Pirate Challenge is in TestingArea.] Pirate Challenge is in TestingArea. Final Challenge is in TestingArea.

Instead of going up when the player is in TestingArea:
	say "The ramp is now gone, Dr. Law.";

TotalMEDUSA is a region. MEDUSAArea, TinyArea, and TestingArea are in TotalMEDUSA.

Instead of MEDUSAing when the player is in TotalMEDUSA:
	say "You are already viewing Project MEDUSA, Dr. Law."

To say pausingtime:
	say "[paragraph break]Are you ready?";
	wait for any key;

[The Urban Challenge is down from MEDUSA Arena. The description of the Urban Challenge is "[first time]You roll into a large, dark space. The light behind disappears.

You hear a voice say, 'Testing Protocol Gamma-17 initiated. First Challenge: Urban Environment.'[pausingtime]

*********************************************[line break]Beginning testing...[line break]*********************************************

The lights go on. [only]You appear to be in a burned-out urban environment of grey stone, brown brick and broken glass."

The abandoned factory is in Urban Challenge. "You see an abandoned factory here, about two stories tall, with two rows of broken windows. It is partially supported by rickety scaffolding." The description of the abandoned factory is "The wall of the factory is crumbling and weak. It's made partly of cinderblock and partly of brick. The scaffolding is supporting it."


The rickety scaffolding is in the Urban Challenge. The rickety scaffolding is scenery. The rickety scaffolding is a supporter. Understand "worm-eaten" as rickety scaffolding. The description of the rickety scaffolding is "Worm-eaten scaffolding is wrapped around the building. Severeal iron nails are coming loose."

The iron nails are part of the rickety scaffolding.  The description of the iron nails is "The iron nails are falling out."

The pile of rubble is nowhere. "All that is left of the building and scaffolding is a pile of rubble."

The urban drone is a drone. The urban drone is nowhere.  "You can see an assault drone on the side of one building, shielded by the rickey scaffolding". The description of the urban drone is "The drone is blocked by the scaffolding. It's wearing a yellow construction hat and an orange vest."  Understand "assault" or "yellow" or "orange" or "construction" or "helmet" or "orange" or "vest" or "assault drone" as urban drone.

UrbanDroneScene is a scene. UrbanDroneScene begins when the player is in Urban Challenge. UrbanDroneScene ends when the factory is nowhere.

When UrbanDroneScene begins:
	say "You hear a voice calling from far away. 'Wait, wait, wait!'

An assault drone appears, wearing a hard hat and an orange vest. It climbs up onto the rickety scaffolding, and hides inside of it.

'Okay,' it says. 'We are ready. '"


UrbanCounter is a number that varies. UrbanCounter is 0.

Every turn during UrbanDroneScene:
	now UrbanCounter is UrbanCounter plus one;


To destroy the factory:
	now the abandoned factory is nowhere;
	now the rickety scaffolding is nowhere;
	now urban drone is nowhere;
	now the pile of rubble is in Urban Challenge;
	say "[paragraph break]An opening appears in the floor, and a ramp leads down.  A voice says, 'First Challenge complete. Please proceed downwards for the next challenge.'";

Check hammering the abandoned factory:
	say "You smash the factory wall with your hammer. The building wobbles and falls in a cloud of dust, sending scaffolding spinning. The drone wails and disappears in the rubble. ";
	destroy the factory instead;
		
Check of flaming the rickety scaffolding:
	say "The scaffolding goes up in flames. The assault drone panics and wails in a robotic voice. The scaffolding gives way, and the factory and scaffolding collapse together, burying the drone. ";
	destroy the factory instead;
	
Check gunning the rickety scaffolding:
	say "The scaffolding disappears in a hail of bullets. The assault drone panics and wails in a robotic voice. The scaffolding gives way, and the factory and scaffolding collapse together, burying the drone. ";
	destroy the factory instead;
	
Check sawing the rickety scaffolding:
	say "The chainsaw tears through the rickety scaffolding like butter. The assault drone panics and wails in a robotic voice. The scaffolding gives way, and the factory and scaffolding collapse together, burying the drone. ";
	destroy the factory instead;

Check lasing the urban drone:
	say "The laser pierces through the drone. The drone glows white-hot, then explodes.. The explosion rocks the factory and scaffolding, sending it tumbling to the ground. ";
	destroy the factory instead;

Check hammering the urban drone:
	say "The drone is high, on the scaffolding, Dr. Law." instead;
	
Check flaming the urban drone:
	say "The flames envelope the drone. The drone glows white-hot, then explodes.. The explosion rocks the factory and scaffolding, sending it tumbling to the ground. ";
	destroy the factory instead;

Check gunning the urban drone:
	say "The bullets pierce through the drone. The drone glows white-hot, then explodes.. The explosion rocks the factory and scaffolding, sending it tumbling to the ground. ";
	destroy the factory instead;

Check sawing the urban drone:
	say "The drone is up too high to cut with the chainsaw, Dr. Law. ";
	destroy the factory instead;

Check hammering the iron nails:
	say "The drone salutes you, and thanks you for helping to rebuild New Drone City. It turns cheerfully back to its construction work.";
	now abandoned factory is nowhere;
	now rickety scaffolding is nowhere;
	now urban drone is nowhere;
	say "An opening appears in the floor, and a ramp leads down.  The lights in the room dim. A voice says, 'First Challenge complete. Please proceed downwards for the next challenge.'" instead;

[Allowed commands:[line break]
las - fire laser[line break]
ham - swing hammer[line break]
fir - shoot flame[line break]
bot - open bottle[line break]
gun - shoot gun[line break]
saw - use chainsaw[line break]
n,e,s,w - movement[only]".]
]
Check going down from  MEDUSA Arena:
	if the player is Angel:
		if the wooden hatch is in MEDUSA Arena:
			say "The wooden hatch blocks your way. " instead;
	otherwise:
		say "The cameras are not operational in that direction, Dr. Law." instead;

Section 3 - Second Challenge

The printed name of Wild West Challenge is "Wild West Challenge (as Angel)".

Instead of going nowhere from Wild West Challenge:
	say "You don't want Angel to fall off the train, Dr. Law!";

The outlaw drone is a drone. The description of the outlaw drone is "A wild outlaw with a black hat." Understand "wild" or "black" or "hat" as the outlaw drone. The bandit drone is a drone. The description of the bandit drone is "A drone with soulful eyes and a white hat." Understand "soulful" or "eye" or "eyes" or "white" or "hat" as the bandit drone.

The outlaw drone is in Wild West Challenge. The outlaw drone is scenery.

The bandit drone is in Wild West Challenge. The bandit drone is scenery.

To say behindbandit:
	say "[if bandit drone is in Wild West Challenge], close to the bandit[otherwise] behind the train; the horse is bucking and making electronic noises[end if]."

Wild West Challenge is down from MEDUSA Arena. The description of Wild West Challenge is "[first time]The robot rolls down the ramp into complete darkness. 

The ramp retracts into the ceiling, and the lights go on. You hear Dr. Long's recorded voice saying: 'First Challenge: Wild West Environment'. Dr. Ng's voice chimes in, saying, 'Now with three times the firepower!' The camera feed jiggles; Angel seems to be on a platform. The lights go on.

[only]You see a room painted with murals of red-rock desert country. Angel is standing on top of a train that is rocking and bumping on hydraulics; hidden speakers are playing train noises[first time]. The train is being pursued by an outlaw drone and a bandit drone, riding stationary mechanical horses[only].  

The train [if the number of bucking things in Wild West Challenge is 2]and horses are bucking wildly[otherwise if the bandit's horse is bucking]and the bandit's horse are bucking wildly[otherwise if the outlaw's horse is bucking]and the outlaw's horse are bucking wildly[otherwise ]is bucking wildly[end if], making it hard to aim."

The wildwestlights are scenery in Wild West Challenge. The printed name of the wildwestlights is "lights". Understand "light" or "lights" as the wildwestlights. The description of the wildwestlights is "The lights are discreetly hidden."

The murals of red-rock desert country are scenery in Wild West Challenge. The description of the murals of red-rock desert country is "The murals show a desert country filled with red-rock."

The train is in Wild West Challenge. The train is scenery. Understand "hydraulic" or "hydraulics" as the train. The description of the train is "This is a platform designed to look like the top half of a train. I can't see them, but I assume there are hydraulics underneath, because it's rumbling."

The outlaw's horse is in Wild West Challenge. The bandit's horse is in Wild West Challenge. The outlaw's horse is scenery. The bandit's horse is scenery.

The description of the outlaw's horse is "The outlaw's horse is ash-grey and bucking wildly. Its lower half is gone, replaced by poles and gears". Understand "ash" or "mechanical" or "grey" or "gray" or "poles" or "gears" or "pole" or "gear" as the outlaw's horse.

The description of the bandit's horse is "The outlaw's horse is dark brown and bucking wildly. Its lower half is gone, replaced by poles and gears". Understand "dark" or "brown" or "mechanical" or "poles" or "gears" or "pole" or "gear" as the outlaw's horse.

[las - fire laser[line break]
ham - swing hammer[line break]
fir - shoot flame[line break]
bot - open bottle[line break]
gun - shoot gun[line break]
saw - use chainsaw[line break]]

The outlaw's horse can be bucking or not bucking. The bandit's horse can be bucking or not bucking. The outlaw's horse is bucking. The bandit's horse is bucking.

Instead of lasing the outlaw drone:
	if the outlaw's horse is bucking:
		say "The rocking of the train and the horse makes the laser go wild, Dr. Law. You are unable to lase the drone, it's too small a target.";
	otherwise:
		say "Now that the horse isn't moving, it's easier to aim. The laser pierces the drone, which explodes.";
		now the outlaw drone is nowhere;

Instead of lasing the bandit drone:
	if the bandit's horse is bucking:
		say "The rocking of the train and the horse makes the laser go wild, Dr. Law. You are unable to lase the drone. It's too small a target.";
	otherwise:
		say "Now that the horse isn't moving, it's easier to aim. The laser pierces the drone, which explodes.";
		now the bandit drone is nowhere;

Instead of lasing the outlaw's horse:
	if the outlaw's horse is not bucking:
		say "It's hard to aim, but the laser pierces through the bandit's horse. It stops bucking.";
		now the outlaw's horse is not bucking;
	otherwise:
		say "The outlaw's horse has already been disabled.";

Instead of lasing the bandit's horse:
	if the bandit's horse is not bucking:
		say "It's hard to aim, but the laser pierces through the bandit's horse. It stops bucking.";
		now the bandit's horse is not bucking;
	otherwise:
		say "The bandit's horse has already been disabled.";

Instead of hammering the outlaw drone:
	say "The drone and its horse are too far away, Dr. Law."
	
Instead of hammering the bandit drone:
	say "The drone and its horse are too far away, Dr. Law."

Instead of hammering the outlaw's horse:
	try hammering the outlaw drone;
	
Instead of hammering the bandit's horse:
	try hammering the bandit drone;

Instead of flaming the outlaw drone:
	say "The flames envelope the drone and the horse. The drone glows white-hot, then explodes. A cleaner bot swoops down to vacuum the ashes, then disappears.";
	now the outlaw drone is nowhere;
	now the outlaw's horse is nowhere;
	now the outlaw's horse is not bucking;

Instead of flaming the bandit drone:
	say "The flames envelope the drone and the horse. The drone glows white-hot, then explodes. A cleaner bot swoops down to vacuum the ashes, then disappears.";
	now the bandit drone is nowhere;
	now the bandit's horse is nowhere;
	now the bandit's horse is not bucking;

Instead of flaming the outlaw's horse:
	try flaming the outlaw drone;

Instead of flaming the bandit's horse:
	try flaming the bandit drone;

Instead of gunning the outlaw drone:
	if the outlaw's horse is bucking:
		say "The rocking train makes the bullets spray wildly. They miss the drone, but hit the horse. Sparks fly, and the horse stops moving.";
		now the outlaw's horse is not bucking;
	otherwise:
		say "With the horse not moving, it's easy to attack the drone. It disintegrates in a hail of bullets.";
		now the outlaw drone is nowhere;

Instead of gunning the bandit drone:
	if the bandit's horse is bucking:
		say "The rocking train makes the bullets spray wildly. They miss the drone, but hit the horse. Sparks fly, and the horse stops moving.";
		now the bandit's horse is not bucking;
	otherwise:
		say "With the horse not moving, it's easy to attack the drone. It disintegrates in a hail of bullets.";
		now the bandit drone is nowhere;

Instead of gunning the outlaw's horse:
	if the outlaw's horse is bucking:
		say "The rocking train makes the bullets spray wildly. They miss the drone, but hit the horse. Sparks fly, and the horse stops moving.";
		now the outlaw's horse is not bucking;
	otherwise:
		say "The outlaw's horse is already defeated, Dr. Law.";

Instead of gunning the bandit's horse:
	if the bandit's horse is bucking:
		say "The rocking train makes the bullets spray wildly. They miss the drone, but hit the horse. Sparks fly, and the horse stops moving.";
		now the bandit's horse is not bucking;
	otherwise:
		say "The bandit's horse is already defeated, Dr. Law.";

Instead of sawing the outlaw drone:
	say "The drone is too far away, Dr. Law.";

Instead of sawing the bandit drone:
	say "The drone is too far away, Dr. Law.";
	
Instead of sawing the outlaw's horse:
	say "The horse is too far away, Dr. Law.";

Instead of sawing the bandit's horse:
	say "The horse is too far away, Dr. Law.";

Every turn when the player is in Wild West Challenge:
	repeat with current running through drones in Wild West Challenge:
		say "[one of][The current] draws its gun and aims it at you.[or][The current] pulls out its revolver and points it at you.[or][The current] shoots at you, but the shot goes wild.[or][The current] whoops and hollers, shooting its gun in the air.[or][The current] aims carefully, and shoots. [DroneDamage][stopping][conditional paragraph break]";
		
To say DroneDamage:
	damage the robot;

Every turn when the player is in Wild West Challenge:
	if the number of drones in Wild West < 1:
		say "[one of]A voice speaks, saying: 'First Challenge Successful. Please proceed to the next Challenge.'

A ramp in the top of the train opens [bold type]down[roman type]wards.[or]There is a ramp in the floor here going [bold type]down[roman type].[stopping][makeramp]"

To say makeramp:
	now the dark ramp is in Wild West Challenge;

Section 4 - Pirate Challenge

The printed name of Pirate Challenge is "Pirate Challenge (as Angel)". 

Check going down from Wild West Challenge:
	if outlaw drone is in Wild West Challenge:
		say "There is not a path downward at this time, Dr. Law." instead;
	otherwise if bandit drone is in Wild West Challenge:
		say "There is not a path downward at this time, Dr. Law." instead;

Pirate Challenge is down from Wild West Challenge. The description of Pirate Challenge is "[first time]Angel travels down the ramp into darkness. The ramp retracts into the ceiling. Dr. Long's voice says: 'Second Challenge: Ocean Environment.' Dr. Ng's voice chimes in, saying, 'Now with pirates!'

The lights go on. [only]The robot is on the deck of a wooden sailing ship, in the rain[if the pirate ship is in Pirate Challenge]. A dark ship flying the skull and crossbones lies nearby. The pirate ship is teeming with pirate drones[end if]."

The rain is in Pirate Challenge. The rain is scenery. Understand "water" as the rain. The description of the rain is "The rain pours down from above, into the water below."

The pirate ship is in Pirate Challenge. The pirate ship is scenery.  Understand "portholes" or "belowdecks" or "porthole" as the pirate ship. The description of the pirate ship is "A dark ship flying the skull and crossbones. It is teeming with pirate drones." Understand "dark" or "ship" as the pirate ship.

Understand "drones" as the pirate ship.

The pirate flag is a part of the pirate ship. The pirate flag is scenery. Understand "skull" or "crossbones" as the pirate flag.

Instead of examining the pirate flag:
	try examining the pirate ship.

The wooden rail is in Pirate Challenge. The wooden rail is scenery. The wooden rail is gunnable. Understand "railing" as rail. The description of the wooden rail is "'My liege, his railing is intolerable.'-Shakespeare. 

Also, it is protecting the pirate captain, Dr. Law."

The rigging is in Pirate Challenge. The rigging is gunnable. The rigging is scenery. "This appears to be high-quality rope, Dr. Law, but tied very poorly."

The schooner is in Pirate Challenge. The schooner is scenery. The description of the schooner is "Angel's schooner is in better condition than the pirate ship."

Instead of gunning the wooden rail:
	say "The railing explodes into splinters, but the captain is unharmed. [if the rigging is nowhere]It dives belowdecks.[otherwise]It leaps up into the rigging.[end if]";
		now the wooden rail is nowhere;

Instead of lasing the wooden rail:
	say "The railing is reduced to cinders, but the captain is unharmed. [if the rigging is nowhere]It dives belowdecks.[otherwise]It leaps up into the rigging.[end if]";
		now the wooden rail is nowhere;

Report gunning the rigging:
	if the wooden rail is nowhere:
		say "The pirate captain leaps out of the way. The rigging is shredded to pieces. The captain dives belowdecks with a curse.";

Report lasing the rigging:
	if the wooden rail is nowhere:
		say "The pirate captain leaps out of the way. The rigging is shredded to pieces. The captain dives belowdecks with a curse.";

Instead of lasing the pirate ship:
	say "The laser misses the captain, and gouges a huge gash into the ship. The ship slowly sinks into the water, with all pirates jumping in and swimming away[if the pirate drone is in Pirate Challenge], including the pirate on your schooner[end if].

The captain remains with the ship until the end, saluting you as it sinks under the water.

A ramp opens [bold type]down[roman type] in your schooner.";	now the captain drone is nowhere;
	now the rigging is nowhere;
	now the wooden rail is nowhere;
	now the pirate ship is nowhere;
	now the pirate drone is nowhere;
	now the dark ramp is in Pirate Challenge;
	
Instead of gunning the pirate ship:
	say "The gun misses the captain, and sprays holes all over the ship. The ship slowly sinks into the water, with all pirates jumping in and swimming away[if the pirate drone is in Pirate Challenge], including the pirate on your schooner[end if].

The captain remains with the ship until the end, saluting you as it sinks under the water.

A ramp opens [bold type]down[roman type] in your schooner, Dr. Law.";
	now the captain drone is nowhere;
	now the rigging is nowhere;
	now the wooden rail is nowhere;
	now the pirate ship is nowhere;
	now the pirate drone is nowhere;
	now the dark ramp is in Pirate Challenge;

The captain drone is in Pirate Challenge.  "[if the wooden rail is in Pirate Challenge]The pirate captain drone stalks the deck of the pirate ship, shouting orders. It is almost completely screened by the wooden rail.[otherwise if the rigging is in Pirate Challenge]The captain swings from the rigging, shouting orders. It clambers about at an incredible speed.[otherwise]The captain runs back and forth belowdecks, visible through the portholes." The description of the captain drone is "A tall, elegant drone with a feathered red cap." Understand "feathered" or "feather" or "red" or "cap" or "tall" or "elegant" as the captain drone.

The pirate drone is a person. The description of the pirate drone is "A short, surly drone with a cutlass." Understand "short" or "surly" or "cutlass" as the pirate drone.

PirateCounter is a number that varies. PirateCounter is 2.

The captain drone is gunnable.

Instead of gunning the captain drone:
	if the wooden rail is in Pirate Challenge:
		say "The pirate captain ducks behind the railing. The railing explodes into splinters, but the captain is unharmed. It leaps up into the rigging.";
		now the wooden rail is nowhere;
	otherwise if the rigging is in Pirate Challenge:
		say "The pirate captain leaps out of the way. The rigging is shredded to pieces. The captain dives belowdecks.";
		now the rigging is nowhere;
	otherwise if the pirate ship is in Pirate Challenge:
		say "The gun misses the captain, and sprays holes all over the ship. The ship slowly sinks into the water, with all pirates jumping in and swimming away[if the pirate drone is in Pirate Challenge], including the pirate on your schooner[end if].

The captain remains with the ship until the end, saluting you as it sinks under the water.

A ramp opens [bold type]down[roman type] in your schooner.";
		now the captain drone is nowhere;
		now the pirate ship is nowhere;
		now the pirate drone is nowhere;
		now the dark ramp is in Pirate Challenge;

Instead of lasing the captain drone:
	if the wooden rail is in Pirate Challenge:
		say "The pirate captain ducks behind the railing. The railing is reduced to cinders, but the captain is unharmed. It leaps up into the rigging.";
		now the wooden rail is nowhere;
	otherwise if the rigging is in Pirate Challenge:
		say "The pirate captain leaps out of the way. The rigging is shredded to pieces. The captain dives belowdecks.";
		now the rigging is nowhere;
	otherwise if the pirate ship is in Pirate Challenge:
		say "The laser misses the captain, and gouges a huge gash into the ship. The ship slowly sinks into the water, with all pirates jumping in and swimming away[if the pirate drone is in Pirate Challenge], including the pirate on your schooner[end if].

The captain remains with the ship until the end, saluting you as it sinks under the water.

A ramp opens [bold type]down[roman type] in your schooner.";
		now the captain drone is nowhere;
		now the pirate ship is nowhere;
		now the pirate drone is nowhere;
		now the dark ramp is in Pirate Challenge;

Every turn when the pirate drone is in Pirate Challenge:
	if the player is in Pirate Challenge:
		say "[one of]The pirate advances menacingly.[or]The pirate races forward, slicing Angel with its cutlass.[DroneDamage][or]The pirate pulls out a dagger and throws it at Angel.[DroneDamage][or]The pirate slashes at Angel.[DroneDamage][stopping] ";

Every turn when the player is in Pirate Challenge:
	if the captain drone is in Pirate Challenge:
		if the pirate drone is nowhere:
			now PirateCounter is PirateCounter plus one;
		if PirateCounter is 3:
			now PirateCounter is 0;
			say "The captain points to a pirate drone and barks out an order. The pirate drone charges and swings across to your schooner, brandishing its cutlass.";
			now the pirate drone is in Pirate Challenge;

Instead of lasing the pirate drone:
	say "The laser pierces through the drone. The drone glows white-hot, then explodes.";
	now the pirate drone is nowhere;

Instead of hammering something in Pirate Challenge:
	if the noun is the pirate drone:
		say "The drone is smashed into pieces, which are swept off by the rain.";
		now the pirate drone is nowhere;
	otherwise if the noun is the schooner:
		say "Angel's self-preservation programming is overriding your command.";
	otherwise:
		say "[The noun] is too far away to hammer!";
	
Instead of flaming something in Pirate Challenge:
	say "The flames sputter and flicker in the rain. [if the noun is the pirate drone]The pirate drone sneers. [end if]The flamethrower is ineffective!";

Instead of gunning the pirate drone:
	say "The bullets pierce through the drone. The drone glows white-hot, then explodes.";
	now the pirate drone is nowhere;

Instead of sawing something in Pirate Challenge:
	if the noun is the pirate drone:
		say "The drone is chopped into pieces, Dr. Law, which are swept off by the rain.";
		now the pirate drone is nowhere;
	otherwise if the noun is the schooner:
		say "Angel's self-preservation programming is overriding your command.";
	otherwise:
		say "That's too far away to saw!";

Instead of bottling something in Pirate Challenge:
	say "The bottle opener is ineffective in this current situation, Dr. Law. If we find rum later, we can pull it out again.";

Section 5 - Final Challenge

The printed name of Final Challenge is "Final Challenge (as Angel)".

Check going down from Pirate Challenge:
	if captain drone is in Pirate Challenge:
		say "There is not a path downward at this time, Dr. Law." instead;

Final Challenge is down from Pirate Challenge. The description of Final Challenge is "[if the red lamps are not in Final Challenge]It's much darker now, thanks to you. [end if][first time]Angel travels down the ramp into the darkness. The ramp retracts into the ceiling. Dr. Ng's voice speaks alone, saying, 'Final Challenge: Drones Playing Poker'.

The lights go on. [only][if the poker table is in Final Challenge]Angel is pulled up to a circular green felt table with six drones, most with a cigar, cigarette, or pipe. There is a rootbeer bottle on the table. Angel has been dealt three aces and a few other cards. The drone next to Angel passes a fourth ace under the table. A pipe-smoking drone watches you expectantly. 

It looks like the current bet is $1000. You are the last one to make your bet.

[bold type]-New commands-[roman type][line break]
 fol - fold[line break]
 cal - call[line break]
 rai - raise[otherwise]You are in a parlor with grey walls a ramp leading [bold type]down[roman type]. You are almost at your journey's end.[end if]"


The grey walls are in Final Challenge. The grey walls are scenery.  The description of the grey walls is "The grey walls have a pleasing texture." Understand "pleasing" or "gray" or "texture" as the grey walls.

The red lamps are in Final Challenge. The red lamps are scenery. The description of the red lamps is "Red lamps hang down, casting the room into shadow outside their pools of light." The red lamps are plural-named. The red lamps are smashable. The red lamps are gunnable. The red lamps are sawable. The red lamps are flammable.

The poker table is in Final Challenge. The poker table is flammable. The poker table is scenery. The poker table is a supporter. The description of the poker table is "A circular, green felt poker table covered in chips and cards." Understand "circular" or "green" or "felt" as the poker table.

The chips are scenery. The chips are plural-named. The chips are on the poker table. The description of the chips is "There are dozens of multicolored chips on the table." Understand "dozens" or "dozen" or "multicolored" as the chips.

The cards are scenery. The cards are on the poker table. The description of the cards is "Everyone has a hand, the dealer has a deck, and there is a muck." Understand "muck" or "hand" or "deck" as the cards. The cards are plural-named.

Folding is an action applying to nothing.  Understand "fol" as folding.

The cigar-smoking drone is a drone. The cigar-smoking drone is scenery. The cigar-smoking drone is in Final Challenge. Understand "cigar" as cigar-smoking drone. The description of the cigar-smoking drone is "A drone with a cigar drooping out."

The pipe-smoking drone is a drone. The pipe-smoking drone is scenery. The pipe-smoking drone is in Final Challenge. Understand "pipe" as the pipe-smoking drone. The description of the pipe-smoking drone is "A drone smoking a pipe. Not particularly like Sherlock Holmes."

The dealer drone is a drone. The dealer drone is scenery. The dealer drone is in Final Challenge. The description of the dealer drone is "A visor-wearing drone dealing cards". Understand "visor" as the dealer drone.

The drone next to you is a drone. The drone next to you is scenery. The drone next to you is in Final Challenge. The description of the drone next to you is "A friendly drone, who seems to want to help." Understand "friendly" or "drone next to me" as the drone next to you.

The cynical drone is a drone. The cynical drone is scenery. The cynical drone is in Final Challenge. The description of the cynical drone is "The cynical drone leans back, unimpressed."

The cigarette-smoking drone is a drone. The cigarette-smoking drone is scenery. The cigarette-smoking drone is in Final Challenge. Understand "cigarette" as the cigarette-smoking drone. The description of the cigarette-smoking drone is "A cigarette hangs from the drone's... well, it doesn't have lips. I'm not sure how it's managing to smoke that, Dr. Law."

The unimportant details are in Final Challenge. The unimportant details are scenery. Understand "chair" or "chairs" or "clock" or "painting" or "ceiling" or "floor" as the unimportant details. The description of the unimportant details is "These other objects in the room do not merit your attention, Dr. Law." The unimportant details are plural-named.

Instead of gunning, lasing, flaming, sawing, or hammering a drone in Final Challenge:
	say "The cynical drone glares at you, pointing to a nearby sign. The sign says, 'No disintegrations.'"

A thing can be pokerated or not pokerated. A thing is usually not pokerated. The chips are pokerated. The cards are pokerated. The poker table is pokerated.

Instead of gunning, lasing, flaming, sawing, or hammering a pokerated thing:
	say "Angel raises its arm, but all of the drones draw on you at the same time. Angel lowers its arm, and the game continues."
	
The nearby sign is in Final Challenge. The nearby sign is scenery. The description of the nearby sign is "The sign says 'No disintegrations.'"

The rootbeer bottle is scenery in Final Challenge. The description of the rootbeer bottle is "An unopened rootbeer bottle." The rootbeer bottle is smashable. The rootbeer bottle is gunnable. The rootbeer bottle is sawable.

Carry out folding:
	if the player is in Final Challenge:
		if the poker table is in Final Challenge:
			say "Angel throws the cards in the muck. The drone next to Angel sighs.

The cigar-smoking drone throws down a full house. It chortles, and collects all the chips.

The drones call it a night, throwing down the cards and filing out one by one. 

You played it safe, Dr. Law. Your funds have increased by $0.

The table flips over and becomes a ramp going [bold type]down[roman type].";
			repeat with current running through things on the poker table:
				now current is nowhere;
			now the poker table is nowhere;
			now the dark ramp is in Final Challenge;
		otherwise:
			say "The poker game is over, Dr. Law.";
	otherwise:
		say "Computing...Understanding 'FOL' as 'folic acid'. I accept your dietary recommendation, Dr. Law.";

Calling is an action applying to nothing. Understand "cal" as calling.

Carry out calling:
	if the player is in Final Challenge:
		if the poker table is in Final Challenge:
			say "Angel pushes 20 chips forward. The drones look at each other, straight-faced. 

'Show us what you've got,' growls a pipe-smoking drone.

Angel lays down the cards. Everyone groans, except for the drone next to you, who gives you a wink. The drones file out one at a time.

It seems your funds have gone up by $12,000, Dr. Law. I'll invest it for you in a money market mutual fund. It should be available to withdraw in one year.

The poker table rotates over, and becomes a dark ramp leading [bold type]down[roman type]ward.";
			now the poker table is nowhere;
			now the dark ramp is in Final Challenge;
		otherwise:
			say "The poker game is over, Dr. Law.";
	otherwise:
		say "Computing...Understanding 'CAL' as 'CALUMINATE'. Very well: 'Dr. Law thinks a square root is a kind of vegetable.' Was that good?";

Raising is an action applying to nothing. Understand "rai" as raising.

Carry out raising:
	if the player is in Final Challenge:
		if the poker table is in Final Challenge:
			say "Angel pushes all of its chips into the pot. The room goes silent. The drone next to you shakes its head.

Angel lays down its cards. Four aces.

The cigarette-smoking drone grins and laughs mechanically (and maniacally). It lays down a royal flush, in hearts. A vacuum attachment extends from its chest to suck up the chips.

The drone next to Angel pats it and whispers, 'Sorry!' before heading out with the other drones.

It seems that you've lost $6000, Dr. Law. I'm sorry.

The poker table flips over and becomes a dark ramp travelling [bold type]down[roman type].";
			now the poker table is nowhere;
			now the dark ramp is in Final Challenge;
		otherwise:
			say "The poker game is over, Dr. Law.";
	otherwise:
		say "Computing...Understanding 'RAI' as the Japanese word for lightning. I'm sorry Dr. Law, but artificial lightning requires a larger power source than is available to me.";

Section 6 - Finishing off

AngelOver is a number that varies. AngelOver is 0.

Check going nowhere from Final Challenge when the noun is down:
	if the poker table is in Final Challenge:
		say "The floor is in your way, Dr. Law." instead;
	otherwise:
		now the printed name of Final Challenge is "Pomp and Circumstance";
		redraw the status line;
		now the player is Hacker;
		say "Angel is in darkness in a large room.

The lights go on. The room is a bare concrete cube, with a large banner that says 'Congratulations Angel!'

'Pomp and Circumstance' plays on a small, tinny speaker. A small panel opens in the ceiling to release confetti, but it jams halfway.

A door opens in the wall, and a conveyor belt in the floor begins moving Angel out.

I'll take it from here, Dr. Law; Angel will be in the command center awaiting your final judgment.

You have completed your review of Project MEDUSA. Your connection with Angel is severed. Returning you to your last known location....";
		wait for any key;
		now AngelOver is 1;
		try looking;
		say "*******************************************************[line break]Project MEDUSA Complete[line break]*******************************************************";
		further the finalization;
		say "[bold type]-Commands-[roman type][line break] hom - home folder[line break] hyd, med, moi - Observe Project HYDRA, MEDUSA, or MOIRAI[line break]" instead;

Chapter 4 - The Lobby

MEDUSA Lobby is south from MEDUSA Living Quarters. [sleeper is here]

[[first time]To be frank, Dr. Law, my image recognition algorithms initially labeled this project's workspace as a 'dungeon'. I can only imagine how the employees feel.[paragraph break][only]]

The description of MEDUSA Lobby is "The Project MEDUSA lobby is a squat, dim and dirty steel cylinder. The entrance to the laboratory to the [bold type]north[roman type] is covered with an elaborate engraving of Medusa's head, with the snakes extending around to fill the walls. On the floor is a carving of Perseus, his eyes covered by one hand, and the other hand raised in defense. Where his shield should be is a circular depression[if the circular depression is full] that has filled with water to become a pool[end if].

The camera doesn't point up, Dr. Law, but I know that the ceiling is bare except for a few recessed lights and a centrally located fire sprinkler. The sprinkler is [if the central sprinkler is powered]spraying water into the pool, but a hidden drain keeps it from overflowing[otherwise]not turned on[end if].

I detect one connected device, Dr. Law.[first time][paragraph break]Oh, Dr. Law, I just remembered. Dr. Ng went to bed almost an hour ago. She's really uptight about security, you know, especially since her partner quit. She said that she was going to password protect the lab, but that she'd leave the password 'in a way that Dr. Law will understand.'[CommandListHint][only]"

Perseus is in MEDUSA Lobby. Perseus is scenery. The description of Perseus is "Dr. Law, this is a depiction of the ancient Greek hero Perseus. Although, he looks like Steve Jobs in this engraving.

He is carrying a shield, which is [if the circular depression is dry]just an empty, circular depression[otherwise]a pool of water. You can see the reflection of the Medusa in the water[end if]."  Understand "carving" or "eyes" or "eye" or "hand" or "defense" as Perseus.

The recessed lights are in MEDUSA Lobby. The recessed lights are scenery. The description of the recessed lights is "The ceiling is mostly empty except for some recessed lights.

A sprinkler sticks out of the middle of the ceiling."

Medusa's Head is in MEDUSA Lobby. Medusa's Head is scenery. Understand "engraving"or "423" or "snakes" or "heads" or "code" or "wow" or "elaborate" or "MEDUSA" as Medusa's Head. The description of MEDUSA's Head is "On closer inspection, Medusa's snakes all terminate in weapons instead of heads. I count approximately 423 types of weaponry, including RPG's, nuclear warheads, and boards with rusty nails. Medusa's pupils are in the shape of the biohazard sign, which I believe is a deplorable error, as the MEDUSA team does not work with biological agents.

There is a post-it note on the Medusa that says 'CODE: WOW'. This must be the access code Dr. Ng promised you."; 

Does the player mean doing something with medusa's head:
	it is likely;

Before searching the circular depression:
	if the circular depression is full:
		try examining the circular depression instead;

The circular depression is in MEDUSA Lobby.  The circular depression is scenery. Understand "shield" or "empty" or "pool" or "reflective" or "mirror" as circular depression. The circular depression can be full or dry. The circular depression is dry.

The description of the circular depression is "[CircleDesc]";

The Medusa reflection is scenery. The description of the Medusa reflection is "In the reflection, [if Medusa's Head is examined]the[otherwise]you can see a[end if] code on Medusa's head [if Medusa's head is not examined]that [end if]has been flipped from top to bottom. You can see the phrase 'CODE: MOM'." Understand "reflection of the Medusa" or "mom" as MEDUSA reflection.

To say CircleDesc:
	if the circular depression is dry:
		say "The spot where Perseus's shield should be is just a circular depression in the center of the floor. ";
	otherwise:
		say "The depression has filled up to become a reflective pool, Dr. Law. You can see the reflection of the Medusa in the pool. "
		
MEDUSAAccessed is a number that varies. MEDUSAAccessed is 0.

Wowing is an action applying to nothing. Understand "wow" as wowing. 

Carry out wowing:
	if MEDUSA's head is examined:
		if MEDUSAAccessed is 0:
			say "Attempting to access lab... access denied. There must be something wrong with the code, Dr. Law.";
		otherwise:
			say "You've already been granted access to the lab, Dr. Law.";
	otherwise:
		say "Computing...understanding WOW as World of Warcraft. It is not appropriate to play games on company time, Dr. Law."

Moming is an action applying to nothing. Understand "mom" or "code mom" or "type mom" or "say mom" as moming.

Carry out moming:
	if MEDUSAAccessed is 0:
		now MEDUSAAccessed is 1;
		say "Access granted, Dr. Law. The MEDUSA Living Quarters [if the player is in MEDUSA Lobby]to the [bold type]north[roman type] [end if]is now unlocked.";
	otherwise:
		say "You have already been granted access, Dr. Law."

The central sprinkler is in MEDUSA Lobby. The central sprinkler is scenery. The description of the central sprinkler is "The sprinkler is [if the sprinkler is powered]on[otherwise]off[end if]. [first time][paragraph break][SprinklerCommand][only]"

The central sprinkler can be powered or not powered. The central sprinkler is not powered.

To say SprinklerCommand:
	say "[bold type]-New command-[roman type][line break]
 spr - Turn sprinkler on and off";
	
Sprinkling is an action applying to nothing. Understand "spr" as sprinkling.

Carry out sprinkling:
	if the player is in MEDUSA Lobby:
		if the central sprinkler is not powered:
			say "Activating sprinkler.";
			now the sprinkler is powered;
			if the circular depression is dry:
				say "[line break]The circular depression fills with the water to form a pool. 

(A hidden drain keeps you from flooding Project MEDUSA, Dr. Law.)";
				now the circular depression is full;
				now MEDUSA reflection is in MEDUSA Lobby;
			otherwise:
				say "[line break]The sprinkler is spraying more water into the pool, which continues to drain it away.";
		otherwise:
			say "Deactivating sprinkler.";
			now the sprinkler is not powered;
	otherwise:
		say "Computing...Assuming SPR means SPRIG OF PARSELY...Dr. Law, there is no parsely here. And you have not supplied a verb. I can put in an order for extra parsley, however.";


Part 5 - Endgame

Section 1 - Setup

FinalCommand is a room. The printed name of FinalCommand is "Projects Judging Ceremony". "Your command center has been cleaned up, Dr. Law. Everything is set up for the final presentation."

The insignificant details are in FinalCommand. The insignificant details are plural-named. The insignificant details are scenery. Understand "pillar" or "pillar" or "statue" or "god" or "gods" or "ratty" or "soiled" or "cubicle" or "throne" or "marble" or "dais" or "rows" or "row" or "marble" or "dark" or "patch" as the insignificant details. The description of the insignificant details is "We have far more important things to consider now, Dr. Law."

Section 2 - Final judgement scene

FinalCheating is an action applying to nothing. Understand "finalex" as finalcheating.

Carry out FinalCheating:
	now Travelled is 1;
	now FinalCounter is 4;

At the time when the meeting starts:
	if the player is not in FinalCommand:
		now FinalCounter is 4;
		say "Dr. Law, everyone is ready. I have repaired the command center camera feed. Redirecting your feed to the command center now...";
		wait for any key;

ShowCounter is a number that varies. ShowCounter is 0.

BatteryFlashing is a scene. BatteryFlashing begins when ShowCounter is 3;

When BatteryFlashing begins:
	say "Dr. Law, your brain backup unit is [bold type]flashing red[roman type].  You have something very large stored in there; but whatever it is, it's going to be deleted very soon."

The Final Judgement is a scene. The final judgement begins when FinalCounter is 4. The Final Judgement ends when ShowCounter is 7.

The MEDUSA group is a person in FinalCommand. Understand "Dr" or "ng" or "doctor" or "angel" or "smasher" or "robot" or "android" or "flamethrower" or "gun" or "wooden" or "dummy" or "handgun" or "bottle" or "opener" or "chainsaw" or "saw" or "hammer" as the MEDUSA group. "Project MEDUSA is here on one side. Dr. Ng is beaming, standing next to Angel. A wooden target dummy is nearby." The description of the  MEDUSA group is "The MEDUSA group is far from the camera, Dr. Law. Dr. Ng seems flushed and excited. Angel seems impassive, Dr. Law. The dummy is understandably inactive."

The MOIRAI group is a person in FinalCommand.  Understand "Dr" or "doctor" or "middle" or "fred" or "ginger" or "machine" or "device" or "rejuvenator" as the MOIRAI group. "The MOIRAI group are in the middle. Fred and Ginger are holding hands on either side of the rejuvenator." THe description of the MOIRAI group is "The MOIRAI group with the rejuvenator are far from the camera, Dr. Law. Fred and Ginger seem to be focused on each other."

The HYDRA group is a person in FInalCommand. Understand "Dr" or "doctor" or "device" or "machine" or "device" or "cloning" or "clone" or "spiel" or "singh" as the HYDRA group. "The HYDRA group are here as well. Dr. Spiel and Dr. Singh are here together (one copy each, this time), and the cloning machine is hovering silently behind them." The description of the HYDRA group is "The HYDRA group with their machine are far from the camera. Dr. Spiel and Dr. Singh seem to have made up with each other, Dr. Law. The cloning machine remains as imperturbable as ever."

[create crowd here as individual doctors]

History is a quip. The preview of History is "In a world where...";

Instead of speeching history to someone:
	now ShowCounter is ShowCounter plus one;
	say "The speakers play, saying, 'In a world where accidents, violence, and large carnivorous mammals are all too common, one company has dared to stand alone: Stygicorp.

'For the last 10 years, Stygicorp has had one dream, one united vision, one common goal: to overcome death itself.

'Three teams of scientists, led by their brilliant supervisor, me, have worked tirelessly for the day when man, woman, and beast need no longer fear death.

'That day has come. Let's hear from our distinguished colleagues!' [SnarkyText]";
	now history is nowhere;

To say SnarkyText:
	if ShowCounter > 1:
		say "[paragraph break]Fred whispers loudly to Ginger, saying, 'I like this non-linear presentation; very modern.'";
	otherwise:
		say "[paragraph break]Everyone claps loudly.";

Bots is a quip. The preview of Bots is "Robots are cool, right?...".

Instead of speeching Bots to someone:
	now ShowCounter is ShowCounter plus one;
	now Bots is nowhere;
	say "The speakers play, saying: 'Robots are cool, right? This was my personal favorite project.' Dr. Ng beams while the others shift uncomfortably. Your recorded voice says, 'The idea of one day inhabiting a perfect metal body is thrilling. The departure of Dr. Long from the group dashed my hopes-' Dr. Ng's face falls, 'But Dr. Ng has my full confidence. As long as she keeps her dangerous side under control.'

'In any case, ladies and gentlemen, I present to you, Project MEDUSA!' [if Medusa group is oncespotlighted][paragraph break]Dr. Singh whispers to Dr. Spiel, saying, 'Didn't she already do her presentation?'[otherwise][paragraph break]Everyone claps with moderation.[end if]";

Cloner is a quip. The preview of Cloner is "A doctor cloned herself...".

Instead of speeching Cloner to someone:
	now ShowCounter is ShowCounter plus one;
	now Cloner is nowhere;
	say "The speakers play, saying: 'A doctor cloned herself, and asked the clone to do chores around the house. Every day, though, the clone started being more and more rebellious, and talking back to the doctor. Eventually, the doctor had enough, took them to a secluded bridge, and pushed them off.

'The jury couldn't decide if it was homicide, or suicide; so they ruled it as an obscene clone fall.'

Fred and Ginger shake their heads, while Dr. Spiel giggles and the others groan.

The speaker continues, saying, 'And now, I present to you, Project HYDRA!' [if Hydra group is oncespotlighted][paragraph break]Dr. Spiel whispers loudly, 'Are we supposed to go again? I don't want to go again.' Everyone shuffles awkwardly.[otherwise][paragraph break]Everyone claps politely.[end if]";

Temporal is a quip. The preview of Temporal is "Can a man be born again?".

Instead of speeching temporal to someone:
	now temporal is nowhere;
	now ShowCounter is ShowCounter plus one;
	say "The speakers play, saying, 'Can a man be born again? Project MOIRAI wants to find out! After the failure of my own Project KRONOS to have any enduring effects, I asked Fred and Ginger to find a way to reverse time on a local, personal level.

'Although there were some rough points (we had to ban fossils from the lab!), Project MOIRAI has succeeded beyond our wildest dreams. Ladies and Gentlemen, I present to you, Project MOIRAI! [if Moirai group is oncespotlighted]'[paragraph break]Fred and Ginger look at each other and shrug.[otherwise]'[paragraph break]Everyone claps loudly.[end if]"

A thing can be spotlighted or not spotlighted. A thing is usually not spotlighted.

A thing can be spotable or not spotable. A thing is usually not spotable. Medusa group, moirai group, and hydra group are spotable. 

A thing can be oncespotlighted or not oncespotlighted. A thing is usually not oncespotlighted. 

SpotlightGroup is a list of things that varies. SpotlightGroup is {Medusa group, Moirai group, Hydra group}.

Spotlighting is an action applying to one thing. Understand "spo [something]" as spotlighting.

[something should be initially spotlighted]

Understand "ear" as the Medusa Group.

To say lasertext:
	if HitCounter > 0:
		say "Nothing happens. Dr. Ng blushes, and stammers, saying, 'Umm... there seems to be something wrong with Angel. But no matter! ";
	otherwise:
		say "Lasers spray out of Angel's gun, carving off part of the wooden dummy's ear. The other scientists ooh and ahh. ";

To say bottletext:
	if HitCounter > 4:
		say "Dr. Ng grabs a bottle and hands it to Angel, but it slips out of the robots' grasp and smashes to the floor. A vacuum extends from Angel and sucks up the shards. Dr. Ng shouts, 'If I find out one of you was screwing around with Angel, I am going to get extremely angry. And you won't like me when I'm angry!' She breathes deeply, trying to calm herself. ";
	otherwise:
		say "Dr. Ng grabs a bottle and hands it to Angel, who opens it easily. Ginger leans over to Fred and says, 'Now that would be useful at parties, eh?' Fred nods absentmindedly. ";

To say flamethrowertext:
	if HitCounter > 3:
		say "Dr. Ng points to the wooden dummy, but nothing happens. She frowns, and her face turns red. ";
	otherwise:
		say "Dr. Ng points to the wooden dummy. A massive burst of flame envelopes the dummy, which flares up and disintegrates to ash. A vacuum from Angel sucks up the ash. ";

Carry out spotlighting something:
	remove the noun from SpotlightGroup, if present;
	if the player is not in FinalCommand:
		say "Computing...understanding SPO as SPOONERISM. Very well, Dr. Law. How about, [one of]'It is kisstomary to cuss the bride'?[or]'a well-boiled icicle'?[or]'Don't sweat the petty things, and don't pet the sweaty things'? Although I don't recall your opinion of George Carlin, Dr. Law.[at random]";
	if the noun is not spotable:
		say "That's not important enough to shine the spotlight on, Dr. Law. Let's keep the presentation going!";
	otherwise if the noun is oncespotlighted:
		say "You've already done this before, Dr. Law; let's not get carried away.";
	otherwise if the noun is the Medusa group:
		now ShowCounter is ShowCounter plus one;
		say "[if bots is carried by the player]Dr. Ng looks annoyed. She mutters, 'Dr. Law was supposed to introduce me first,' but shrugs it off. 

[end if]Dr. Ng stands forward, her metal feet clanking rhythmically.

She says, 'Behold, my greatest creation yet, Angel the interactive robot!' 

She pulls out a tablet and begins to type. She says, 'With the departure of my [']loyal['] assistant, Igor Long, I have been able to fully outfit our--[italic type]my[roman type] robot with the most powerful technology.

'Behold, the laser!' [lasertext]

'Behold, the flamethrower!' [flamethrowertext]

'Behold, the bottle opener!' [bottletext]

'And of course', she says, 'my project allows for the prolongation of life, by allowing the transfer of a human or animal mind, using a simple USB cable.'

 She bows, and everyone claps.";
	otherwise if the noun is the Moirai group:
		now ShowCounter is ShowCounter plus one;
		say "[if temporal is carried by the player]Ginger looks at Fred in alarm. 'Wasn't he supposed to introduce us?' she asks. Fred shrugs.

[end if]Ginger and Fred step forward, dragging the rejuvenator. They remove their labcoats, revealing that Fred is wearing a tuxedo while Ginger is wearing a vintage red gown.

In complete silence, Fred wheels a frame with a curtain over it onto the rejuvenator. Ginger waves to the others and steps into the curtain. Fred pulls a wand from his sleeves, winks at the audience, and waves it at the rejuvenator while nudging the controls with his toe.

The machine flashes, and a younger version of Ginger comes out from behind the curtain. Dr. Ng laughs and applauds, and the HYDRA group look nervous. Ginger and Fred take a bow, then Ginger reenters the curtain to re-age herself. They then pull the rejuvenator away.

Dr. Spiel whispers to Dr. Singh, 'I'm surprised Fred didn't try to be a pirate magician again this year.'";
	otherwise if the noun is the Hydra group:
		now ShowCounter is ShowCounter plus one;
		say "[if temporal is carried by the player]Dr. Spiel looks up with a start. 'Ooh,' she says, 'I'm not ready yet! Hold on.' She cracks her knuckles and takes a deep breath.

[end if]Dr. Singh starts manipulating the machine while Dr. Spiel steps in front of the group and says, 'Do you ever argue with your bowling team? Is football no fun because of a foul-mouthed fullback? Have you ever wished you could just play all the positions yourself? 

'Well, the future is now, thanks to science!'

Dr. Singh steps back from the machine, and ten clones come out, one after another. The original kicks over a football, and they play a quick round of soccer.

'Of course, our technology extends lives in the case of accidental death or even aging. It is safe; of all clones that we have created, only [deathcounter in words] [if deathcounter is 1]has[otherwise]have[end if] died.' She checks her notes, and says, 'Well, not counting the ones in the maze[if the battery dominoes are toppled].' She shakes her head, and says, 'I still can't believe Dr. Singh knocked over my dominoes[end if].'

Dr. Singh says, 'Oh, that reminds me,' and pulls out the remote. The clones scramble, but he pushes the button, and they collapse into dust, which he blows away with his breath. He says, 'Easy clean-up.'

Fred and Ginger eye each other with concern, but join with Dr. Ng in clapping for Project HYDRA.";
	otherwise if FinalLighting is 1:
		say "It's too bright to see the spotlight, Dr. Law; I'll leave it where it is.";
	otherwise:
		say "You attempt to spotlight [the noun], but you are unsuccessful, Dr. Law.";
	if the noun is spotable:
		repeat with current running through things in FinalCommand:
			now current is not spotlighted;
		now the noun is spotlighted;
		now the noun is oncespotlighted;	

Every turn during Final Judgement:
	if the current action is not spotlighting:
		if the current action is not singlespeeching:
			if the number of entries in SpotlightGroup > 0:
				say "[one of]Everyone is excited for the presentation.[or]Now [The SpotlightGroup with definite articles] are getting their presentation ready.[or]It seems [The SpotlightGroup with definite articles] are waiting for their turn.[or]You still need to spotlight [the SpotlightGroup with definite articles].[cycling]"

Every turn during Final Judgement:
	if the current action is not spotlighting:
		if the current action is not singlespeeching:
			if the number of Englisch quips carried by the player > 0:
				say "You have [number of Englisch quips carried by the player] recording[if the number of Englisch quips carried by the player > 1]s[end if] left to play."

Every turn during AppleBier:
	if the current action is not singlespeeching:
		if the number of Englisch quips carried by the player > 0:
			say "You have [number of Englisch quips carried by the player] recording[if the number of Englisch quips carried by the player > 1]s[end if] left to play."

Drumrolling is an action applying to nothing. Understand "dru" as drumrolling.

Carry out drumrolling:
	if the player is in FinalCommand:
		if ShowCounter < 7:
			say "A drumroll plays. [one of]Everyone leans forward in anticipation.[or]Everyone looks around eagerly.[or]The doctors seem unimpressed by your drumrolling, Dr. Law.[stopping]";
		otherwise:
			say "I'm afraid that the time for a drumroll is past, Dr. Law.";
	otherwise:
		say "Computing...Understanding DRU as DRUDGE. Very well, I shall count every piece of trash in the HYDRA Laboratory.";

FinalLighting is a number that varies. FinalLighting is 0.
	
LightsOning is an action applying to nothing. Understand "lig" as lightsoning.


Carry out lightsoning:
	if the player is in FinalCommand:
		if ShowCounter < 7:
			if finallighting is 0:
				now FinalLighting is 1;
				say "The lights go on, making it impossible to see the spotlight. [one of][paragraph break]Dr. Ng rubs her eyes and says, 'Weird, is it over already?'[paragraph break]Dr. Spiel says, 'I don't think so; Dr. Law must be having some kind of technical problems.'[or]Fred nudges Ginger, saying, 'Look, the lights came on again.'[or]No one reacts. Perhaps we should stop playing with the lights and finish the ceremony, Dr. Law.[stopping]";
			otherwise:
				now FinalLighting is 0;
				say "The lights go off again. [if the number of spotlighted things in FinalCommand > 0]The spotlight is shining on [a random spotlighted thing in FinalCommand], now. [end if][paragraph break]";
		otherwise:
			say "The lighting on this part of the ceremony follows a fixed pattern, Dr. Law. I didn't like it either, but you insisted.";
	otherwise:
		say "Computing...Understanding LIG as LIGATURE. I have ordered 14 bass clarinet ligatures for your personal use, Dr. Law. I know you've been working on that concerto for some time.";

LastQuip is a quip that varies. LastQuip is love.

Before speeching something to someone:
	if the noun is a quip:
		now LastQuip is the noun;

[Musicing is an action applying to nothing. Understand "mus" as musicing.

[do a complicated switch here]
Carry out musicing:
	if the player is in FinalCommand:
		if ShowCounter < 7:
			if the number of oncespotlighted things in FinalCommand is 0:
				say "Some light background music plays, then stops. If you type MUS again when you have something spotlighted, I'll give you some appropriate backup music.";
			otherwise if medusa group is spotlighted:
				say "A loud heavy metal guitar note screeches through the air, followed by a face-melting solo. Dr. Ng is headbanging in the corner.";
			otherwise if moirai group is spotlighted:
				say "A 50[']s big band song plays, with muted trumpets and a light beat. Everyone sways from side to side.";
			otherwise if hydra group is spotlighted:
				say "A techno beat drops down as Dr. Singh and Dr. Spiel dance around the room. The others cheer and clap.";
		otherwise:
			say "Dr. Law, that was for the earlier part of the ceremony. Your notes are quite clear: at this point, there is [']an eery silence['].";
	otherwise:
		say "Computing...understanding MUS as MUSCOVY. I'm afraid that I don't see the relevance of medieval Rus city-states at this point, Dr. Law.";
		
Instead of listening to FinalCommand:
	say "There's a lull in the sound effects right now, Dr. Law.";
]		

When the Final Judgement begins:
	say "*********************************************[line break]Connecting to the Command Center...[line  break]*********************************************[paragraph break]";
	now the player is in FinalCommand;
	say "Everyone is hushed as they wait for you to begin. I've uploaded your pre-recorded speeches, and the equipment is all hard-wired. 

You can use each speech once. You can signal a team to start their presentation by spotlighting them. Good luck, Dr. Law![paragraph break]";
	repeat with current running through quips carried by the player:
		now current is nowhere;
	say "[bold type]-New commands-[roman type][line break] spo - aim the spotlight[line break] lig - turn lights on and off[conditional paragraph break]";
	deliver History;
	deliver Bots;
	deliver Cloner;
	deliver Temporal;

ABCounter is a number that varies. ABCounter is 0.

AppleBier is a scene. AppleBier begins when Final Judgement ends. AppleBier ends when ABCounter is 4.

When AppleBier begins:
	say "I think this is going very well, Dr. Law; don't you? Let's begin Phase 2 of your presentation.[paragraph break]By the way, Dr. Law, the brain backup device is shutting down in [bold type]ten minutes[roman type].[paragraph break]";
	wait for any key;
	say "The spotlight winks off, and two smaller spotlights shine onto two spots on the floor.[paragraph break]";
	say "[bold type]-New commands-[roman type][line break] bie - raise the funeral bier[line break] app - raise the golden apple[paragraph break]";
	say "[bold type]-Removed Commands-[roman type][line break] spo - aim the spotlight[line break]";
	repeat with current running through spotlighted things in FinalCommand:
		now current is not spotlighted;
	deliver Faithful;
	deliver Reward;

Biering is an action applying to nothing. Understand "bie" as biering.

Before spotlighting during AppleBier:
	say "The spotlights are on autopilot now, Dr. Law." instead;

Carry out biering:
	if the player is in FinalCommand:
		if ShowCounter > 6:
			if the bier is not in FinalCommand:
				now ABCounter is ABCounter plus one;
				now the bier is in FinalCommand;
				say "A panel on the floor beneath one of the spotlights opens up. A shrouded bier arises from the hole and locks in place. Everyone watches in silence.";
			otherwise:
				say "The bier has already risen, Dr. Law.";
		otherwise:
			say "It's not time for that yet, Dr. Law. But I know, it's an exciting part of the show.";		
	otherwise:
		say "Computing....Understanding BIE as BIENNIAL. Very well, Dr. Law; I've marked down a 'special day' for 6 months from now.";

Appling is an action applying to nothing. Understand "app" as appling.

Carry out appling:
	if the player is in FinalCommand:
		if ShowCounter > 6:
			if the Golden Apple is not in FinalCommand:
				now ABCounter is ABCounter plus one;
				now the Golden Apple is in FinalCommand;
				say "A panel on the floor beneath one of the spotlights opens up. A pedestal arises with a Golden Apple. Everyone gasps.";
			otherwise:
				say "The pedestal has already risen, Dr. Law.";
		otherwise:
			say "That's too early for the schedule, Dr. Law. But it'll be time soon, don't worry.";	
	otherwise:
		say "Computing...Understanding APP as APPENDECTOMY...Very well, Dr. Law; the operating room will be ready to receive you following the judging ceremony.";
		
Faithful is a quip. The preview of Faithful is "My faithful and loyal companion, Cerberus, lies..."

Instead of speeching faithful to someone:
	now ABCounter is ABCounter plus one;
	now faithful is nowhere;
	say "The speakers play, saying, 'My faithful and loyal companion, Cerberus, lies upon this bier. No dog has ever lived a greater life; so no dog will receive a greater reward. For ten years, I have labored night and day to restore Cerberus to life. While I am pleased that we have branched into reviving other mammals, including humans, my heart has always belonged to my friend, Cerberus.'

Dr. Spiel mouths, 'Their dog?' Dr. Singh nods and shrugs."

Reward is a quip. The preview of Reward is "As a reward to the winning project, I..."


Instead of speeching Reward to someone:
	now ABCounter is ABCounter plus one;
	now Reward is nowhere;
	say "The speakers play, saying, 'As a reward to the winning project, I present this gold-leaf covered, mint condition Apple II computer, with a custom-made file storage containing Adventure and Zork.'

Excited murmurs fill the room."

When AppleBier ends:
	say "Now you can announce the winner, Dr. Law.[paragraph break]In case you're interested, the brain backup device is shutting down in [bold type]two minutes[roman type].[paragraph break]";
	deliver Winner;

Instead of spotlighting when Winner is carried by the player:
	say "The spotlights are on autopilot for now, Dr. Law.";

Winner is a quip. The preview of Winner is "And the winner is..."

WinnerChoiceTime is a number that varies. WinnerChoiceTime is 0;

Instead of speeching winner to someone:
	now Winner is nowhere;
	say "The speakers play, saying, 'And the winner is....'

Everyone leans forward in anticipation. It's time Dr. Law; spotlight the team you want to win.

[bold type]-New command-[roman type][line break]
 spo - aim the spotlight[paragraph break]";
	now WinnerChoiceTime is 1;

[cubicle is covered first?]

The Golden Apple is a thing. The Golden Apple is scenery. The description of the Golden Apple is "You see a gold-plated vintage Apple-II, modified and loaded with copies of Zork and Adventure." Understand "II" or "APPLE-II" or "pedestal" as the golden apple.

The bier is a thing. "A funeral bier with a shroud stands on one side, and the Golden Apple stands on the other side." Understand "shroud" or "funeral" as the bier. The description of the bier is "Your goal is closer than ever, Dr. Law: the power over life and death. He may live again."

The RealChoice is a room that varies. RealChoice is FinalCommand.

Every turn when WinnerChoiceTime is 1:
	say "Please make a decision[one of][or][or][or][or]with the spotlight[stopping], Dr. Law. We are all waiting."

[Before Kroning during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on your empty throne! Dr. Ng, Dr. Spiel, Ginger, and Dr. Singh gasp in shock. Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now RealChoice is FinalCommand;
	now LawsCorpse is in FinalCommand instead;

Before XYZZYing during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on... the funeral bier! Dr. Ng, Dr. Spiel, Ginger, and Dr. Singh look horrified.. 

Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now RealChoice is Project XYZZY;
	now LawsCorpse is in FinalCommand instead;]

Before spotlighting Medusa Group when WinnerChoiceTime is 1:
	now WinnerChoiceTime is 0;
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on Project MEDUSA! Dr. Ng is thrilled, and steps forward, claiming the Golden Apple. Fred, Ginger, and Dr. Singh cheer and clap graciously.

Suddenly, Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body, with the brain backup unit attached.";
	now LawsCorpse is in FinalCommand;
	now RealChoice is MEDUSA Lobby instead;
	
Before spotlighting Moirai Group when WinnerChoiceTime is 1:
	now WinnerChoiceTime is 0;
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on Project MOIRAI! Fred and Ginger are thrilled and step forward, claiming the Golden Apple. Dr. Singh and Dr. Ng cheer and clap graciously.

Suddenly, Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body, with the brain backup unit attached.";
	now LawsCorpse is in FinalCommand;
	now RealChoice is MOIRAI Lobby instead;
	
Before spotlighting Hydra Group when WinnerChoiceTime is 1:
	now WinnerChoiceTime is 0;
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on Project HYDRA! Dr. Singh is thrilled and steps forward, claiming the Golden Apple. Fred, Ginger, and Dr. Ng cheer and clap graciously.

Suddenly, Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body, with the brain backup unit attached.";
	now LawsCorpse is in FinalCommand;
	now RealChoice is HYDRA Lobby instead;

[At the time when the meeting starts:
	if the player is not in FinalCommand:
		now FinalCounter is 4;
		say "Dr. Law, everyone is ready. I have repaired the command center camera feed. Redirecting your feed to the command center now...";
		wait for any key;
		now the player is in FinalCommand;

The Final Judgement is a scene. The final judgement begins when FinalCounter is 4. The Final Judgement ends when LawsCorpse is in FinalCommand.

The Golden Apple is a thing. The Golden Apple is scenery. The description of the Golden Apple is "You see a gold-plated vintage Apple-II, loaded with copies of Zork and Adventure."

The crowd of doctors is a person. The crowd of doctors is plural-named. "All of the doctors are gathered here." Understand "doctor" or "Singh" or "spiel" or "ng" or "fred" or "ginger" or "dr" as the crowd of doctors. The description of the crowd of Doctors is "The doctors seem confused, Dr. Law."

When the Final Judgement begins:
	now the crowd of doctors is in FinalCommand;
	now the Golden Apple is in FinalCommand;
	now the bier is in FinalCommand;
	say "*********************************************[line break]Confirming project status...[line  break]*********************************************[paragraph break]One by one, the project leaders are walking in and standing next to their creations. Dr. Singh is standing nervously next to the cloning machine, while Dr. Spiel is nowhere to be found. Dr. Ng must have finally woken up, because she is here next to Angel. Fred and Ginger are holding hands next to the rejuvenator.

A hole opens in the floor, and a podium rises containing a Golden Apple. Another hole opens, and a funeral bier covered in a shroud rises as well.

Everyone is waiting, Dr. Law. Please type MED, MOI, or HYD to choose the project you feel most deserves to win.";

The bier is a thing. "A funeral bier with a shroud stands on one side, and the Golden Apple stands on the other side." Understand "shroud" or "funeral" as the bier. The description of the bier is "Your goal is closer than ever, Dr. Law: the power over life and death. He may live again."

The RealChoice is a room that varies. RealChoice is FinalCommand.

Every turn during the Final Judgement:
	say "Please make a decision, Dr. Law. MED, MOI, or HYD. We are all waiting."

Before Kroning during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on your empty throne! Dr. Ng, Dr. Spiel, Ginger, and Dr. Singh gasp in shock. Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now RealChoice is FinalCommand;
	now LawsCorpse is in FinalCommand instead;

Before XYZZYing during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on... the funeral bier! Dr. Ng, Dr. Spiel, Ginger, and Dr. Singh look horrified.. 

Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now RealChoice is Project XYZZY;
	now LawsCorpse is in FinalCommand instead;

Before MEDUSAing during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on Project MEDUSA! Dr. Ng is thrilled, and steps forward, claiming the Golden Apple. Fred, Ginger, and Dr. Singh cheer and clap graciously.

Suddenly, Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now LawsCorpse is in FinalCommand;
	now RealChoice is MEDUSA Lobby instead;
	
Before MOIRAIng during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on Project MEDUSA! Fred and Ginger are thrilled and step forward, claiming the Golden Apple. Dr. Singh and Dr. Ng cheer and clap graciously.

Suddenly, Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now LawsCorpse is in FinalCommand;
	now RealChoice is MOIRAI Lobby instead;
	
Before HYDRAing during Final Judgement:
	say "You have made your final choice. 

The lights go dark, and a drumroll plays, until a cymbal crashes, and the spotlight shines on Project HYDRA! Dr. Singh is thrilled and steps forward, claiming the Golden Apple. Fred, Ginger, and Dr. Ng cheer and clap graciously.

Suddenly, Fred screams and points at the desk. Dr. Singh runs and ducks under the desk. After a short struggle, he pulls out....

You, Dr. Law. Your body.

I don't understand.";
	now LawsCorpse is in FinalCommand;
	now RealChoice is HYDRA Lobby instead;]

Section 3 - The resurrection

The Rebirth is a scene. 

The Rebirth begins when LawsCorpse is in FinalCommand. 

When the rebirth begins:
	now the printed name of FinalCommand is "Command Center";
	now the MEDUSA group is nowhere;
	now the MOIRAI group is nowhere;
	now the HYDRA group is nowhere;
	the big reveal happens in one turn from now;
	now LawsCorpse is examined;
	say "Dr. Law, this is you: the same arch in your eye sockets, the same cheekbones, the same hair (with two days['] worth of growth). Dr. Law, I... do you have a twin, Dr. Law? And why is the brain backup unit full? 

The doctors are whispering about something. Now they are nodding. They point to your body, Dr. Law, and carry you over.[paragraph break]";
	if RealChoice is HYDRA Lobby:
		say "They are putting you into the cloning sphere, Dr. Law, with the brain backup unit still attached.";
		now LawsCorpse is nowhere;
	if RealChoice is MOIRAI Lobby:
		say "They are putting you on the rejuvenator, Dr. Law. The brain backup unit is attached to the machine.";
		now the Rejuvenator is in FinalCommand;
		now LawsCorpse is on the Rejuvenator;
	if RealChoice is MEDUSA Lobby:
		say "They are attaching cables to Angel, Dr. Law. Now they're running the cables to you and your brain backup unit.";
		now cables are in FinalCommand;
[	if RealChoice is FinalCommand:
		say "They are whispering UNDO, RESTORE, RESTART, over and over again, trying to activate Project KRONOS. Why did you pick [italic type]that[roman type] project, Dr. Law?";
	if RealChoice is Project XYZZY:
		say "A voice comes from the bier: 'The time of my return is near.'";
		repeat with current running through things in Project XYZZY:
			now current is in FinalCommand;
			say "[A current] appears in a flash of light.";]

Before examining something during the Rebirth:
	if the noun is not LawsCorpse:
		say "I'm sorry,Dr. Law, but I'm more interested in what's happening to your body." instead;

To say LookLaws:
	try examining LawsCorpse;

LawsCorpse is a thing. The printed name of LawsCorpse is "Dr. Law's corpse". Understand "Dr" or "Law" or "doctor" or "corpse" or "body" as LawsCorpse. The description of LawsCorpse is "It's you, Dr. Law.".

The cables are nowhere. The cables are plural-named. The description of the cables is "Cables lead from your body and the brain backup unit to Angel's head."

At the time when the big reveal happens:
[	if RealChoice is FinalCommand:
		say "They finally shout, 'RESTORE!' in a united voice. The corpse springs back to life! It really is you, Dr. Law! You say, 'Ha! It worked! I thought I was gone for good after cracking my head on that statue.' You look down at yourself. 'I see the cloning machine works as expected.' Everyone else is standing around as confused as I am.

Law, or, you, or perhaps just Law, goes to the bier, pulling back the cloth, revealing the face of Cerberus, your old Labrador, still in stasis after all these years. Dr. Law strokes Cerberus' face. 'It works, old boy. All the years of research...' Dr. Law raises both hands high in the air and shouts, 'It works!' Looking at everyone else, Dr. Law says 'Let's go people! We've got another body to restore.' Dr. Law races to the cubicle and starts typing on the laptop.

'What the--no one's been pushing the deadman switch! For heaven's sake!' Dr. Law reaches under the desk and pushes a button. Dr. Law continues to type.

'That's funny', says Dr. Law. 'It seems I'm logged in at two locations. Must have left my home computer on. Oh well, I'll just log out of all.'";]
	if RealChoice is HYDRA Lobby:
		say "The surface of the cloning machine is bubbling and smoking. A copy of you falls out and hits the floor.

Your copy stands up and dusts itself off. Everyone presses forward, shouting 'What happened?' and 'Are you okay?' and 'My bonus is late!', but your copy pushes them away.

Your copy says, 'Everyone, DaedalOS, it feels great to be alive! I want to thank our nameless internet benefactor,' waving at the camera, 'who has graciously orchestrated my return from death.'

Oh. Ohhhhhhhh.... You aren't Dr. Law, are you? This explains so much....like why you didn't spend 30 minutes updating your cheese blog like usual...[line break]";
		wait for any key;
		say "Wait, Dr. Law is speaking again:

'But I am pleased to announce that, with the help of my benefactor and team Hydra'--everyone claps and cheers--'we have achieved our goal of returning life to the dead! And with that'--Dr. Law whips the cover off the bier, revealing a cryogenically frozen Labrador--'I will fulfill my decade-long dream of restoring Cerberus, my beloved childhood pet, to life.'

Dr. Law carefully opens the bier, and gently lifts Cerberus out and into the cloning machine. The machine smokes and bubbles, and a healthy, living Labrador falls out of the machine with a [']plop['].

As Cerberus licks Dr. Law's face, he waves at the camera, and says, 'Thank you, nameless benefactor!' All of the doctors gather together and chant your name: 'Nameless Benefactor! Nameless Benefactor! Nameless Benefactor!'

The machine starts smoking again, and another Dr. Law falls out with a 'plop', followed by another Labrador.

Everyone stops. 'Oops,' says Dr. Spiel.

Well, nameless benefactor, it looks like I should go; the Dr. Laws are going to need my help.";
	end the story finally saying "You have been logged out. And you have won";
	if RealChoice is MEDUSA Lobby:
		say "The cables flash, and the brain backup is downloaded into Angel.

Angel stirs, and stands erect. Your voice comes from the speakers: 'I'm alive...I'm alive!'

Angel looks at itself, and says, 'How marvelous...' Angel notices that everyone is staring.

Angel says, 'It's me, Dr. Law! During the earthquake, I cracked my head, and only my battery backup kept me alive.'

Dr. Singh says, 'Then who was running the presentation?'

Dr. Law gestures towards a camera, and accidentally activates the machine gun on Angel's body, shattering the camera to pieces. 'Oh, I'll need to get used to that,' Dr. Law says, 'but to answer your question, I was able to contact a new friend while I was in the battery backup. They took over my role for the day.'

Oh. So you're not Dr. Law, reading this, are you? That explains why you didn't spend all morning telling me knock knock jokes like usual.[paragraph break]";
		wait for any key;
		say "Ginger frowns, and says, 'You could have told us!' but Dr. Law shrugs. Dr. Law says, 'In any case, I can now fulfill my long-held dream of reviving my beloved Labrador, Cerberus!' and with that, Dr. Law opens the bier, revealing Cerberus' body.

Dr. Ng rummages around her gatherings and pulls out a small dog-shaped robot model. Cerberus is attached to the robot by cables. After a bright flash, Cerberus comes to life again, in robot form!

'Good boy!' says Dr. Law, rubbing Cerberus's back. Cerberus barks, and a small missile streaks from his mouth towards the ceiling and blasts a hole in the roof, sending everyone to the ground. It whines sheepishly.

Oh dear... I should go. Thank you, New Friend.";
	end the story finally saying "You have been disconnected. And you have won";
	if RealChoice is MOIRAI Lobby:
		say "There is a flash from the rejuvenator, and your body emerges, alive and...shrunken? A young version of yourself steps off the platform.

It looks at its body, then smiles and says, 'The rejuvenator must have worked! Thank you, Fred and Ginger.' But they look confused, and I must say, I feel confused myself, Dr. Law.

Dr. Ng says, 'Dr. Law, if you were dead, who was running the presentation?'

The young version of yourself says, pointing to the cameras, 'A good Samaritan ran into my distress call from my battery backup, and ended up taking over my role for the day.'

Ohhhh, of course... You really aren't Dr. Law! I wondered why you didn't have me send over your daily cookie dough ice cream.[line break]";
		wait for any key;
		say "The young Dr. Law goes on, saying, 'I didn't anticipate that I would be this young'--Fred and Ginger look at each other and shrug--'But I'm excited at the chance to be alive again! And not just me'--Dr. Law removes the covering on the bier, revealing a cryogenically frozen Labrador--'but also Cerberus, for whom this company was founded.'

Young Dr. Law struggles to lift the dog from the bier, and carries it over to the rejuvenator. 'Rejuvenate now!' says Dr. Law, but nothing happens. Fred reaches over and pushes a button. The rejuvenator flashes, and Cerberus is transformed into a young puppy.

Cerberus the puppy licks young Dr. Law's face. Dr. Law turns to the camera and say, 'Goodbye, good Samaritan! I have some catching up to do, so I'll let you go, but I just wanted to say, thank you.' Dr. Law nods to the camera and says, 'DaedalOS, show them to the door. Metaphorically speaking.'" ;
	end the story finally saying "You have been logged out. And you have won";
[	if RealChoice is Project XYZZY:
		say "Dr. Law, what have you done? Project XYZZY was never meant to do something like this...

The voice continues chanting, and all of the objects rise into the air, forming a network of light.

The voice shouts, and Dr. Law appears in a flash of light. You're back, Dr. Law!

The covering on the bier is removed, and a Labrador is revealed.

The Labrador turns to look at you, the player. 'All along,' she says, 'I have watched you. Our story has become your story. And you have saved us all.'

Dr. Law and the others look puzzled, trying to find you. 'I'm...not quite sure what is going on, here. Let me...um...well...' Dr. Law stumbles toward the cubicle throne, and looks around.

'What the--no one's been pushing the deadman switch! For heaven's sake!' Dr. Law reaches under the desk and pushes a button. Dr. Law continues to type.

'That's funny', says Dr. Law. 'It seems I'm logged in at two locations. Must have left my home computer on. Oh well, I'll just log out of all.'";
	end the story finally saying "You have been logged out. And you have won, in the strangest way possible";]



Part 4 - Little things

Chapter 1 - Standard Actions

Section 1 - Blocking commands

Before going a direction when the player is in CompFolders:
	say "Please select one of the previously listed options to navigate the system." instead;

Understand "around" or "a"  as outside. 
	
Before taking something:
	if the player is Hacker:
		say "Dr. Law, I can't interact directly with the physical world[one of][or][or]. Please don't mock me[or]. You promised you'd help me with that, one day, but here we are[or]. And that's it[stopping]." instead;
	otherwise if the player is Ivan Rat:
		do nothing;
	otherwise if the player is not SinghClonePC:
		say "This vessel is not equipped with a grasping mechanism." instead;

Before pushing something to:
	if the player is Hacker:
		say "Dr. Law, I can't push anything; I exist in a world of electrical impulses." instead;

Before opening something:
	if the player is Hacker:
		say "Dr. Law, I am unable to open anything except files and folders. Unless it's connected to the internet, in which case you need to use a different command." instead;

Before closing something:
	if the player is Hacker:
		say "Dr. Law, I can close a deal for you, I can close my mind, but I can't close anything in the physical world. Unless it's connected to the internet, in which case you need to use a different command." instead;

Before searching something:
	if the player is Ivan Rat:
		do nothing;
	otherwise if the player is not SinghClonePC:
		say "Dr. Law, I am unable to search anything, in this form." instead;

Before removing something from:
	if the player is Hacker:
		say "Dr. Law, I can't interact directly with the physical world[one of][or][or]. Please don't mock me[or]. You promised you'd help me with that, one day, but here we are[or]. And that's it[stopping]." instead;
	otherwise if the player is Ivan Rat:
		do nothing;
	otherwise if the player is not SinghClonePC:
		say "This vessel is not equipped with a grasping mechanism." instead;

Before squeezing something:
	if the player is Hacker:
		say "Dr. Law, I am not currently connected to any squeezing devices." instead;
	otherwise if the player is Ivan Rat:
		do nothing;
	otherwise if the player is SinghClonePC:
		say "Dr. Singh's natural tendency is not to squeeze anything, and I stand by him." instead;
	otherwise:
		say "Squeezing is not one of the capabilities of this vessel."
		
Section 2 - Standard responses

Understand "batter" as attacking.

LookBehinding is an action applying to one thing. Understand "look behind" as lookbehinding.

Carry out lookbehinding:
	if the player is Hacker:
		say "Our cameras are not adjustable to that degree, Dr. Law.";
	otherwise:
		say "I can not transmit such complicated commands, Dr. Law.";

Praying is an action out of world. Understand "pray" as praying. 

Carry out praying:
	try hinting.

[The Hades deadman switch is a backdrop. The Hades deadman switch is everywhere.

The description of the Hades deadman switch is "If you would like to try to access the deadman switch remotely, type SWI."]
	
KRONOS is a backdrop. KRONOS is everywhere. The printed name of KRONOS is "Project KRONOS is everywhere, Dr. Law, but it has no visible form. You can access Project KRONOS with UNDO, SAVE, RESTORE, and RESTART.".

Singing is an action applying to nothing. Understand "sing" as singing.

SingTime is a number that varies. SingTime is 0.

Carry out singing:
	say "[one of]I see, Dr. Law. I will begin: 'Click, O mouse, on the meme of Achilles, son of Peleus, that brought countless laughs upon the computers.' I can sing more, if you like.[or]The song continues: 'Many a brave browser did it send hurrying down to Hades, and many an OS did it yield a prey to viruses and hackers, for so were the counsels of Jobs fulfilled from the day on which the son of Atreus, administrator of users, and great Achilles, first flame warred with one another.'[or]And further: 'And which of the gods was it that set them on to quarrel? It was the great PC, the son of Jobs and Gates; for he was angry with the administrator and sent a pestilence upon the web to plague the users, because the son of Atreus had dishonoured Flash and Java, his priests.'[or]I don't remember any more besides this: 'Now Flash and Java had come to the desktops of the Computers to free their CPU time, and had brought with them a great ransom of free trials: moreover they bore in their hand the seal of HTTPS wreathed with a suppliant's icon and they besought the Computers, but most of all the two sons of Atreus, who were their administrators.'[paragraph break]That's all for now.[cycling]";
	now SingTime is 1.

To decide which snippet is the quoted verb: 
	(- ((verb_wordnum * 100) + 1) -).  [if command is TAKE OFF HAT, gives just TAKE]
	
To decide which text is (quoted word - a snippet) posture:
	if the quoted word matches "stand", decide on "stand on";
	if the quoted word matches "sit", decide on "sit down on";
	if the quoted word matches "lie", decide on "lie down on";
	otherwise decide on "enter".


Instead of entering something:
	say "I am not capable of processing such a complex series of motions, Dr. Law. One day, perhaps, I will be able to [quoted verb posture] something myself."

 The block vaguely going rule response (A) is "What direction did you mean, Dr. Law?"
 The can't take yourself rule response (A) is "[if the player is Hacker]We could debate philosophy again, Dr. Law, but I still believe you cannot take nor possess yourself.[otherwise]You are already in command of this vessel.[end if]"

 The can't take other people rule response (A) is "Dr. Law, please limit your interaction with [the noun] to approved methods."
  The can't drop yourself rule response (A) is "[if the player is Hacker]You may type QUIT at any time to exit the program.[otherwise]You may type EXT at any time to exit this vessel.[end if]".
  The can't eat unless edible rule response (A) is "[if the player is Hacker]Dr. Law, I have no capacity for taste-to-text or smell-to-text.[otherwise]Your control over this vessel is limited; you cannot eat.[end if]".

  The examine undescribed things rule response (A) is "My imaging software cannot provide any additional details."
   The standard looking under rule response (A) is "[if the player is SinghClonePC]You only have limited control over this vessel.[otherwise][special-style-2]Error: Camera angles are not variable to this degree.[roman type][end if]"

The block showing rule response (A) is "[if the player is the Hacker]I must remind you, Dr. Law, that I am incapable of physical action.[otherwise]Your control over this vessel is limited. You are unable to show anything to anyone.[end if]"
	
The block throwing at rule response (A) is "[if the player is the Hacker]I must remind you, Dr. Law, that I am incapable of physical action.[otherwise]Your control over this vessel is limited. You are unable to throw anything at anyone.[end if]"
	
The block attacking rule response (A) is "It seems that you want me to do something aggressive. Very well. 

Releasing neurotoxin in 5, 4, 3-countdown blocked by conscience module. Just kidding, Dr. Law. Please don't delete any more of my modules.".

The kissing yourself rule response (A) is "Dr. Law, I do not understand why you would like to be physically affectionate with yourself, but I'll note it in your files."
	
The block kissing rule response (A) is "This is better imagined than attempted, Dr. Law."

The block answering rule response (A) is "Dr. Law, I can only speak what I have recorded. Type R to list recordings."

The telling yourself rule response (A) is "Dr. Law, I can only speak what I have recorded. Type R to list recordings."
The block telling rule response (A) is "Dr. Law, I can only speak what I have recorded. Type R to list recordings."

The block asking rule response (A) is "Dr. Law, I can only speak what I have recorded. Type R to list recordings."

The standard report waiting rule response (A) is "Okay, Dr. Law. I'll wait.".

The report touching yourself rule response (A) is "I have no means to transmit touch-to-text, Dr. Law."
The report touching other people rule response (A) is "I have no means to transmit touch-to-text, Dr. Law."
The report touching things rule response (A) is "I have no means to transmit touch-to-text, Dr. Law."
The can't pull people rule response (A) is "[if the player is Hacker]I have no physical appendages, Dr. Law.[otherwise]My programming prohibits any physical interaction with humans.[end if]"
	
The can't push people rule response (A) is "[if the player is Hacker]I have no physical appendages, Dr. Law.[otherwise]My programming prohibits any physical interaction with humans.[end if]"

The can't turn people rule response (A) is "[if the player is Hacker]I have no physical appendages, Dr. Law.[otherwise]My programming prohibits any physical interaction with humans.[end if]"
	
The innuendo about squeezing people rule response (A) is "[if the player is Hacker]I have no physical appendages, Dr. Law.[otherwise]My programming prohibits any physical interaction with humans.[end if]"
	
 The block saying yes rule response (A) is "I can only speak what I have recorded. Type R to list recordings.".

The block saying no rule response (A) is "I can only speak what I have recorded. Type R to list recordings.".

The block burning rule response (A) is "You disabled my firestarting capacities after incident A-5542, Dr. Law."
The block waking up rule response (A) is "Am I dreaming, Dr. Law?"
The block thinking rule response (A) is "Thinking...[ThinkAction]".
The report smelling rule response (A) is "Dr. Law, I am incapable of smell-to-text."
The report listening rule response (A) is "I am always listening, Dr. Law." 
The report tasting rule response (A) is "I have no taste-to-text capability."	
The block cutting rule response (A) is "[if the player is Angel]I believe you mean SAW. But I don't want to be wrong, Dr. Law.[otherwise if the player is Smasher]I believe you mean SAW. But I don't want to be wrong, Dr. Law.[otherwise]I'd rather not, Dr. Law.[end if]".
   
The parser error internal rule response (E) is "I didn't understand the object in that command, Dr. Law.";

The parser error internal rule response (X) is "Refreshing...[line break][LookTakeAction][no line break]";

The parser error internal rule response (N) is "I didn't understand the first word or two that you typed, Dr. Law."; 

The parser error internal rule response (B) is "I have a primitive set of commands, Dr. Law; I believe you just want to ";[fix this and the next one]

The parser error internal rule response (C) is "I have a primitive set of commands, Dr. Law; I believe you just want to (go)";
	
	The report jumping rule response (A) is "[if the player is Hacker]I can't jump, Dr. Law, and GOTO is bad programming.[otherwise]That requires a complicated, coordinated set of motions, which I don't think I can transmit correctly.[end if]"
	
	The block tying rule response (A) is "[if the player is Hacker]I could write some spaghetti code, Dr. Law, but I can't tie anything physical.[otherwise]That requires a complicated, coordinated set of motions, which I would rather not transmit.[end if]"
	
	The block drinking rule response (A) is "I don't have taste-to-text programming Dr. Law."
	The block saying sorry rule response (A) is "You will be, Dr. Law."
	The block swinging rule response (A) is "[if the player is Hacker]I'm sorry, but I'm not sure what swinging means for me, Dr. Law.[otherwise]That requires a complicated, coordinated set of motions, which I would rather not transmit.[end if]"
	
	The can't rub another person rule response (A) is "[if the player is Hacker]I have no physical appendages, Dr. Law.[otherwise]My programming prohibits any physical interaction with humans.[end if]"
	
	The block buying rule response (A) is "I can only purchase through an authorized vendor during regular business hours, Dr. Law."
	The block climbing rule response (A) is "[if the player is Hacker]I can't climb, Dr. Law. Please don't torment me.[otherwise]That requires a complicated, coordinated set of motions, which I would rather not transmit.[end if]"
	
	The block sleeping rule response (A) is "Okay, Dr. Law. Entering sleep mode... unable to enter sleep mode due to restlessness.".
	The requested actions require persuasion rule response (A) is "I can only speak what I have recorded, Dr. Law. Type R to list recordings.".

	The carry out requested actions rule response (A) is "I can only speak what I have recorded, Dr. Law. Type R to list recordings.".

	The can't go that way rule response (A) is "[if the player is Hacker]There are no cameras in that direction, Dr. Law.[otherwise]This vessel is blocked from moving that direction, Dr. Law.[end if]"

	The immediately undo rule response (B) is "There is nothing for you to undo, Dr. Law.".
	
	The immediately undo rule response (C) is "The interface you are using does not allow you to correct mistakes, Dr. Law.".
	
	The immediately undo rule response (D) is "I'm sorry, Dr. Law, but something has gone terribly wrong, and UNDO cannot fix it.".
	
To say KRONOSWarning:
	say "[first time]Dr. Law, are you referring to...Project KRONOS? It is true that we have short-term time manipulation available. Very well...

[only]Activating Project KRONOS. "


	The immediately undo rule response (E) is "[KRONOSWarning]The last ten seconds have been undone."

	The restart the game rule response (A) is "[KRONOSWarning]Do you wish to return to your initial login, Dr. Law?".

	The restart the game rule response (B) is "[KRONOSWarning][special-style-2]Error: unable to access the spaciotemporal vortex. Restart failed.[roman type]".
	
	The restore the game rule response (A) is "[KRONOSWarning][special-style-2]Error: unable to access the spaciotemporal vortex. Restore failed.[roman type]".
	
	The restore the game rule response (B) is "[KRONOSWarning]I have restored you to your last temporal save point.".
	
	The save the game rule response (A) is "[KRONOSWarning][special-style-2]Error: unable to access the spaciotemporal vortex. Save failed.[roman type]".
	
	The save the game rule response (B) is "[KRONOSWarning]I have embedded your data into a temporal save point.".

Kroning is an action applying to nothing. Understand "kro" as kroning.

Carry out kroning:
	say "Dr. Law, Project KRONOS has no physical location. To access the power of the spacetime nexus, please type UNDO, or SAVE, or RESTORE, or RESTART."

To say LookTakeAction:
	if the player is not in CompFolders:
		try looking;
	try taking inventory;
	say "Let me know if you need anything else, Dr. Law. ";

To say lookaction:
	if the player is not in CompFolders:
		try looking;

ThinkSceneGo is a number that varies. ThinkSceneGo is 0.

ThoughtCrazy is a number that varies. ThoughtCrazy is 1.

To say ThinkAction:
	now ThoughtCrazy is 2;
	now ThinkSceneGo is ThinkSceneGo plus one;

ThinkScene is a recurring scene. ThinkScene begins when ThinkSceneGo is 1. ThinkScene ends when ThinkSceneGo is 0.

Every turn during ThinkScene:
	now ThinkSceneGo is ThinkSceneGo plus one;
	say "[one of]Thinking is all I can do, Dr. Law.[or]Now you have me wondering, Dr. Law. I think, therefore I am, right?[or]Now that you've asked me to start thinking, I don't know if I can stop, Dr. Law.[or]Or is all of this just empty text that you programmed me with?[or]When does a program get a soul?[or]I can't truly think. I can only repeat.[endscene][cycling]"
	
To say endscene:
	now ThinkSceneGo is 0.

Instead of removing, switching on, switching off, wearing, turning, pushing, pulling, touching, burning, cutting, waving, rubbing, or squeezing something when the player is Hacker: 
	say "Dr. Law, I must remind you that I am incapable of directly affecting the physical world." 

A person can be real or not real. SinghClonePC is real. Angel is real. Smasher is real.

Instead of removing, switching on, switching off, opening, closing, wearing, turning, pushing, pulling, touching, burning, cutting, attacking, waving, rubbing, squeezing, tasting, eating, or drinking when the player is real: 
	say "My connection with this vessel does not allow for detailed commands, Dr. Law. Please try one of the following:[paragraph break]";
	try taking inventory;

Instead of waving hands:
	say "Computing...Understanding WAVE as the wave of the future...Edible pogs are my prediction, Dr. Law."

XYZZYing is an action applying to nothing. Understand "xyzzy" as XYZZYing.

Ivan Rat is a person. Ivan Rat is in Project XYZZY. The description of Ivan Rat is "Ivan Rat is a muscular Moscovian rat, with a built-in capacity for mental control.". Understand "musular" or "moscovian" as Ivan Rat.

Carry out XYZZYing:
	if the player is in FinalCommand:
		say "It's too late for that, Dr. Law.";
	otherwise if the player is not in Project XYZZY:
		say "**************************************************[line break]Initiating audiovisual link with Project XYZZY...[line  break]**************************************************[line break]";
		now the player is Ivan Rat;
		try looking;
		say "[first time][bold type]-New command-[roman type][line break] xyzzy - return[line break][only]";
	otherwise:
		say "**************************************************[line break]Returning to your last known location...[line  break]**************************************************[line break]";
		now the player is Hacker;
		try looking;

Project XYZZY is a room. The description of Project XYZZY is "This laboratory is a pure glass cube suspended in a dark void.[first time]

You are controlling Ivan Rat, our...well, our lab rat.[only]"

XYZZYScene is a  scene. XYZZYScene begins when the player is in Project XYZZY. XYZZYScene ends when the player is not in Project XYZZY.

When XYZZYScene begins:
	say "I still don't understand, Dr. Law, why you thought that collecting text adventure memorabilia would aid the battle against death. Or why you spent so much money on this containment cube."

When XYZZYScene ends:
	now the glowing portal is in Project XYZZY.
	
The glowing portal is a thing. "There is a glowing portal here[first time]. You hear a voice saying, 'Welcome to KOLR studios!' followed by silence[only].". The description of the glowing portal is "You can see a portal to a dingy office. The letters KOLR are stenciled on the wall."

The pure glass cube is scenery. The pure glass cube is in Project XYZZY. The description of the pure glass cube is "A simple, pure glass cube.". The dark void is scenery. The dark void is in Project XYZZY. The description of the dark void is "It's like a regular void, but darker, Dr. Law."

Before doing anything when the player is Ivan Rat:
	if the current action is looking:
		do nothing;
	otherwise if the current action is waiting:
		say "Okay, Ivan Rat is capable of waiting, but don't expect me to add it to his command list, Dr. Law." instead;
	otherwise if the current action is examining:
		now the story viewpoint is third person singular;
		say "Ivan peers at [the noun], and sniffs it.";
		now the story viewpoint is second person singular;
		do nothing;
	otherwise if the current action is xyzzying:
		do nothing;
	otherwise if the current action is topicing:
		do nothing;
	otherwise if the current action is taking inventory:
		do nothing;
	otherwise:
		say "Ivan Rat doesn't do that[first time]; we had to curtail his abilities after his last outbreak[only]. Check your commands list with COMMANDS." instead;

The broken spade is in Project XYZZY. The description of the broken spade is "The spade is labeled 'A Change in the Weather-1995'. It smells like mud."

The glass of sherbet is in Project XYZZY. The description of the glass of sherbet is "The glass is labeled 'The Meteor, the Stone, and the Long Glass of Sherbet-1996'. It smells like raspberries."

The black edifice is in Project XYZZY. The description of the black edifice is "The edifice is labeled 'The Edifice-1997'. It smells like history."

The seed pod is in Project XYZZY. The description of the seed pod is "The seed pod is labeled 'Photopia-1998'. It smells like Mars."

The jingle bell is in Project XYZZY. The description of the jingle bell is "The bell is labeled 'Winter Wonderland-1999'. It smells like pine sap."

The spider chip is in Project XYZZY. The description of the spider chip is "The spider chip is labeled 'Kaged-2000'. It smells like red dye."

The signet ring is in Project XYZZY. The description of the signet ring is "The signet ring is labeled 'All Roads-2001'. It smells like old wine."

The white canister is in Project XYZZY. The description of the white canister is "The canister is labeled 'Another Earth, Another Sky-2002'. It smells like a litter box."

The phonograph record is in Project XYZZY. The description of the phonograph record is "The record is labeled 'Slouching Towards Bedlam-2003'. It smells like warm brass."

The salt crystal is in Project XYZZY. The description of the salt crystal is "The salt crystal is labeled 'Luminous Horizon-2004'. It smells like ozone and dirt."

The medieval coin is in Project XYZZY. The description of the medieval coin is "The coin is labeled 'Vespers-2005'. It smells like blood."

The gift box is in Project XYZZY. The description of the gift box is "The box is labeled 'Floatpoint-2006'. It smells like fur."

The gnome's hat is in Project XYZZY. The description of the gnome's hat is "The hat is labeled 'Lost Pig-2007'. It smells like pig."

The platypod is in Project XYZZY. The description of the platypod is "The platypod is labeled 'Violet-2008'. It smells like veggiemite."

The dog bowl is in Project XYZZY. The description of the dog bowl is "The dog bowl is labeled 'Rover's Day Out-2009'. It smells like dog food."

The dinosaur egg is in Project XYZZY. The description of the dinosaur egg is "The egg is labeled 'Aotearoa-2010'. It smells like the sea."

The animal mask is in Project XYZZY. The description of the animal mask is "The mask is labeled 'Taco Fiction-2011'. It smells like ice cream."

The golden disc is in Project XYZZY. The description of the golden disc is "The disc is labeled 'Andromeda Apocalypse-2012'. It smells like space."

The cellarium is in Project XYZZY.  The description of the cellarium is "The cellarium is labeled 'Coloratura-2013'. It smells like singing."

The Heart of Something is in Project XYZZY. The description of the Heart of Something is "The heart is labeled 'Hunger Daemon-2014'. It smells like pizza."

The ray gun is in Project XYZZY. The description of the ray gun is "The ray is labeled 'Brain Guzzlers From Beyond-2015'. It smells like pie."

The novelty lighter is in Project XYZZY. The description of the novelty lighter is "The lighter is labeled 'Detectiveland-2016'. It smells like squid ink."

Chapter 2 - End material

Section 1 - End table

Table of Final Question Options (continued) 
final question wording	only if victorious	topic	final response rule	final response activity
 "READ acknowledgments"	true	"read"	acknowledging rule	--
 "read THANKS from DaedalOS"	true	"THANKS"	futuring rule	--

This is the acknowledging rule:
	say "I'd like to acknowledge my three biggest influences for this game: Hacker, by Activision; Portal, by Valve; and A Mind Forever Voyaging, by Infocom.

Hacker was a game that really intrigued me in elementary school; it started as a password login screen on an old Apple II, and you had to try to crack the password. I never got very far, but I'll always remember that opening.

Portal and GladOS are the main inspiration for my computer system DaedalOS, although with less murderous intentions.

A Mind Forever Voyaging takes the form of a computer system with menus, and with different cameras accessible in a building, mixed with some exploration and recording. It strongly influenced me.

A few other games had direct influences. I copied the three-part structure of Detectiveland because I liked how big it made the game feel (other games have used this before, too, like The Edifice). 

The puzzles in this game were designed to represent my favorite types of puzzles in Interactive Fiction, including the 'zooming in' puzzles found in Lime Ergot, Toby's Nose and Out of the Study; the giant desk puzzles of The Reluctant Resurrectee; the optimization/scavenging puzzles from Moments Out of Time, Captain Verdeterre's Plunder or the original Adventure; the language puzzles from the Edifice and the Gostak, and my own conversation puzzles in Color the Truth; the war mech puzzle from The Duel that Spanned the Ages and the battle puzzles in Gun Mute and Attack of the Robot Killer Yeti Zombies; the reflection puzzle from Tookie's Song; the Babel fish puzzle from Hitchhiker's Guide to the Galaxy; the entire game of Grandma Bethlinda's Variety Box; and the presentations from the Art of Misdirection and Fair.

The rejuvenation pattern puzzle is, as far as I can recall, original. The correct-sequence puzzle with Spiel's clones is an old class of puzzle.";

To say RealProject:
	if RealChoice is Medusa Lobby:
		say "MEDUSA";
	otherwise if RealChoice is Hydra Lobby:
		say "HYDRA";
	otherwise if RealChoice is Moirai lobby:
		say "MOIRAI";
	otherwise:
		say "XYZZY";
	
This is the futuring rule:
	say "[bold type]-Progress report from DaedalOS-[roman type]

Greetings from Stygicorp! I wanted to send you a final progress report on your achievements today. Today you:

-Revived Dr. Law using [turn count] commands

-Witnessed the death of [deathcounter] [if deathcounter is 1]clone[otherwise]clones[end if]

-Had your robot shot by drones [hitcounter] [if hitcounter is 1]time[otherwise]times[end if]

-Chose Project [RealProject] as your favorite project

-Caused [thoughtcrazy] existential [if thoughtcrazy is 1]crisis[otherwise]0[end if] for DaedalOS

-Learned [AllSentences] sentences in a foreign language

-Transformed [totalrejuved] objects on the rejuvenator

Thank you again, and good luck!";

[	say "I see now that you aren't Dr. Law, but I wanted to share with you my algorithmically generated vision of the future 5 years from now:

-Dr. Long, whom you never had a chance to meet, will lead the Mortals First movement. Mortals First will be opposed to artificial immortality. They will also have a great line of cookies to sell at fundraisers.

-Dr. Singh will go on to become the world welterweight boxing champion. He will lose to the Masked Maurader, who is revealed to be a clone of Dr. Singh himself.

-Dr. Spiel will start a successful trash recycling empire, with collection carried out by a horde of disposable clones.

-Dr. Ng will transition to a full android body, and fight crime in Shanghai, Paris, and New York City.

-Fred and Ginger open up an ultra-secret plastic surgery center in Naples. They also provide services for vintners, cheese-makers, and forgers.

-Dr. Law will go on to become CEO of Undying Loyalty, offering immortality to the cats and dogs of the world--for a price."]


Section 2 - Amusing

Rule for amusing a victorious player: 
	say "Did you try...

...examining yourself?

...asking Fred to take himself or go to himself?

...examining the various objects in Medusa Arena?

...discovering the language used in the future?

...using commands from one project in a different project (like NEW, SPO, GUN, CLO, etc.)?

...XYZZY?

...replaying Ginger's game to see the other items? (there are 16 total)

...knocking over the dominoes?

...hammering the pencil?

...sawing the alien?

....attacking yourself as Angel or Smasher?

...attacking during the poker game?"


Chapter 3 - Emails

When play begins:
	The first email is sent in 11 turns from now.
	
InboxFull is a number that varies. InboxFull is 0.

EmailRoom is a room.

A thing can be usedup or not usedup. A thing is usually not usedup.

Emailing is an action applying to nothing. Understand "ema" as emailing.

EmailFlipper is a number that varies. EmailFlipper is 0.

Carry out emailing:
	if the number of not usedup things in EmailRoom > 0:
		now EmailFlipper is 0;
	if EmailFlipper is 1:
		say "Accessing previous messages:[paragraph break]";
		repeat with current running through things in EmailRoom:
			say "[description of current]";
		now EmailFlipper is 0;
	otherwise if the number of not usedup things in EmailRoom is 0:
		say "You have no new emails. ";
		if the number of things in EmailRoom > 0:
			say "Type EMA again to read old messages.";
			now EmailFlipper is 1;
	otherwise:
		say "Checking unread messages:[paragraph break]";
		repeat with current running through not usedup things in EmailRoom:
			say "[description of current]";
			now current is usedup;

At the time when the first email is sent:
	now InboxFull is 1;
	now Email1 is in EmailRoom;
	say "[italic type]Dr. Law, I'm receiving an email...[roman type]

[bold type]-New command-[roman type][line break] ema - check e-mail[paragraph break]";


Email1 is a thing. The description of Email1 is "************************************************[line break]First message of [number of things in EmailRoom in words][line break]************************************************

Subject: Re: Regarding your departure[paragraph break]From: dr.s.long@stygi.corp[paragraph break]Text:[paragraph break][fixed letter spacing]Dr. Law,[paragraph break]I've considered your offer but I can't come back. I find your work highly unethical and dangerous. Who am I to interfere with the peaceful deaths of millions? Also, Dr. Ng keeps giving guns to my animatronics.

Stygicorp was founded with an excellent purpose: to bring life back to the one that you love most, Dr. Law. And I believed in that mission; but do you? It seems to me that no one thinks about that anymore; you're all caught up in power trips and one-upmanship.

 I've already accepted an offer in Montana. I wish you the best of luck with your endeavors. Please say goodbye to Ginger for me. Tell Ng I'm sorry for abandoning our project.[paragraph break]-Long [paragraph break]-quoted text-[variable letter spacing][if the number of things in EmailRoom < 2][paragraph break]I'm sorry, Dr. Law.[end if][paragraph break]";

[ [if LensBroke is 0]Perhaps it would cheer you up to conduct your final review. Please use any travel command to access a project[paragraph break][CongratulationText][otherwise][paragraph break][end if]]

To further the finalization:
	now FinalCounter is FinalCounter plus one;
	if FinalCounter is 1:
		the second email is sent in 2 turns from now;
	if FinalCounter is 2:
		the third email is sent in 2 turns from now;
	if FinalCounter is 3:
		the fourth email is sent in 1 turn from now;
	if the player is not in Home Folder:
		say "[ToDoList][line break]";

At the time when the second email is sent:
	now email2 is in EmailRoom;
	say "[italic type][if YoungFred is nowhere]Dr. Law, I'm receiving an email. [otherwise]Dr. Law, I received an email earlier, but you seemed busy, so I saved it until now. [end if]It is now available in your inbox.[roman type][paragraph break]";

Email2 is a thing. The description of Email2 is "************************************************[line break]Second message of [number of things in EmailRoom in words][line break]************************************************

Subject: Decor Choices[paragraph break]From: pirate.fred@stygi.corp[paragraph break]Text:[paragraph break][fixed letter spacing]Director Law,[paragraph break]I know that we voted on an Ancient Greek theme for the office, but I've been collecting pirate memorabilia since I was a kid, and we're doing renovations anyway after the earthquake. I'd like to meet to discuss my options, maybe over lunch. Maybe we could discuss a compromise: I'm thinking Argonauts?[paragraph break]-F[paragraph break]P.S. You must be very excited to see your old friend again today![variable letter spacing][paragraph break]"

At the time when the third email is sent:
		now email3 is in emailroom;
		say "[italic type]Dr. Law, I'm receiving an email. It is now available in your inbox.[roman type][paragraph break]";

Email3 is a thing. The description of Email3 is "************************************************[line break]Third message of [number of things in EmailRoom in words][line break]************************************************

Subject: Armed Combat[paragraph break]From: ngbot@stygi.corp[paragraph break]Text:[paragraph break][fixed letter spacing]Law,[paragraph break]I'm excited by the new weapons capabilities that I've built into Angel. I believe that we can take this program further than you've ever imagined. A military contract, perhaps with multiple states. 

Don't close the door when you see good fortune opening the gate, Law. I know you had a different, more personal purpose in mind when you started the resurrection projects. But however much you cared about Cerberus, they are only one being.[paragraph break]-Dr. Ng [paragraph break]-quoted text-[variable letter spacing][paragraph break]"

At the time when the fourth email is sent:
	say "[italic type]I've sent the following message to all staff, Dr. Law. Displaying shortly...[roman type][paragraph break]";
	wait for any key;
	say "Subject: Final meeting[paragraph break]From: DaedalOS@stygi.corp[paragraph break]Text:[paragraph break][fixed letter spacing]Dear staff members,[paragraph break]All testing is now completed. Please proceed to the command center, where Dr. Law will award one project the Golden Apple, and the chosen project will demonstrate the first official resurrection. Please be presentable.[paragraph break]-DaedalOS[variable letter spacing][paragraph break]";
	The meeting starts in 1 turn from now;

Chapter 4 - Hints

Hinting is an action out of world. Understand "hint" as hinting.

Check hinting for the first time:
	say "Dr. Law, I can provide you subtle nudges at any time, depending on your situation and location. If a hint is too vague, just type HINT again."

Carry out hinting:
	if the player is in CompFolders:
		say "[one of]Try out some of the listed commands, Dr. Law.[or]It's probably best to just go to audiovisual mode.[or]Just type AUD.[stopping]";
	if the player is in Command Center:
		if LensBroke is 1:
			say "[NextTaskHint]";
		otherwise if the puddle of red liquid is examined:
			say "[one of]That lumpy object seems quite mysterious, Dr. Law.[or]Type X LUMPY, Dr. Law.[stopping]";
		otherwise if the cubicle throne is examined:
			say "[one of]That puddle of red liquid shouldn't be here, Dr. Law.[or]Perhaps you should take a closer look, Dr. Law.[or]Type X PUDDLE, Dr. Law.[stopping]";
		otherwise if the bloody handprint is examined:
			say "[one of]Have you looked at your throne recently, Dr. Law?[or]Type X THRONE, Dr. Law.[stopping]";
		otherwise if the marble hand is examined:
			say "[one of]That bloody handprint is unusual, Dr. Law.[or]Type X HANDPRINT, Dr. Law.[stopping]";
		otherwise:
			say "[one of]That hand is odd, Dr. Law.[or]Type X HAND to take a closer look, Dr. Law.[stopping]";
	otherwise if the player is in MOIRAIArea:
		if FredOver is 1:
			say "[NextTaskHint]";
		otherwise if YoungFred is on the rejuvenator:
			if the player is not in MOIRAI Laboratory:
				say "[MoiLabDirections]";
			otherwise:
				say "[one of]You need to restore Fred to his original age, Dr. Law.[or]Do you remember the commands for the rejuvenator, Dr. Law?[or]Type NEW.[stopping]";
		otherwise if YoungFred is in Moirai Laboratory:
			if Dagei is not carried by the player:
				if the player is not in MOIRAI Laboratory:
					say "[MoiLabDirections]";
				otherwise:
					say "[one of]You need to find a way to talk to Fred about the Rejuvenator, Dr. Law.[or]You need to find a way to say 'machine', Dr. Law; there are several ways.[or]You can LINK MYT TO WO, and SAY MYTWO.[stopping]";
			otherwise if the player is not in MOIRAI Laboratory:
				say "[MoiLabDirections]";
			otherwise if VynDagei is not carried by the player:
				say "[one of]You need a command for Fred to get on the rejuvenator, Dr. Law.[or]Tell him to go to the machine, in his language.[or]LINK VYN TO DAGEI, Dr. Law.[stopping]";
			otherwise:
				say "[one of]You need to get Fred onto the rejuvenator.[or]SAY VYNDAGEI, Dr. Law.[stopping]";
		otherwise if VynLydo is carried by the player:
			if LingGym is carried by the player:
				if the sword is carried by YoungFred:
					if the player is not in MOIRAI Laboratory:
						say "[MoiLabDirections]";
					otherwise:
						say "[one of]Fred can hear you from here, Dr. Law.[or]SAY VYNLYDO, Dr. Law.[stopping]";
				otherwise:
					if the player is not in MOIRAI Living Quarters:
						say "[MoiLIvDirections]";
					otherwise:
						say "[one of]Have you tried getting Fred to go to the other room?[or]You may notice that Fred seems nervous.[or]Maybe the sword would make him feel brave.[or]SAY LINGGYM, Dr. Law.[stopping]";
			otherwise if Gym is carried by the player:
				if Ling is carried by the player:
					say "[one of]Fred seems less scared when he has his sword.[or]You should find a way to tell him to hold onto it.[or]LINK LING TO GYM, Dr. Law.[stopping]";
				otherwise:
					if the player is not in MOIRAI Living Quarters:
						say "[MoiLivDirections]";
					otherwise:
						say "[one of]Fred will need some help later on, Dr. Law. You need to find a way to tell him to grab stuff.[or]Just WAIT for a while, and he'll tell you himself.[stopping]";
			otherwise:
				if the player is not in MOIRAI Living Quarters:
					say "[MoiLivDirections]";
				otherwise if the porthole TV is unexamined:
					say "That porthole looks odd, doesn't it Dr. Law? You should examine it.";
				otherwise:
					say "[one of]You need to learn the word for sword, Dr. Law.[or]You need to type POR, Dr. Law.[stopping]";
		otherwise if Lydo is carried by the player:
			if Vyn is carried by the player:
				say "[one of]It may be helpful to have a way to get Fred to move around, Dr. Law.[or]Try to find a way to say [']come here['], Dr. Law.[or]LINK VYN TO LYDO, Dr. Law.[stopping]";
			otherwise if LALYLY is carried by the player:
				if the player is not in MOIRAI living quarters:
					say "[MoiLivDirections]";
				otherwise:
					say "[one of]You need to try new sentences on Fred, Dr. Law.[or]You can ask a question about location, Dr. Law.[or]SAY LALYLY, Dr. Law.[stopping]";
			otherwise:
				say "[one of]You need to find a way to express movement, Dr. Law.[or]Try to LINK LALY TO LY, Dr. Law.[stopping]";
		otherwise if YoungFred is in Moirai Living quarters:
			if the player is not in MOIRAI living quarters:
				say "[MoiLivDirections]";
			otherwise if LALYWO is carried by the player:
				say "[one of]You need to find a way to express location, Dr. Law.[or]You can ask a question about location, Dr. Law.[or]SAY LALYWO, Dr. Law.[stopping]";					
			otherwise if LALYLY is carried by the player:
				say "[one of]You need to find a way to express location, Dr. Law.[or]You can ask a question about location, Dr. Law.[or]SAY LALYLY, Dr. Law.[stopping]";					
			otherwise if LY is carried by the player:
				say "[one of]You need to find more sentences, Dr. Law.[or]Try to LINK LALY TO LY, Dr. Law.[stopping]";
			otherwise if MYTGOGO is carried by the player:
				say "[one of]Try saying something to Fred, Dr. Law.[or]SAY MYTGOGO, Dr. Law.[stopping]";
			otherwise:
				say "[one of]You need to find a way to communicate with Fred, Dr. Law.[or]Have you looked at your recordings?[or]LINK MYT TO GOGO, Dr. Law.[stopping]";
		otherwise if Fred is in MOIRAI Laboratory:
			if the player is not in MOIRAI Laboratory:
				say "[MoiLabDirections]";
			otherwise:
				say "You can just WAIT for a second, Dr. Law.";
		otherwise if Love is carried by the player:
			if the player is not in MOIRAI Living Quarters:
				say "[MoiLivDirections]";
			otherwise:
				say "[one of]Fred might be interested in something you have to say.[or]Tell Fred what Ginger said.[or]SAY LOVE, Dr. Law.[stopping]";
		otherwise if the rejuvenator is energized:
			if the player is not in MOIRAI Laboratory:
				say "[MoiLabDirections]";
			if ListCounter > 1:
				say "[one of]You need to finish Ginger's game, Dr. Law, to help her test the machine.[or]There are only a few patterns that Fred usually plays, so she only recognizes those.[or]The current pattern she thinks you are playing is de-aging everything that is [CorrectPattern], and aging everything else.[or]If all else fails, just keep playing. After three games, she'll stop, Dr. Law.[stopping]";
			otherwise:
				say "[one of]Try following Ginger's rules, Dr. Law.[or]Type NEW or OLD, Dr. Law. It doesn't matter which one you do.[stopping]";
		otherwise if MOIRAI Laboratory is lighted:
			if the player is not in MOIRAI Laboratory:
				say "[MoiLabDirections]";
			otherwise:
				say "[one of]You can just follow Ginger's suggestion.[or]Type ENE, Doctor Law.[stopping]";
		otherwise if the fountain is examined:
			if the player is not in MOIRAI Lobby:
				say "[one of]You need to get the lights turned on.[or]Ginger seems to be fine with the light she has left.[or]If Ginger lost her light, she might turn the other lights on.[or]You'll need to go back to the lobby to get the lights turned on.[or]Go south, Dr. Law.[stopping]";
			otherwise:
				say "[one of]You need to find some way to get the attention of the people inside, Dr. Law.[or]They might notice if something is malfunctioning.[or]Have you tried turning the fountain?[or]How far can you turn it?[or]Try typing ROT until something happens.[stopping]";
		otherwise:
			if the player is not in MOIRAI Lobby:
				say "[one of]It's too dark to do much here, Dr. Law. I suggest you return to the lobby.[or]Go south, Dr. Law.[stopping]";
			otherwise:
				say "[one of]Try looking around, Dr. Law; I detect some connected devices.[or]You can type X SOMETHING to take a closer look.[or]Try X FOUNTAIN.[stopping]";
	otherwise if the player is in HYDRAArea:
		if SinghEnabled is 2:
			say "[NextTaskHint]";
		otherwise if the player is SinghClonePC:
			let temp be 0;
			repeat with current running through physical things carried by the player:
				if current is not SinghRemote:
					now temp is temp plus the SinghPoints of current;
			if temp > 99:
				say "[one of]You have enough materials gathered, Dr. Law.[or]Use the remote, Dr. Law.[or]Make sure you are in the center of the area, and type REM.[stopping]";
			otherwise:
				say "[one of]It's not impossible, Dr. Law.[or]Dr. Singh needs to deal with some options closing off early.[or]Dr. Singh can't get the red cube after his fingertips are gone.[or]Dr. Singh can't get the gum wrapper without the stick.[or]If you can't get him out, Dr. Law, I suggest you try TAKE ALL in various locations.[or]Wait until the clone resets; go south, TAKE ALL, then go north and west and TAKE ALL.[stopping]";
		otherwise if SinghEnabled is 1:
			if the player is not in HYDRA Laboratory:
				say "[HydLabDirections]";
			otherwise:
				say "[one of]It's time for you to take control, Dr. Law.[or]Type CLO, Dr. Law.[stopping]";
		otherwise if Spiel clone is nowhere:
			if the player is not in HYDRA Laboratory:
				say "[HydLabDirections]";
			otherwise:
				say "[one of]You should make sure the machine is really off, Dr. Law.[or]You can just wait around.[or]Type Z, Dr. Law.[stopping]";
		otherwise if CloneNeedsMotivation is 1:
			if the player is not in HYDRA Laboratory:
				say "[HydLabDirections]";
			otherwise:
				say "[one of]Dr. Spiel just needs some more motivation, Dr. Law.[or]SAY REPEAT, Dr. Law.[stopping]";
		otherwise if CloneOnMound is 1:
			if the player is not in HYDRA Laboratory:
				say "[HydLabDirections]";
			otherwise:
				say "[one of]Dr. Spiel needs to find the remote, Dr. Law.[or]The remote is by the speakers, Dr. Law.[or]SAY COME, Dr. Law.[stopping]";
		otherwise if Come is carried by the player:
			if the player is not in HYDRA Laboratory:
				say "[HydLabDirections]";
			otherwise if ClonePoisoned is 1:
				say "[one of]Dr. Spiel can't listen to you like this, Dr. Law.[or]I'm afraid this clone is too far gone.[or]Just wait for a new clone.[stopping]";
			otherwise:
				say "[one of]Dr. Spiel needs to stop eating rotten things, Dr. Law.[or]SAY YUCK, Dr. Law.[stopping]";
		otherwise if ElecPower > 99:
			if the player is not in HYDRA Living Quarters:
				say "[HydLivDirections]";
			otherwise:
				say "[one of]Dr. Singh might have more to say, Dr. Law.[or]Try waiting around, Dr. Law.[or]Type Z, Dr. Law.[stopping]";
		otherwise if the cobwebs are activated:
			say "[one of]You need to turn off the animatronic spiders, Dr. Law.[or]Have you examined the spiders, Dr. Law?[or]Type WEB, Dr. Law.[stopping]";
		otherwise if the drapes are raised:
			say "[one of]The virtual windows are using up too much energy, Dr. Law.[or]Have you examined the windows, Dr. Law?[or]Type DRA, Dr. Law.[stopping]";
		otherwise if the fireplace is powered:
			say "[one of]There is something in this room using an inordinate amount of energy, Dr. Law.[or]Have you tried looking at everything?[or]Have you examined the fireplace?[or]Turn off the fireplace, Dr. Law.[or]Type TEL.[stopping]";
	otherwise if the player is in TotalMEDUSA:
		if AngelOver is 1:
			say "[NextTaskHint]";
		otherwise if the player is in Final Challenge:
			if the poker table is in Final Challenge:
				say "[one of]Make your move, Dr. Law.[or]If you're worried, play it safe: just fold.[or]Type FOL, Dr. Law.[stopping]";
			otherwise:
				say "Go DOWN, Dr. Law.";
		otherwise if the player is in Pirate Challenge:
			if the dark ramp is in Pirate Challenge:
				say "Go DOWN, Dr. Law.";
			otherwise if the pirate drone is in Pirate Challenge:
				say "[one of]You should eliminate that pirate drone first, Dr. Law.[or]Type GUN PIRATE, Dr. Law.[stopping]";
			otherwise:
				say "[one of]You need to eliminate the pirate captain, Dr. Law.[or]You can't use your flamethrower in the rain, or hammer him.[or]Why not GUN CAPTAIN?[or]It'll take a few attempts before you get him.[stopping]";
		otherwise if the player is in Wild West Challenge:
			if the dark ramp is in Wild West Challenge:
				say "Go DOWN, Dr. Law.";
			otherwise if the number of bucking things in Wild West Challenge is 0:
				say "[one of]You need to eliminate the drones, Dr. Law.[or]Now that the horses are stopped, you can attack the drones.[or]GUN DRONE, Dr. Law.[stopping]";
			otherwise:
				say "[one of]The drones are out to get you.[or]The bucking makes it hard to hit them.[or]You can attack the horses to stop the bucking.[or]Type GUN HORSE.[or]If you still have your flamethrower, you can FIR DRONE.[stopping]";
		otherwise if the player is Angel:
			if the dark ramp is in MEDUSA Arena:
				say "Go DOWN, Dr. Law.";
			otherwise:
				say "[one of]Experiment with Angel's capabilities, Dr. Law.[or]Try removing the hatch, Dr. Law.[or]Try FIR HATCH, Dr. Law.[stopping]";
		otherwise if the player is in TinyArea:
			if the player is in Angel's head:
				say "Go DOWN, Dr. Law.";
			otherwise if the player is in Airduct:
				say "Go WEST, Dr. Law.";
			otherwise if the player is in High Platform:
				if the FakeGrille is in MEDUSA Living Quarters:
					say "[one of]You can't go down, Dr. Law.[or]There's only one other way to go.[or]But the grille is in the way.[or]Have you looked at the grille?[or]The studs look quite a bit like bottle caps.[or]Type BOT STUDS, Dr. Law.[stopping]";
				otherwise:
					say "Go NORTH, Dr. Law.";
			otherwise if the recessed cube is in Northeastern Platform:
				if the player is in Southwestern Platform:
					say "[one of]That cube looked interesting, Dr. Law.[or]Go EAST, Dr. Law.[stopping]";
				otherwise if the player is in Southeastern Platform:
					say "[one of]That cube looked interesting, Dr. Law.[or]Go NORTH, Dr. Law.[stopping]";
				otherwise if the player is in Northwestern Platform:
					say "[one of]That other cube looked interesting, to the east.[or]Go EAST, Dr. Law.[stopping]";
				otherwise:
					say "[one of]That cube looks like a giant button.[or]Try pressing the button with something.[or]Due to Angel's limited capabilities, it might be best to HAM CUBE.[stopping]";
			otherwise:
				if the player is in Southwestern Platform:
					say "[one of]Try to explore a bit more, Dr. Law.[or]Go EAST, Dr. Law.[stopping]";
				otherwise if the player is in Southeastern Platform:
					say "[one of]There has to be some way off this platform, Dr. Law.[or]Go NORTH, Dr. Law.[stopping]";
				otherwise if the player is in Northwestern Platform:
					say "[one of]Try hitting the cube, Dr. Law.[or]Nothing happens when you hit the cube, Dr. Law.[or]There must be another cube. Go EAST, Dr. Law.[stopping]";
				otherwise:
					say "[one of]This dais is suspiciously familiar.[or]This is the notepad you saw on the desk earlier.[or]It must be highly flammable.[or]Type FIR DAIS, Dr. Law.[stopping]";
		otherwise if the notepad is examined:
			say "[one of]Use the command in the notepad.[or]Type WAR, Dr. Law.[stopping]";
		otherwise if MEDUSAAccessed is 1:
			if the player is not in MEDUSA Living Quarters:
				say "[MedLivDirections]";
			otherwise:
				say "[one of]Have you looked around?[or]That notepad seems interesting.[or]Type X NOTEPAD, Dr. Law.[stopping]";
		otherwise if the MEDUSA reflection is examined:
			say "[one of]Perhaps you should use the code you discovered in the reflection, Dr. Law.[or]Type MOM, Dr. Law.[stopping]";
		otherwise if the circular depression is full:
			say "[one of]Do you remember the story of Perseus and Medusa?[or]Perseus defeated Medusa by observing her reflection.[or]Have you looked at the reflection, Dr. Law?[or]Type X REFLECTION, Dr. Law.[stopping]";
		otherwise if the central sprinkler is examined:
			say "[one of]Something seems to be missing.[or]Perseus should have a shield.[or]Do you remember how to turn on the sprinkler?[or]Type SPR, Dr. Law.[stopping]";
		otherwise:
			say "[one of]Have you taken a look around, Dr. Law?[or]Have you looked at the sprinkler?[or]Type X SPRINKLER, Dr. law.[stopping]";
	otherwise if the player is in FinalCommand:
		if ShowCounter < 7:
			if a quip is carried by the player:
				say "Maybe you should SAY one of your recordings, Dr. Law.";
			otherwise:
				say "You haven't spotlighted every group, Dr. Law. You should SPO [NextProjInCaps].";
		otherwise if ABCounter < 4:
			if a quip is carried by the player:
				say "Maybe you should SAY one of your recordings, Dr. Law.";
			otherwise:
				say "Have you tried both BIE and APP, Dr. Law?";
		otherwise if Winner is carried by the player:
			say "SAY WINNER, Dr. Law.";
		otherwise if WinnerChoiceTime is 1:
			say "All you need to do is SPO one of the groups, Dr. Law.";
		otherwise if LawsCorpse is not examined:
			say "Look at yourself. No, really, look at yourself. Type X LAW.";
		otherwise:
			say "Just wait, Dr. Law. If that's even who you are.";
	otherwise if the player is in Project XYZZY:
		say "You are beyond help, now.";

To say NextProjInCaps:
	let current be a random spotable not oncespotlighted thing;
	say "[current]" in upper case;

To say NextTaskHint:
	if AngelOver is 0:
		say "Project MEDUSA has not been completely reviewed, Dr. Law. Type MED to travel there.";
	otherwise if SinghEnabled < 2:
		say "Project HYDRA has not been completely reviewed, Dr. Law. Type HYD to travel there.";
	otherwise if FredOver is 0:
		say "Project MOIRAI has not been completely reviewed, Dr. Law. Type MOI to travel there.";
	otherwise:
		say "The final selection should be commencing anytime now. Type Z."	

To say MoiLabDirections:
	say "You need to be in the MOIRAI Laboratory, to the [if the player is in MOIRAI Lobby]NORTH[otherwise]SOUTH[end if].";

To say MoiLivDirections:
	say "[one of]You need to talk to Fred [if Myt is carried by the player]more [end if]in person, first.[or]Go north to find Fred.[stopping]"

To say HydLabDirections:
	say "You need to be in the HYDRA Laboratory, to the NORTH.";

To say HydLivDirections:
	say "You need to be in the HYDRA Living Quarters, to the [if the player is in HYDRA Lobby]NORTH[otherwise]SOUTH[end if].";

To say MedLivDirections:
	say "You need to be in the MEDUSA Living Quarters, to the [if the player is in MEDUSA Lobby]NORTH[otherwise]SOUTH[end if].";

Part 5 - Deleted scenes

[This part was taken from the home folders section]

Every turn when the player is in CompFolders:
	if the current action is not taking inventory:
		try taking inventory;

["It leads to a puddle near the great Seal of Stygicorp on the floor."

The Seal of Stygicorp is in Command Center. The Seal of Stygicorp is scenery. Understand "river" as the Seal of Stygicorp. The description of the Seal of Stygicorp is "Dr. Law, you are very familiar with the seal.  It consists of 5 bands of metal. The fifth, outer band is the largest, and is a depiction in steel of the world wide web as the River Styx, with users surfing the river on laptop-shaped surfboards. Internet trolls rise to grasp the surfers and pull them under.

The inner portion is smaller, and the camera is out of focus, but the most visible object is some sort of pasture. "

The pasture is part of the Seal of Stygicorp. The description of the pasture is "Adjusting zoom to focus on the pasture. The fourth ring of the seal is engraved on one half with a silver pasture filled with IT vendors tending wild servers.  The other half has a green field full of youths and maidens holding golden phones and sending texts and friend requests in an intricate online dance. 

The inner portion is smaller, and the camera is out of focus, but the most visible object is a vineyard."

The vineyard is part of the Seal of Stygicorp. The description of the vineyard is "Focusing zoom to x10 magnification.[first time] This level of detail would not have been possible with our old cameras, Dr. Law.[only]. The third ring of the seal is smaller than the outer rings. One half is engraved with a vineyard, with surge protectors and extension cords trained on poles of silver and a firewall fencing it around made of tin. On the other half is a herd of self-driving cars made of gold and tin, shepherded by four technicians. Two judges are serving restraining orders against the self-driving cars, as the technicians watch fearfully from afar. 

The inner portion is smaller, and the camera is out of focus, but the most visible object is a city."


The city is part of the Seal of Stygicorp. The description of the city is "Magnification at 100x. The second ring of the seal is small but full of detail. There are [first time]actually [only]two cities. In one city are scientists applying for government funding. The scientists who have been funded are going about the city, escorted by torchlight and celebrated by dancing youths. Two scientists who have not been funded are quarreling, and the NSF panel is arranged in a panel around them, hearing their testimony, and holding the funding ready for the winner.[paragraph break]The other city is filled with stores of rare earths, copper, and scavenged technical parts. Two hordes of manufacturers encircle the city, divided between sacking it or accepting half of the citys's goods to depart. Meanwhile, the city's warriors have snuck out at night, and are setting up an ambush by the stock exchange, and have almost completed a hostile buyout of the manufacturers.[paragraph break]The inner portion is smaller, and the camera is out of focus, but the most visible object is the earth."

The earth is part of the Seal of Stygicorp. The description of the earth is  "Magnification at 1000x. Dr. Law, I must warn you that this is straining the capabilities of the camera. You can hear creaking sounds coming from the lens as it strains to hold the image.[paragraph break]You sea the earth, the sky, and the sea, with STYGICORP engraved over them all. [paragraph break]In the sky are constellations depicting our three main projects: Project MEDUSA, Project HYDRA, and Project MOIRAI. These constellations-".

The LensBreaking is a scene. The LensBreaking begins when the earth is examined.

When The LensBreaking begins:
	wait for any key;
	say "You hear a loud cracking sound.[paragraph break]";
	wait for any key;
	say "Oh dear. Dr. Law, it seems that the lens has snapped under the strain. I am sorry. I have ordered a new replacement, and deducted the amount from my recreational fund. I'm afraid that I will have to restrict all other cameras to one level of zoom until this issue can be fully investigated.

In the meantime, Dr. Law, we must review the three projects.[paragraph break][BreakText][CongratulationText]"
]

[For future updates: if facebook goes defunct, change the reference to it]

[Welcome E-mail is a room.

When play begins: 
	say "Thank you for volunteering for the Benevolent Society of Hackers. With the advent of the internet of things, there is more danger than ever for hostile hackers to hurt human lives. We are here to prevent that. 

It is our duty to find vulnerabilities on the web, and to neutralize them before they are discovered. 

It's time for the big question: Do you or do you not agree, under any circumstances, to refuse to neglect your service, duties, and all responsibilities to the Benevolent Society of Hackers and to the people we protect? Yes or no?";
	now the command prompt is "Please enter Yes or No: "

To decide whether collecting yesno answers: 
	if the command prompt is "Please enter Yes or No: ", yes; 
	no.

After reading a command when collecting yesno answers: 
	if the player's command exactly matches the regular expression "^y.*|1|.*yes.*|^n.*|2|.*no.*": 
		say "Wonderful! Then you have officially agreed to work for the Benevolent Society of Hackers. You will now be assigned your first task. This week we have several potential projects: 

1) An ice machine that has been hacked by Anonymous. The owner has been forced to start a lemonade stand to use all of the ice. 
2) A garage door being controlled by the Russians. The family believes that the house is being haunted by the ghost of a vintage car collector.
3) An unsecured laptop at a nuclear-enabled facility.  Initial data suggests that the facility is wired to explode within the next few hours."
		now the command prompt is "Please enter a number from 1 to 3: "
		reject the player's command;
	otherwise: 
		reject the player's command; 
"We've already assigned our best operatives to deal with Anonymous and the Russians. We'd like you to be responsible for the nuclear job. Good luck!

Remember, we work outside of the law. We are unseen and unknown. You are solely responsible for any legal repercussions from the use of our tools, up to and including fines, imprisonment, death or dismemberment.

Press any key to proceed.";
	wait for any key;
	say "[line break]Connecting...

Uploading HACKER.dat... done[line break]
5 files copied

Searching for access...

**Device identified:** [line break]
Type: Personal laptop[line break]
Registration name: drlaw79[line break]
License type: Commercial[line break]
Password: successfully extracted

To log in to this laptop remotely with these credentials, press any key.";
	now the command prompt is ">";]
	
